
//////////////
// PnP Fiends
//////////////


INCLUDE "atweaks/lib/c150_funs.tpa"
INCLUDE "atweaks/lib/dsandstuff.tpa"
INCLUDE "atweaks/lib/rr#fiend.tph"

// Imp

ACTION_FOR_EACH ~file~ IN
              ~DIMP01~                                                             // Imp
             ~GORBAT4~                                                             // Imp
               ~IMP01~                                                             // Imp
             ~TELIMP1~                                                             // Imp
             ~RUMAR03~                                                             // Imp (Ranger stronghold quest)
            ~MEKIMP01~                                                             // Imp (Mekrath's stolen mirror quest)
BEGIN                                                                              // execute the following
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN                                   // if the file exists
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF make_imp END                                                         
    BUT_ONLY_IF_IT_CHANGES
  END                                                                                // ends ACTION_IF FILE_EXISTS_IN_GAME block
END                                                                                // ends ACTION_FOR_EACH block
ACTION_IF FiendsOriginal = 0 BEGIN
  ACTION_FOR_EACH file IN
               ~TCIMP01~                                                           // Imp (Classic Adventures)
  BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
      COPY_EXISTING "%file%.cre" override
        LPF make_imp INT_VAR enforce = 1 END
      BUT_ONLY
    END
  END
END


// Hellcat

ACTION_FOR_EACH ~file~ IN
              ~GORCAMB6~                                                           // Hellcat
              ~GORCAMB7~                                                           // Hellcat
               ~HGFEL01~                                                           // Hellcat
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF make_hellcat END
    BUT_ONLY_IF_IT_CHANGES
  END
END

// Special case, inlined script addition for Aesgareth's Hellcats (Watcher's Keep Cambion)

<<<<<<<<aTweaks/inlined/rr#fcatx.baf
IF
        Allegiance(Myself,EVILCUTOFF)
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck(Myself,STATE_INVISIBLE)
	!Global("dvgldust","LOCALS",1)
	!CheckStatGT(Player1,0,TRUE_SIGHT)
	!CheckStatGT(Player2,0,TRUE_SIGHT)
	!CheckStatGT(Player3,0,TRUE_SIGHT)
	!CheckStatGT(Player4,0,TRUE_SIGHT)
	!CheckStatGT(Player5,0,TRUE_SIGHT)
	!CheckStatGT(Player6,0,TRUE_SIGHT)
	!CheckStatGT([GOODCUTOFF.0.PLANATAR],0,TRUE_SIGHT)
	!CheckStatGT([GOODCUTOFF.0.DARKPLANATAR],0,TRUE_SIGHT)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ApplySpellRES("RR#WI405",Myself) // Improved Invisibility
END
>>>>>>>>

EXTEND_TOP ~GORCAMB6.BCS~  ~aTweaks/inlined/rr#fcatx.baf~


// Erinyes

ACTION_FOR_EACH ~file~ IN
              ~DERINY01~                                                           // Erinyes
               ~GORBAT2~                                                           // Erinyes
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF make_erinyes END
    BUT_ONLY_IF_IT_CHANGES
  END
END


// Bone Fiend

ACTION_FOR_EACH ~file~ IN
              ~DBONEF01~                                                           // Bone Fiend
               ~GORBAT5~                                                           // Bone Fiend
              ~MELSUM02~                                                           // Bone Fiend
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF make_bone_fiend END
    BUT_ONLY_IF_IT_CHANGES
  END
END


// Abishai (Red)

ACTION_FOR_EACH ~file~ IN
              ~ABISRED1~                                                           // Abishai
              ~DEMABI01~                                                           // Abishai
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF make_abishai END
    BUT_ONLY_IF_IT_CHANGES
  END
END


// Cornugon

ACTION_FOR_EACH ~file~ IN
              ~DEMCOR01~                                                           // Cornugon
              ~DEADDEM1~                                                           // Cornugon
               ~GORBAT3~                                                           // Cornugon
               ~TELCOR1~                                                           // Cornugon
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF make_cornugon END
    BUT_ONLY_IF_IT_CHANGES
  END
END


// Pit Fiend

ACTION_FOR_EACH ~file~ IN
                ~DEMPIT~                                                           // Pit Fiend
              ~DEMPIT01~                                                           // Pit Fiend
               ~TELPIT1~                                                           // Pit Fiend
               ~TELPIT2~                                                           // Pit Fiend
               ~GORBAT1~                                                           // Ka'rashur (Watcher's Keep Baatezu leader)
               dempitsu                                                            //Summoned Pit Fiend  
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF make_pit_fiend END
    BUT_ONLY_IF_IT_CHANGES
  END
END

ACTION_IF FiendsOriginal = 0 BEGIN
  ACTION_FOR_EACH file IN
                  dw#bazpf                                                         //SCSII Pit Fiend at Abazigal's
  BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
      COPY_EXISTING "%file%.cre" override
        LPF make_pit_fiend INT_VAR enforce = 1 END
      BUT_ONLY
    END
  END
END



// Quasit

ACTION_FOR_EACH ~file~ IN
             ~DEADDEM2~                                                            // Quasit
              ~DQUAS01~                                                            // Quasit
              ~GORTAN5~                                                            // Quasit
             ~IMPQUA01~                                                            // Quasit
              ~TELQUA1~                                                            // Quasit
              ~TELQUA2~                                                            // Quasit
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF make_quasit END
    BUT_ONLY_IF_IT_CHANGES
  END
END


// Cambion

ACTION_FOR_EACH ~file~ IN
              ~CAMBION1~                                                           // Cambion
              ~CAMBION2~                                                           // Cambion
              ~IDEMON01~                                                           // Cambion (trapped, non-interactable)
              ~IDEMON02~                                                           // Cambion
               ~ILLCAMB~                                                           // Cambion (illusion?)
              ~ILLCAMB2~                                                           // Cambion (illusion?)
              ~KDEMON01~                                                           // Cambion (illusion?)
              ~DEMOSUM4~                                                           // Cambion
               ~TELCAM1~                                                           // Cambion
               ~GORCAMB~                                                           // Aesgareth (Watcher's Keep Cambion)
               ~PWARDEN~                                                           // Warden (Planar Prison Cambion)
BEGIN                                                                              // execute the following
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN                                   // if the file exists
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF make_cambion END
    BUT_ONLY_IF_IT_CHANGES
  END                                                                                // ends ACTION_IF FILE_EXISTS_IN_GAME block
END                                                                                // ends ACTION_FOR_EACH block
ACTION_IF FiendsOriginal = 0 BEGIN
  ACTION_FOR_EACH file IN
               ~TCCAMBI1~                                                          // Cambion (Classic Adventures)
               ~TCCAMBI2~                                                          // Cambion (Classic Adventures)
  BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
      COPY_EXISTING "%file%.cre" override
        LPF make_cambion INT_VAR enforce = 1 END
      BUT_ONLY
    END
  END
END


// Alu-Fiend

ACTION_FOR_EACH ~file~ IN
              ~ALUFIE01~                                                           // Alu-Fiend
              ~ALUFIE02~                                                           // Alu-Fiend
              ~GORALU01~                                                           // Alu-Fiend
               ~TELALU1~                                                           // Alu-Fiend
               ~TELSUC1~                                                           // Alu-Fiend
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF make_alu-fiend END
    BUT_ONLY_IF_IT_CHANGES
  END
END


// Maurezhi

ACTION_FOR_EACH ~file~ IN
              ~DEMMAU01~                                                           // Maurezhi
               ~GORTAN6~                                                           // Maurezhi (Watcher's Keep Tanar'ri group)
              ~OBSDEM04~                                                           // Maurezhi (Planar Sphere)
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF make_maurezhi END
    BUT_ONLY_IF_IT_CHANGES
  END
END


// Succubus

ACTION_FOR_EACH ~file~ IN
              ~DEMSUC01~                                                           // Succubus
              ~GORSUC01~                                                           // Succubus
               ~GORTAN2~                                                           // Succubus
              ~GORWOM01~                                                           // Nalmissra
                ~KIRINH~                                                           // Kirinhale, the Succubus in Durlag's Tower (BGT)
               ~_KIRINH~                                                           // Kirinhale, the Succubus in Durlag's Tower (Tutu)
              ~x#amelia~                                                           // Amelia, the Succubus added by BG1 NPCs during Coran's quest
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF make_succubus END
    BUT_ONLY_IF_IT_CHANGES
  END
END


// Replace one generic Tanar'ri with a Nabassu inside the Planar Sphere

COPY_EXISTING ~AR0414.ARE~ ~override~                                              // Planar Sphere, Abyssal Plane
  READ_LONG  0x54 "actor_off"
  READ_SHORT 0x58 "actor_num"
  WHILE ("%actor_num%" > 0) BEGIN
    SET "actor_num" = ("%actor_num%" - 1)
    READ_SHORT ("%actor_off%" + 0x20 + ("%actor_num%" * 0x110)) "x_coord"
    READ_SHORT ("%actor_off%" + 0x22 + ("%actor_num%" * 0x110)) "y_coord"
    READ_ASCII ("%actor_off%" + 0x80 + ("%actor_num%" * 0x110)) "cre_file"
    PATCH_IF (("%cre_file%" STRING_EQUAL_CASE "ABYDEM01") AND ("%x_coord%" = 2155)) BEGIN
      WRITE_ASCII ("%actor_off%" +        ("%actor_num%" * 0x110)) ~Nabassu~ #32
      WRITE_ASCII ("%actor_off%" + 0x80 + ("%actor_num%" * 0x110)) ~OBSDEM05~ #8
    END
  END
BUT_ONLY_IF_IT_CHANGES


// Special case, the restored Planar Sphere Nabassu needs a heart and an allegiance change

COPY_EXISTING ~obsdem05.cre~ ~override~                                            // Restored Planar Sphere Nabassu
  ADD_CRE_ITEM "MISC6M" #0 #0 #0 none inv1                                         // Demon Heart
  WRITE_BYTE  0x270 "255"                                                          // set initial allegiance to ENEMY
BUT_ONLY_IF_IT_CHANGES


// Nabassu

ACTION_FOR_EACH ~file~ IN
              ~DEMNAB01~                                                           // Nabassu
              ~DEMNAB02~                                                           // Nabassu
//            ~ENDDEM04~                                                           // Nabassu (unused?)
//            ~ENDDEM05~                                                           // Nabassu (Planar Sphere)
              ~OBSDEM05~                                                           // Nabassu
//             ~PPNAB01~                                                           // Nabassu (cutscene only?)
//             ~PPNAB02~                                                           // Nabassu (cutscene only?)
//             ~PPNAB03~                                                           // Nabassu
               ~PMASTER~                                                           // Master of Thralls (named Nabassu within the Planar Prison)
               ~RUMAR02~                                                           // Nabassu
                ~UDNABA~                                                           // Nabassu
              ~TANARI01~                                                           // Tanar'ri (Nabassu within the Planar Sphere)
                 ~TANAR~                                                           // Aec'Letec (BGT)
                ~_TANAR~                                                           // Aec'Letec (Tutu)
                demnabsu                                                           //Summoned Nabassu  
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF make_nabassu END
    BUT_ONLY_IF_IT_CHANGES
  END
END

ACTION_IF FiendsOriginal = 0 BEGIN
  ACTION_FOR_EACH file IN
                ~dw#nabsu~                                                         // SCSII Nabassu
  BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
      COPY_EXISTING "%file%.cre" override
        LPF make_nabassu INT_VAR enforce = 1 END
      BUT_ONLY
    END
  END
END


// Glabrezu

ACTION_FOR_EACH ~file~ IN
              ~ABYDEM01~                                                           // Tanar'ri (Planar Sphere, becomes a Glabrezu through aTweaks)
              ~DEMGLA01~                                                           // Glabrezu
               ~DEMGLAB~                                                           // Glabrezu
              ~DEMGLAB2~                                                           // Glabrezu
              ~DEMOSUM3~                                                           // Glabrezu
               ~DGLAB01~                                                           // Glabrezu
//            ~ENDDEM01~                                                           // Glabrezu (unused?)
//            ~ENDDEM02~                                                           // Glabrezu (unused?)
//            ~ENDDEM02~                                                           // Glabrezu (unused?)
               ~GORTAN4~                                                           // Glabrezu
              ~JONDEM01~                                                           // Glabrezu
              ~JONDEM03~                                                           // Glabrezu
              ~JONDEM05~                                                           // Glabrezu
              ~MELSUM01~                                                           // Glabrezu
               ~TELTAN1~                                                           // Glabrezu
               ~TELTAN2~                                                           // Glabrezu
               demglasu                                                            //Summoned Glabrezu
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF make_glabrezu END
    BUT_ONLY_IF_IT_CHANGES
  END
END

ACTION_IF FiendsOriginal = 0 BEGIN
  ACTION_FOR_EACH file IN
                  dw#drxg1                                                         //SCSII Glabrezu by Underdark exit
                  dw#drxg2                                                         //SCSII Glabrezu by Underdark exit
                  dw#sengl                                                         //SCSII Glabrezu at Sendai's
  BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
      COPY_EXISTING "%file%.cre" override
        LPF make_glabrezu INT_VAR enforce = 1 END
      BUT_ONLY
    END
  END
END


// Marilith

ACTION_FOR_EACH ~file~ IN
              ~DEMOSUM1~                                                           // Marilith
              ~ICMARILI~                                                           // Marilith (no animation)
              ~MELSUM05~                                                           // Marilith
              ~GORWOM03~                                                           // Y'tossi (Watcher's Keep final seal Huntress' party)
              ~OBSDEM01~                                                           // Lea'liyl (named Marilith within the Planar Sphere)
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF make_marilith END
    BUT_ONLY_IF_IT_CHANGES
  END
END

ACTION_IF FiendsOriginal = 0 BEGIN
  ACTION_FOR_EACH file IN
                  d0qpwima                                                         //Questpack's Infernal Thievery Marilith
  BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
      COPY_EXISTING "%file%.cre" override
        LPF make_marilith END
      BUT_ONLY
    END
  END
END



// Balor

ACTION_FOR_EACH ~file~ IN
               ~BALOR01~                                                           // Balor
              ~DEMBAL01~                                                           // Balor
              ~DEMBAL02~                                                           // Balor
              ~DEMOSUM2~                                                           // Balor
//            ~ENDDEM03~                                                           // Balor (unused?)
                ~GORDEM~                                                           // Balor (mismatched animation fixed)
              ~GORDEMC1~                                                           // Balor (cutscene only)
              ~JONDEM02~                                                           // Balor (mismatched animation fixed)
              ~JONDEM04~                                                           // Balor (mismatched animation fixed)
              ~M05DEMON~                                                           // Balor (cutscene only)
              ~MELSUM04~                                                           // Balor
               ~SUDEMON~                                                           // Balor
              ~SUDEMON2~                                                           // Balor
              ~SUDEMDE1~                                                           // Balor
              ~SUDEMDE2~                                                           // Balor
               ~SUDEMW1~                                                           // Balor
               ~TELBAL1~                                                           // Balor
               ~TELBAL2~                                                           // Balor
               ~UDBALOR~                                                           // Balor
               ~GORTAN1~                                                           // Tahazzar (Watcher's Keep Tanar'ri leader)
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN
    COPY_EXISTING ~%file%.cre~ ~override~
      LPF make_balor END
    BUT_ONLY_IF_IT_CHANGES
  END
END



// Gelugon
   

COPY ~atweaks/cre/RR#SGELU.CRE~ ~override/RR#HGEL.CRE~                             // Gelugon (hostile)
LAUNCH_PATCH_FUNCTION cold_spell_immunities END                                    // grant immunities to cold-based area effect spells
LAUNCH_PATCH_FUNCTION poison_spell_immunities END                                  // grant immunities to poison-based area effect spells
  PATCH_IF FiendsScripts BEGIN                                                       //If we are using existing values for stats, copy HP from Glabrezus
    INNER_ACTION BEGIN
      COPY_EXISTING demglasu.cre override
        READ_SHORT 0x26 newhp
      BUT_ONLY
    END
    LPF RR#MDCRE
      INT_VAR
        newhp
    END
  END
  SAY NAME1 @670 SAY NAME2 @670
  WRITE_LONG 0x10 0                                                                // clear flags
  WRITE_LONG  0x14 "19000"                                                         // XP value
  WRITE_ASCII 0x248 ~RR#HGELU~ #8                                                  // override AI script
  WRITE_BYTE  0x270 "255"                                                          // set initial allegiance to ENEMY
  WRITE_BYTE  0x275 1                                                              //gender: male
  WRITE_ASCII 0x280 ~RR#HGELU~ #32                                                 // assign new death variable

COPY_EXISTING ~AR3004.ARE~ ~override~                                              // Watcher's Keep maze
  REPLACE_TEXTUALLY CASE_INSENSITIVE "GORDEM2" "RR#HGEL"                           // replace Velithuu with Gelugon (note: name lengths must be the same)
BUT_ONLY_IF_IT_CHANGES


////////////////////////////////////////
// Yugoloth
////////////////////////////////////////

//Ultroloth
COPY "atweaks/cre/fiendbase.cre" "override/fl#sultr.cre"
  LPF make_ultroloth END

//Nycaloth
COPY "atweaks/cre/fiendbase.cre" "override/fl#snyca.cre"
  LPF make_nycaloth END

//Arcanaloth
COPY "atweaks/cre/fiendbase.cre" "override/fl#sarca.cre"
  LPF make_arcanaloth END


////////////////////////////////////////
// Nonaligned
////////////////////////////////////////

//Shadow Fiend
ACTION_FOR_EACH file IN
                shadfi01 //regular
                sdshadfi //summoned in shadow temple
                sewsha01 //Saradush sewers
                sewsha02 //ditto
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      LPF make_shadow_fiend END
      LPF RR#MDCSC
        STR_VAR
          newscript = fl#shafi
          script01 = wtasight
          script02 = dw#melee
          script03 = bpmag14m
          script04 = bpwtsigt
          script05 = bpshadow
      END
      LPF extraplanar_traits END
    BUT_ONLY
  END
END

//Devil Shade (Shadow Fiend on steroids)
ACTION_FOR_EACH file IN
                hgwra02 //Master Wraith's pal
                hgwra03 //ditto
                shadfi02 //regular
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      LPF fire_spell_immunities END
      LPF cold_spell_immunities END
      LPF electrical_spell_immunities END
      LPF RR#MDCRE
        INT_VAR
          newlvl1 = 20
          newer = 100
      END
      LPF RR#MDCSC
        STR_VAR
          newscript = fl#shaf2
          script01 = shadfi02
          script02 = wtasight
          script03 = dw#melee
          script04 = bpmag14m
          script05 = bpwtsigt
      END
      LPF extraplanar_traits END
    BUT_ONLY
  END
END


//////////////////////////////////////////////////////////////////////
// New dialogues
//////////////////////////////////////////////////////////////////////

// revised Planar Sphere Nabassu dialogue (adds mephit/quasit spawning)
COMPILE ~atweaks/dlg/obsdem05.d~

// Revised Planar Sphere Tanar'ri dialogue (removes mephit/quasit spawning)
COPY_EXISTING abydem01.dlg override
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("imp01",[1217.1897],2)~ ~~
    REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("imp01",[1030.2269],9)~ ~~
    REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("imp01",[932.1837],0)~ ~~
    REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("impqua01",[780.1918],15)~ ~~
    REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("impqua01",[1289.2230],7)~ ~~
    REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("mepfir01",[1179.2257],7)~ ~~
    REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("mepfir01",[1345.2081],4)~ ~~
  END
BUT_ONLY

// Make the Planar Sphere demons' dialogue files pause the game
COPY_EXISTING abydem01.dlg override
              obsdem01.dlg override
              obsdem05.dlg override
  WRITE_LONG 0x30 0
BUT_ONLY

// Special case, inlined script change for the new Nabassu in the Planar Sphere quest
<<<<<<<< .../atweaks/inlined/obsdem05.baf
IF
	Global("RR#Intd","LOCALS",0)
	NumTimesTalkedTo(0)
        See([PC])
THEN
	RESPONSE #100
		SetGlobal("RR#Intd","LOCALS",1)
		FaceObject([PC])
		StartDialogueNoSet([PC])
END
>>>>>>>>
COMPILE ~.../atweaks/inlined/obsdem05.baf~


////////////////////////////////////////////////////////////////////////////////
// New AI Scripts
////////////////////////////////////////////////////////////////////////////////

COMPILE ~atweaks/baf/fiends/rr#hquas.baf~ // Quasit
        ~atweaks/baf/fiends/rr#haluf.baf~ // Alu-Fiend
        ~atweaks/baf/fiends/rr#hcamb.baf~ // Cambion
        ~atweaks/baf/fiends/rr#hmaur.baf~ // Maurezhi
        ~atweaks/baf/fiends/rr#hsucc.baf~ // Succubus
        ~atweaks/baf/fiends/rr#hnaba.baf~ // Nabassu
        ~atweaks/baf/fiends/rr#hglab.baf~ // Glabrezu
        ~atweaks/baf/fiends/rr#hmari.baf~ // Marilith
        ~atweaks/baf/fiends/rr#hbalr.baf~ // Balor
        ~atweaks/baf/fiends/rr#himp.baf~  // Imp
        ~atweaks/baf/fiends/rr#hfcat.baf~ // Hellcat
        ~atweaks/baf/fiends/rr#herin.baf~ // Erinyes
        ~atweaks/baf/fiends/rr#hbfnd.baf~ // Bone Fiend
        ~atweaks/baf/fiends/rr#habis.baf~ // Abishai
        ~atweaks/baf/fiends/rr#hcorn.baf~ // Cornugon
        ~atweaks/baf/fiends/rr#hgelu.baf~ // Gelugon
        ~atweaks/baf/fiends/rr#hpitf.baf~ // Pit Fiend
        ~atweaks/baf/fiends/rr#karas.baf~ // Ka'rashur's reinforcement summoning script
        ~atweaks/baf/fiends/rr#tahaz.baf~ // Tahazzar's reinforcement summoning script
        ~atweaks/baf/fiends/fl#shafi.baf~ // Shadow Fiend
        ~atweaks/baf/fiends/fl#shaf2.baf~ // Devil Shade

// Make Ka'rashur use aTweaks' Pit Fiend script once he goes hostile
EXTEND_TOP karash.bcs ~atweaks/baf/fiends/karash.baf~
// Make Tahazzar use aTweaks' Balor script once he goes hostile
EXTEND_TOP tahazz.bcs ~atweaks/baf/fiends/tahazz.baf~

// Fix the Glabrezu spawning in the Guarded Compound
COPY_EXISTING kettaatk.bcs override
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY EXACT_MATCH
      ~ForceSpell(LastTrigger(Myself),WIZARD_SUMMON_FIEND)~
      ~CreateCreatureObject("DEMGLA01",LastTrigger(Myself),0,0,0)~
    REPLACE_TEXTUALLY EXACT_MATCH
      ~ForceSpellRES("dw#sumgl",LastTrigger(Myself))~
      ~CreateCreatureObject("DEMGLA01",LastTrigger(Myself),0,0,0)~ // SCSII
  END
BUT_ONLY

// Succubus Kiss cutscene in Watcher's Keep maze
COPY_EXISTING cut215a.bcs override
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY EXACT_MATCH
      ~ActionOverride("gorsuc01",ForceSpell(Player1,PHASE_SPIDER_TELEPORT))~
      ~ActionOverride("gorsuc01",ForceSpellRES("RR#TELWE",Player1))~ // Teleport Without Error
    REPLACE_TEXTUALLY EXACT_MATCH
      ~ActionOverride("gorsuc01",ForceSpell(Player1,SUCCUBUS_KISS))~
      ~ActionOverride("gorsuc01",ForceSpellRES("RR#SKISS",Player1))~ // Kiss
  END
BUT_ONLY

// Watcher's Keep Succubus escape script
COPY_EXISTING gorsuccu.bcs override
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY EXACT_MATCH
      ~ForceSpell(Myself,DRYAD_TELEPORT)~
      ~ForceSpellRES("RR#TELAW",Myself)~ // Teleport away
  END
BUT_ONLY

// Prevent annoying perma-invisibility
COPY_EXISTING telqua1.bcs override // Watcher's Keep Quasit script (Wild Magic room)
              telqua2.bcs override // Watcher's Keep Quasit script (Wild Magic room)
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY EXACT_MATCH
      ~ApplySpell(Myself,WIZARD_INVISIBILITY)~
      ~SpellNoDec(Myself,WIZARD_INVISIBILITY)~
  END
BUT_ONLY

// Restore Lea'liyl's original dialogue
COPY_EXISTING obsdem01.bcs override
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY EXACT_MATCH
      ~Detect([PC])~ ~NumTimesTalkedTo(0) Detect([PC])~
    REPLACE_TEXTUALLY EXACT_MATCH
      ~SetGlobal("summon","LOCALS",1)~
      ~SetGlobal("summon","LOCALS",1) FaceObject([PC]) StartDialogueNoSet([PC])~
  END
BUT_ONLY

// Add another Shadow Fiend to the playhouse encounter, because the new
// Shadow Fiends are weak-sauce compared to Bioware's creative take on them
COPY_EXISTING ar0510.bcs override
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY
      ~\(CreateVisualEffect("SPSTRENH",\[915\.463\])\)~
      ~\1 CreateVisualEffect("SPSTRENH",[893.482])~
    REPLACE_TEXTUALLY
      ~\(CreateCreature("SHADFI01",\[915\.463\],0)\)~
      ~\1 CreateCreature("SHADFI01",[893.482],0)~
  END
BUT_ONLY

// The Pit Fiend summoned through the portal in the maze below Spellhold
// should not be attacked before the gating animation is complete
COPY_EXISTING ppportal.bcs override
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY EXACT_MATCH
      ~ReallyForceSpell(Myself,SUMMON_PIT_FIEND)~
      ~SetInterrupt(FALSE) CreateVisualEffect("SPPORTAL",[245.500])
       Wait(1) CreateCreature("DEMPIT01",[245.500],0) SetInterrupt(TRUE)~
  END
BUT_ONLY


//////////////////////////////////////////////////////////////////////
// Creature changes, summons
//////////////////////////////////////////////////////////////////////

OUTER_SPRINT $sscript(demnabsu) rr#snaba
OUTER_SPRINT $sscript(demglasu) rr#sglab
OUTER_SPRINT $sscript(dempitsu) rr#spitf

COPY_EXISTING demnabsu.cre ~override/demnabsu.cre~
              telcor1.cre  ~override/rr#scorn.cre~
              demglasu.cre ~override/demglasu.cre~
              rr#hgel.cre  ~override/rr#sgelu.cre~
              dempitsu.cre ~override/dempitsu.cre~
              telbal1.cre  ~override/rr#sbalr.cre~
              fl#sultr.cre ~override/fl#sultr.cre~
              fl#snyca.cre ~override/fl#snyca.cre~
              fl#sarca.cre ~override/fl#sarca.cre~
  WRITE_LONG 0x10 2 // Clear flags, leave only "no corpse"
  WRITE_LONG 0x14 0 // XP is handled by script
  PATCH_IF VARIABLE_IS_SET $sscript("%DEST_RES%") BEGIN
    WRITE_ASCIIE 0x248 $sscript("%DEST_RES%") #8 // Summoned script
  END ELSE BEGIN
    WRITE_ASCIIE 0x248 "%DEST_RES%" #8
  END
  WRITE_ASCII  0x250 ~~ #8 // Class
  WRITE_ASCII  0x258 ~~ #8 // Race
  WRITE_ASCII  0x260 ~~ #8 // General
  WRITE_ASCII  0x268 ~~ #8 // Default
  WRITE_BYTE   0x270 128 // Initially neutral
  WRITE_BYTE   0x275 9 // SUMMONED_DEMON
  WRITE_ASCIIE 0x280 ~%DEST_RES%~ #32 // Script name
  // Make all items unstealable&undroppable
  READ_LONG 0x2bc io
  FOR (i=0;i<LONG_AT 0x2c0;++i) BEGIN
    WRITE_BYTE io + 0x14*i + 0x10 THIS BOR 10
  END
  PATCH_IF FILE_EXISTS_IN_GAME dw#sumfi.itm AND
           !FILE_CONTAINS_EVALUATED ("%SOURCE_FILE%" "dw#sumfi")
  BEGIN
    ADD_CRE_ITEM dw#sumfi #0 #0 #0 none boots
  END


//////////////////////////////////////////////////////////////////////
// Spell changes, summons
//////////////////////////////////////////////////////////////////////

COPY_EXISTING spcaco.eff override // Summon Cacofiend
              spfiend.eff override // Summon Fiend
              spgate.eff override // Gate
  WRITE_LONG  0x18 0 // Power (in case the spell is cast directly on a Rakshasa or something)
BUT_ONLY

////////////////////////////////////////
//Cacofiend
////////////////////////////////////////

COPY_EXISTING spwi707.spl override // Cacofiend
  SAY NAME1 #17320 SAY NAME2 #17320
  SAY DESC @751 SAY UNIDENTIFIED_DESC @751
  WRITE_SHORT 0x1e BIT8 // Exclude diviners
  WRITE_SHORT 0x22 14 // Casting animation: conj
  WRITE_SHORT 0x25 2 // School: conj
  GET_OFFSET_ARRAY aa 0x64 4 0x68 2 0 0 0x28
  PHP_EACH aa AS i => off BEGIN
    WRITE_SHORT off + 0x0e 30 // Range
    WRITE_SHORT off + 0x12 7 // Casting time
  END
  LPF fiend_summoning_spell
    INT_VAR
      duration = 120
    STR_VAR
      lawful = rr#scorn
      lcneutral = fl#sarca
      chaotic = spcaco
  END
BUT_ONLY

COPY_EXISTING spcaco.eff override // Summon Nabassu
  WRITE_ASCII 0x30 DEMNABSU #8 // Resref
BUT_ONLY

COPY_EXISTING spcaco.eff ~override/rr#scorn.eff~ // Summon Cornugon
              spcaco.eff ~override/fl#sarca.eff~ // Summon Arcanaloth
  WRITE_ASCIIE 0x30 "%DEST_RES%" #8 // Resref
BUT_ONLY

////////////////////////////////////////
// Summon Fiend
////////////////////////////////////////

COPY_EXISTING spwi807.spl override // Summon Fiend
  SAY NAME1 #17360 SAY NAME2 #17360
  SAY DESC @752 SAY UNIDENTIFIED_DESC @752
  GET_OFFSET_ARRAY aa 0x64 4 0x68 2 0 0 0x28
  PHP_EACH aa AS i => off BEGIN
    WRITE_SHORT off + 0x0e 30 // Range
    WRITE_SHORT off + 0x12 8 // Casting time
  END
  LPF fiend_summoning_spell
    INT_VAR
      duration = 198
    STR_VAR
      lawful = rr#sgelu
      lcneutral = fl#snyca
      chaotic = spfiend
  END
BUT_ONLY

COPY_EXISTING spfiend.eff override // Summon Glabrezu
  WRITE_ASCII 0x30 DEMGLASU #8 // Resref

COPY_EXISTING spfiend.eff ~override/rr#sgelu.eff~ // Summon Gelugon
              spfiend.eff ~override/fl#snyca.eff~ // Summon Nycaloth
  WRITE_ASCIIE 0x30 "%DEST_RES%" #8 // Resref
BUT_ONLY

////////////////////////////////////////
// Gate
////////////////////////////////////////

COPY_EXISTING spwi905.spl override // Gate (Wizard)
  SAY NAME1 #14260 SAY NAME2 #14260
  SAY DESC @753 SAY UNIDENTIFIED_DESC @753
  WRITE_ASCII 0x3a fl#905c #8 // New spellbook icon
  GET_OFFSET_ARRAY aa 0x64 4 0x68 2 0 0 0x28
  PHP_EACH aa AS i => off BEGIN
    WRITE_ASCII off + 0x4 fl#905b #8 // New icon (stone background)
    WRITE_SHORT off + 0x0e 30 // Range
    WRITE_SHORT off + 0x12 9 // Casting time
  END
  LPF fiend_summoning_spell
    INT_VAR
      duration = 396
    STR_VAR
      lawful = spgate
      lcneutral = fl#sultr
      chaotic = rr#sbalr
  END
BUT_ONLY

COPY_EXISTING sppr703.spl override // Gate (Cleric)
  SAY NAME1 #14260 SAY NAME2 #14260
  SAY DESC @754 SAY UNIDENTIFIED_DESC @754
  WRITE_ASCII 0x3a fl#905c #8 // New spellbook icon
  WRITE_ASCII 0x10 CAS_P03 #8 // Casting sound
  WRITE_SHORT 0x25 2 // Spell school (Conjuration)
  WRITE_SHORT 0x22 14 // Casting graphics (Conjuration)
  GET_OFFSET_ARRAY aa 0x64 4 0x68 2 0 0 0x28
  PHP_EACH aa AS i => off BEGIN
    WRITE_ASCII off + 0x4 fl#905b #8 // New icon (stone background)
    WRITE_SHORT off + 0x0e 30 // Range
    WRITE_SHORT off + 0x12 5 // Casting time
  END
  LPF fiend_summoning_spell
    INT_VAR
      duration = 396
    STR_VAR
      lawful = spgate
      lcneutral = fl#sultr
      chaotic = rr#sbalr
  END
BUT_ONLY

COPY_EXISTING spgate.eff override // Summon Pit Fiend
  WRITE_ASCII 0x30 DEMPITSU #8 // Resref
BUT_ONLY

COPY_EXISTING spgate.eff ~override/rr#sbalr.eff~ // Summon Balor
              spgate.eff ~override/fl#sultr.eff~ // Summon Ultroloth
  WRITE_ASCIIE 0x30 "%DEST_RES%" #8 // Resref
BUT_ONLY

////////////////////////////////////////
// Scroll changes
////////////////////////////////////////

COPY_EXISTING scrl8i.itm override // Cacofiend scroll
  SAY NAME1 #17320 SAY NAME2 #17320
  SAY DESC @751 SAY UNIDENTIFIED_DESC @751
  WRITE_BYTE 0x2d THIS BAND BNOT BIT2 // Remove illusionist flag
  WRITE_BYTE 0x2d THIS BOR BIT0 // Add diviner flag
BUT_ONLY

COPY_EXISTING scrl9b.itm override // Summon Fiend scroll
  SAY NAME1 #17360 SAY NAME2 #17360
  SAY DESC @752 SAY UNIDENTIFIED_DESC @752
BUT_ONLY

COPY_EXISTING scrl9n.itm override // Gate scroll
  WRITE_ASCII 0x3a fl#905a #8 // New scroll icon
  SAY NAME1 #14260 SAY NAME2 #14260
  SAY DESC @753 SAY UNIDENTIFIED_DESC @753
  GET_OFFSET_ARRAY aa ITM_V10_HEADERS
  PHP_EACH aa AS i => off BEGIN
    WRITE_ASCII off + 0x4 fl#905a #8
  END
BUT_ONLY


//////////////////////////////////////////////////////////////////////
// Scripts, summons
//////////////////////////////////////////////////////////////////////

COMPILE ~atweaks/baf/fiends/rr#snaba.baf~ // Summoned Nabassu AI script
        ~atweaks/baf/fiends/rr#scorn.baf~ // Summoned Cornugon AI script
        ~atweaks/baf/fiends/rr#tempt.baf~ // Summoned Glabrezu script for tempting mortals
        ~atweaks/baf/fiends/rr#sglab.baf~ // Summoned Glabrezu AI script
        ~atweaks/baf/fiends/rr#sgelu.baf~ // Summoned Gelugon AI script
        ~atweaks/baf/fiends/rr#spitf.baf~ // Summoned Pit Fiend AI script
        ~atweaks/baf/fiends/rr#sbalr.baf~ // Summoned Balor AI script
        ~atweaks/baf/fiends/fl#sarca.baf~ // Summoned Arcanaloth AI script
        ~atweaks/baf/fiends/fl#snyca.baf~ // Summoned Nycaloth AI script
        ~atweaks/baf/fiends/fl#sultr.baf~ // Summoned Ultroloth AI script

// Replace RunAwayFrom() with RandomWalkContinuous()
// Reasoning: RunAwayFrom() often tends to slow down the game
COPY_EXISTING runenemy.bcs override // Innocent bystander run away script
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~RunAwayFrom(LastSeenBy(Myself),30)~
                      ~RandomWalkContinuous()~
  END
BUT_ONLY

// Make innocent bystanders run away from summoned fiends
EXTEND_TOP runenemy.bcs ~atweaks/baf/fiends/runenemy.baf~

// Note: this is basically a bugfix, as ApplySpell() casts the
// spell at 1st level even if the caster's level is higher
COPY_EXISTING mage20b.bcs override // High level mage script
              hellgen.bcs override // Enslaved Genie (Tears of Bhaal)
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY
      ~ApplySpell(Myself,WIZARD_PROTECTION_FROM_EVIL)~
      ~ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_EVIL)~
  END
BUT_ONLY


//////////////////////////////////////////////////////////////////////
// Dialogue changes
//////////////////////////////////////////////////////////////////////

ACTION_FOR_EACH file IN
                gorbat1 // Ka'rashur (Baatezu Leader in Watcher's Keep)
                gortan1 // Tahazzar (Tanar'ri Leader in Watcher's Keep)
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.dlg~ BEGIN
    COPY_EXISTING ~%file%.dlg~ override
     PATCH_IF SOURCE_SIZE > 0x01 BEGIN
       DECOMPILE_AND_PATCH BEGIN
         REPLACE_TEXTUALLY EXACT_MATCH
           ~SetGlobal("BatEnemy","GLOBAL",1)~
           ~SetGlobal("BatEnemy","GLOBAL",1) Enemy() Help()~
         REPLACE_TEXTUALLY EXACT_MATCH
           ~SetGlobal("TanEnemy","GLOBAL",1)~
           ~SetGlobal("TanEnemy","GLOBAL",1) Enemy() Help()~
       END
     END
   BUT_ONLY
  END
END


//////////////////////////////////////////////////////////////////////
// New load screen hints
//////////////////////////////////////////////////////////////////////

COPY_EXISTING loadhint.2da override
              loadh25.2da override
  COUNT_2DA_ROWS 2 rows
  PATCH_FOR_EACH traref IN
                 9000 // Etheralness tip
                 9001 // Death Gaze tip
                 9002 // Fiend gating tip
                 9004 // Extraplanar healing immunity tip
  BEGIN
    strref = RESOLVE_STR_REF ((AT traref))
    INSERT_2DA_ROW rows 2 ~%rows% %strref%~
    ++rows
  END
  //PRETTY_PRINT_2DA // Uncomment at WeiDU 232 or later
BUT_ONLY


//////////////////////////////////////////////////////////////////////
// More sensible encounters
//////////////////////////////////////////////////////////////////////

// Replace the Abishai and the Demon Knight near Diaytha with a
// Glabrezu and a Balor
COPY_EXISTING ar6105.are override // Ogremoch's lair in Sendai's enclave
  GET_OFFSET_ARRAY aa 0x54 4 0x58 2 0 0 0x110
  PHP_EACH aa AS i => off BEGIN
    READ_ASCII off + 0x80 file
    PATCH_IF "%file%" STRING_EQUAL_CASE DEMABI01 BEGIN
      WRITE_ASCII off + 0x80 DEMGLA01 #8
    END
    PATCH_IF "%file%" STRING_EQUAL_CASE DEATHKNI BEGIN
      WRITE_ASCII off + 0x80 DEMBAL01 #8
    END
  END
BUT_ONLY

// Melissan should not summon Bone Fiends during the final battle in
// order to avoid Blood War issues
COPY_EXISTING meliss01.bcs override
              meliss02.bcs override
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~MELSUM02~ ~MELSUM01~
  END
BUT_ONLY


//////////////////////////////////////////////////////////////////////
// Compatibility
//////////////////////////////////////////////////////////////////////

// Make sure summoned fiends have fiend immunities
COPY_EXISTING demnabsu.cre override // Nabassu
              demglasu.cre override // Glabrezu
              dempitsu.cre override // Pit Fiend
  PATCH_IF !FILE_CONTAINS_EVALUATED (~%SOURCE_FILE%~ RINGDEMN) BEGIN
    ADD_CRE_ITEM RINGDEMN #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ RRING
  END
BUT_ONLY

////////////////////////////////////////
// BG1 NPC
////////////////////////////////////////

// Make Amelia change into a Sirine instead of a Fire Elemental
ACTION_IF FILE_EXISTS_IN_GAME x#amelia.dlg BEGIN
  COMPILE ~atweaks/dlg/x#amelia.d~
END

////////////////////////////////////////
// SCS
////////////////////////////////////////

// Create scripts and creatures outside the ACTION_IF in case SCSII is installed after
COMPILE ~atweaks/baf/fiends/fl#nabsu.baf~
        ~atweaks/baf/fiends/fl#corsu.baf~
        ~atweaks/baf/fiends/fl#glasu.baf~
        ~atweaks/baf/fiends/fl#gelsu.baf~
        ~atweaks/baf/fiends/fl#balsu.baf~
        ~atweaks/baf/fiends/fl#pitsu.baf~

COPY_EXISTING demnab01.cre ~override/fl#nabsu.cre~
              telcor1.cre  ~override/fl#corsu.cre~
              demgla01.cre ~override/fl#glasu.cre~
              rr#hgel.cre  ~override/fl#gelsu.cre~
              dembal01.cre ~override/fl#balsu.cre~
              dempit01.cre ~override/fl#pitsu.cre~
  WRITE_LONG 0x10 2 // Clear flags, leave only "no corpse"
  WRITE_ASCIIE 0x248 "%DEST_RES%" #8 // Summoned script
  WRITE_ASCII  0x250 ~~ #8 // Class
  WRITE_ASCII  0x258 ~~ #8 // Race
  WRITE_ASCII  0x260 ~~ #8 // General
  WRITE_ASCII  0x268 ~~ #8 // Default
  WRITE_BYTE   0x270 255 // Enemy
  WRITE_BYTE   0x275 1 // Male
  WRITE_ASCIIE 0x280 ~%DEST_RES%~ #32 // Script name
  // Make all items unstealable&undroppable
  READ_LONG 0x2bc io
  FOR (i=0;i<LONG_AT 0x2c0;++i) BEGIN
    WRITE_BYTE io + 0x14*i + 0x10 THIS BOR 10
  END
  PATCH_IF FILE_EXISTS_IN_GAME dw#sumfi.itm AND
           !FILE_CONTAINS_EVALUATED ("%SOURCE_FILE%" "dw#sumfi")
  BEGIN
    ADD_CRE_ITEM "dw#sumfi" #0 #0 #0 none boots
  END

//Commandeer SCSII's private fiend-summoning spells and have them summon SCSII-aTweaks hybrids
ACTION_IF FILE_EXISTS_IN_GAME dw#cacof.eff AND !FiendsOriginal BEGIN //FiendsOriginal eq "do not alter mod-added fiends"

  ACTION_IF FILE_EXISTS_IN_GAME dw#cacof.eff AND FILE_EXISTS_IN_GAME dw#pitsu.eff BEGIN //If SCSII Improved Fiends is installed
    OUTER_SPRINT $crefile(dw#cacof) fl#nabsu
    OUTER_SPRINT $crefile(dw#sumbf) fl#corsu
    OUTER_SPRINT $crefile(dw#sumgl) fl#glasu
    OUTER_SPRINT $crefile(dw#sumco) fl#gelsu
    OUTER_SPRINT $crefile(dw#basum) fl#balsu
    OUTER_SPRINT $crefile(dw#pitsu) fl#pitsu

    COPY_EXISTING dw#cacof.eff override
                  dw#sumbf.eff override
                  dw#sumgl.eff override
                  dw#sumco.eff override
                  dw#basum.eff override
                  dw#pitsu.eff override
      WRITE_ASCIIE 0x30 $crefile("%SOURCE_RES%") #8
    BUT_ONLY
  END /* ELSE BEGIN //Otherwise it is just SCSII Smarter Mages

    //These are copies of Mordenkainen's Sword; change them to conjure up the appropriate fiend instead
    COPY_EXISTING dw#sumgl.spl override
                  dw#sumco.spl override
                  dw#cacof.spl override
                  dw#sumbf.spl override


    //These are copies of Gate; change the other to conjure up a Balor
    COPY_EXISTING dw#gatem.spl override
                  dw#basum.spl override


  END*/
END

//SCSII colours these dudes green, but we turn them into glabrezus and mariliths
ACTION_IF FILE_EXISTS_IN_GAME dw#fiends_notinnate.mrk OR FILE_EXISTS_IN_GAME dw#fiends_innate.mrk BEGIN
  COPY_EXISTING abydem01.cre override
		obsdem01.cre override
    LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete = 51 END
END

////////////////////////////////////////
// Spell Revisions
////////////////////////////////////////

//Restore Protection from SUMMONED_DEMON to Protection from Evil
ACTION_IF MOD_IS_INSTALLED spell_rev.tp2 0 BEGIN
  COPY_EXISTING spcl213.spl override
                spcl233.spl override
                sppr107.spl override
                sppr408.spl override
                spwi113.spl override
    PATCH_IF SOURCE_SIZE > 0x71 BEGIN
      READ_LONG  0x64 ao
      READ_LONG  0x6a eo
      READ_SHORT 0x70 ei
      FOR (i = 0; i < SHORT_AT 0x68; ++i) BEGIN
        WRITE_SHORT ao + 0x28 * i + 0x20 ei
        READ_SHORT  ao + 0x28 * i + 0x1e ne
        FOR (j = 0; j < ne; ++j) BEGIN
          READ_SHORT eo + 0x30 * (ei + j) type
          PATCH_IF type = 219 BEGIN
            READ_ASCII     eo + 0x30 * (ei + j)       copy (0x30)
            INSERT_BYTES   eo + 0x30 * (ei + j)       0x30
              WRITE_ASCIIE eo + 0x30 * (ei + j)       "%copy%"
              WRITE_SHORT  eo + 0x30 * (ei + j)       100
              WRITE_LONG   eo + 0x30 * (ei + j) + 0x4 9
              WRITE_LONG   eo + 0x30 * (ei + j) + 0x8 7
            ++ne
            j=ne
          END
        END
        WRITE_SHORT ao + 0x28 * i + 0x1e ne
        ei += ne
      END
    END
  BUT_ONLY
END
