
COPY    ~aTweaks/BAM/SPDIMNDR.BAM~   ~override~                                    // IWD1's Dimension Door animation
COPY    ~aTweaks/WAV/EFF_M09.WAV~    ~override~                                    // IWD1's Dimension Door sound effect
COPY    ~atweaks/bam/rr#ddbg2.bam~   ~override~                                    // Default BG2 Dimension Door animation (needed for a few static animations)

LAF handle_old_dd END

// Ensure that the portal in Sendai's lair always uses the BG2-style Dimension Door animation (IWD-style doesn't look so good here)
COPY_EXISTING ~ar6108.are~ ~override~
  READ_LONG 0xac "anim_num"
  READ_LONG 0xb0 "anim_off"
  FOR (index = 0 ; index < anim_num ; index = index + 1) BEGIN
    READ_ASCII ("%anim_off%" + ("%index%" * 0x4c)) "anim_name"
    PATCH_IF ("%anim_name%" STRING_EQUAL_CASE "SPDIMNDR") BEGIN
      WRITE_ASCII ("%anim_off%" + 0x28 + ("%index%" * 0x4c)) "rr#ddbg2" #8 // Animation resref (BAM)
    END
  END
BUT_ONLY_IF_IT_CHANGES

DEFINE_PATCH_FUNCTION replace_create_creature_door BEGIN
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP
      ~ActionOverride(\(.+\),CreateCreatureDoor(\(\".+\"\),\(\[-?[0-9]+\.-?[0-9]+\]\),\([0-9]+\)))~
      ~ActionOverride(\1,CreateVisualEffect("spdimndr",\3))
       ActionOverride(\1,CreateCreature(\2,\3,\4))~

    REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP
      ~ActionOverride(\(.+\),CreateCreatureObjectDoor(\(\".+\"\),\(\"?.+\"?\),\([0-9]+\),\([0-9]+\),\([0-9]+\)))~
      ~ActionOverride(\1,CreateVisualEffectObject("spdimndr",\3))
       ActionOverride(\1,CreateCreatureObject(\2,\3,\4,\5,\6))~

    REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP
      ~CreateCreatureDoor(\(\".+\"\),\(\[-?[0-9]+\.-?[0-9]+\]\),\([0-9]+\))~
      ~SetInterrupt(FALSE)
       CreateVisualEffect("spdimndr",\2)
       CreateCreature(\1,\2,\3)
       SetInterrupt(TRUE)~

    REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP
      ~CreateCreatureObjectDoor(\(\".+\"\),\(\"?.+\"?\),\([0-9]+\),\([0-9]+\),\([0-9]+\))~
      ~SetInterrupt(FALSE)
       CreateVisualEffectObject("spdimndr",\2)
       CreateCreatureObject(\1,\2,\3,\4,\5)
       SetInterrupt(TRUE)~
  END
END


COPY_EXISTING_REGEXP GLOB ~.*\.dlg$~ override
  LPF replace_create_creature_door END
IF ~CreateCreatureDoor\|CreateCreatureObjectDoor~
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~.*\.bcs$~ override
  LPF replace_create_creature_door END
IF ~23[12]OB~
BUT_ONLY

LAF make_dimension_door STR_VAR graphic = SPDIMNDR sound = EFF_M09 END
LAF make_dryad_teleport STR_VAR graphic = SPDIMNDR sound = EFF_M09 END
