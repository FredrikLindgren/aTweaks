
///////////////////////////////////////////////////////////
//Create gating spells
///////////////////////////////////////////////////////////

//Balor: summon 1-6 (4) lesser (alu-fiend, cambion, succubus), 1-4 (2) greater (nabassu) or 1 true tanar'ri (balor, marilith, glabrezu)
COPY ~atweaks/spl/base.spl~ "override/fl#gtbal.spl"
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 parameter1 = 1 probability1 = 33 STR_VAR resource = fl#gtba1 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 parameter1 = 1 probability1 = 66 probability2 = 34 STR_VAR resource = fl#gtba2 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 parameter1 = 1 probability1 = 100 probability2 = 67 STR_VAR resource = fl#gtba3 END
COPY_EXISTING spwi905.spl "override/fl#gtba1.spl"
  LPF prep_for_gate END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter2 = 2 timing = 1 probability1 = 33 STR_VAR resource = rr#hsbal END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter2 = 2 timing = 1 probability1 = 66 probability2 = 34 STR_VAR resource = rr#hsgla END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter2 = 2 timing = 1 probability1 = 100 probability2 = 67 STR_VAR resource = rr#hsmar END
COPY_EXISTING spwi905.spl "override/fl#gtba2.spl"
  LPF prep_for_gate END
  FOR (i=0;i<2;++i) BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter2 = 2 timing = 1 STR_VAR resource = rr#hsnab END
  END
COPY_EXISTING spwi905.spl "override/fl#gtba3.spl"
  LPF prep_for_gate END
  probability1 = 0
  probability2 = 0
  PATCH_FOR_EACH resource IN rr#hsalu rr#hscam rr#hssuc BEGIN
    probability2 = probability1 > 0 ? probability2 + 33 : probability2
    probability2 = probability2 = 33 ? probability2 + 1 : probability2
    probability1 += 33
    probability1 = probability1 = 99 ? 100 : probability1
    FOR (i=0;i<4;++i) BEGIN
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter2 = 2 timing = 1 probability1 probability2 STR_VAR resource END
    END
  END
COPY_EXISTING 
              spcaco.eff "override/rr#hsbal.eff"
              spcaco.eff "override/rr#hsmar.eff"
              spcaco.eff "override/rr#hsgla.eff"
              spcaco.eff "override/rr#hsnab.eff"
              spcaco.eff "override/rr#hsalu.eff"
              spcaco.eff "override/rr#hscam.eff"
              spcaco.eff "override/rr#hssuc.eff"
  WRITE_ASCIIE 0x30 "%DEST_RES%" #8
      
//Marilith: summon 1-6 (4) lesser, 1-4 (2) greater or 1 true tanar'ri
//Mariliths can use the Balor spell

//Glabrezu: summon 1 true tanar'ri (balor, marilith, glabrezu)
COPY_EXISTING spwi905.spl "override/fl#gtgla.spl"
  LPF prep_for_gate END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter2 = 2 timing = 1 probability1 = 33 STR_VAR resource = rr#hsbal END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter2 = 2 timing = 1 probability1 = 66 probability2 = 34 STR_VAR resource = rr#hsgla END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter2 = 2 timing = 1 probability1 = 100 probability2 = 67 STR_VAR resource = rr#hsmar END

//Succubus: summon 1 balor
COPY_EXISTING spwi905.spl "override/fl#gtsuc.spl"
  LPF prep_for_gate END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 1 parameter2 = 2 timing = 1 STR_VAR resource = rr#hsbal END
  

//Pit Fiend: summon 2 lesser (abishai, erinyes, osyluth) or 1 greater (cornugon, gelugon, pit fiend) baatezu
COPY ~atweaks/spl/base.spl~ ~override/fl#gtpit.spl~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 parameter1 = 1 probability1 = 50 STR_VAR resource = fl#gtpi1 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 parameter1 = 1 probability2 = 51 STR_VAR resource = fl#gtpi2 END
COPY_EXISTING spwi905.spl ~override/fl#gtpi1.spl~
  LPF prep_for_gate END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter2 = 2 timing = 1 probability1 = 33 STR_VAR resource = rr#hspit END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter2 = 2 timing = 1 probability1 = 66 probability2 = 34 STR_VAR resource = rr#hsgel END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter2 = 2 timing = 1 probability2 = 67 STR_VAR resource = rr#hscor END
COPY_EXISTING spwi905.spl "override/fl#gtpi2.spl"
  LPF prep_for_gate END
  probability1 = 0
  probability2 = 0
  PATCH_FOR_EACH resource IN rr#hsabi rr#hseri rr#hsbfn BEGIN
    probability2 = probability1 > 0 ? probability2 + 33 : probability2
    probability2 = probability2 = 33 ? probability2 + 1 : probability2
    probability1 += 33
    probability1 = probability1 = 99 ? 100 : probability1
    FOR (i=0;i<2;++i) BEGIN
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter2 = 2 timing = 1 probability1 probability2 STR_VAR resource END
    END
  END
COPY_EXISTING 
              spcaco.eff "override/rr#hspit.eff"
              spcaco.eff "override/rr#hsgel.eff"
              spcaco.eff "override/rr#hscor.eff"
              spcaco.eff "override/rr#hsabi.eff"
              spcaco.eff "override/rr#hseri.eff"
              spcaco.eff "override/rr#hsbfn.eff"
  WRITE_ASCIIE 0x30 "%DEST_RES%" #8

//Gelugon: summon 2-8 (5) osyluth or 1-2 (1) gelugons
COPY_EXISTING spwi905.spl "override/fl#gtgel.spl"
  LPF prep_for_gate END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter2 = 2 timing = 1 probability1 = 50 STR_VAR resource = rr#hsgel END
  FOR (i=0;i<5;++i) BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter2 = 2 timing = 1 probability2 = 51 STR_VAR resource = rr#hsbfn END
  END
  
//Cornugon: summon 2-16 (8) abishai or 1-3 (2) cornugons
COPY_EXISTING spwi905.spl "override/fl#gtcor.spl"
  LPF prep_for_gate END
  FOR (i=0;i<2;++i) BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter2 = 2 timing = 1 probability1 = 50 STR_VAR resource = rr#hscor END
  END
  FOR (i=0;i<8;++i) BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter2 = 2 timing = 1 probability2 = 51 STR_VAR resource = rr#hsabi END
  END
  
//Abishai: summon 1-3 abishai
COPY_EXISTING spwi905.spl "override/fl#gtabi.spl"
  LPF prep_for_gate END
  probability1 = 0
  probability2 = 0
  PATCH_FOR_EACH n IN 1 2 3 BEGIN
    probability2 = probability1 > 0 ? probability2 + 33 : probability2
    probability2 = probability2 = 33 ? probability2 + 1 : probability2
    probability1 += 33
    probability1 = probability1 = 99 ? 100 : probability1
    FOR (i=0;i<n;++i) BEGIN
      LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter2 = 2 timing = 1 probability1 probability2 STR_VAR resource = rr#hsabi END
    END
  END
  
//Osyluth: summon 1-2 osyluth
COPY_EXISTING spwi905.spl "override/fl#gtosy.spl"
  LPF prep_for_gate END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter2 = 2 timing = 1 probabiltiy1 = 50 STR_VAR resource = rr#hsbfn END
  FOR (i=0;i<2;++i) BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter2 = 2 timing = 1 probability2 = 51 STR_VAR resource = rr#hsbfn END
  END
  
  
///////////////////////////////////////////////////
//Create creatures
///////////////////////////////////////////////////

OUTER_SPRINT $script(rr#hsbal) rr#hbalr
OUTER_SPRINT $script(rr#hsgla) rr#hglab
OUTER_SPRINT $script(rr#hsmar) rr#hmari
OUTER_SPRINT $script(rr#hsnab) rr#hnaba
OUTER_SPRINT $script(rr#hssuc) rr#hsucc
OUTER_SPRINT $script(rr#hsalu) rr#haluf
OUTER_SPRINT $script(rr#hscam) rr#hcamb
OUTER_SPRINT $script(rr#hspit) rr#hpitf
OUTER_SPRINT $script(rr#hsgel) rr#hgelu
OUTER_SPRINT $script(rr#hscor) rr#hcorn
OUTER_SPRINT $script(rr#hsabi) rr#habis
OUTER_SPRINT $script(rr#hseri) rr#herin
OUTER_SPRINT $script(rr#hsbfn) rr#hbfnd

COPY_EXISTING 
              telbal1.cre  "override/rr#hsbal.cre"
              demgla01.cre "override/rr#hsgla.cre"
              demosum1.cre "override/rr#hsmar.cre"  
              demnab01.cre "override/rr#hsnab.cre"
              gorsuc01.cre "override/rr#hssuc.cre"
              alufie01.cre "override/rr#hsalu.cre"
              cambion1.cre "override/rr#hscam.cre"
              dempit01.cre "override/rr#hspit.cre"
              rr#hgel.cre  "override/rr#hsgel.cre"
              demcor01.cre "override/rr#hscor.cre"
              demabi01.cre "override/rr#hsabi.cre"
              deriny01.cre "override/rr#hseri.cre"
              dbonef01.cre "override/rr#hsbfn.cre"
  WRITE_ASCII_LIST 0x248 $script("%DEST_RES%") ~~ ~~ ~~ ~~
  WRITE_BYTE   0x270 255 //hostile
  WRITE_ASCIIE 0x280 ~%DEST_RES%~ #32                                              //script name
  WRITE_ASCII  0x2cc ~~ #8
  LPF DELETE_CRE_ITEM STR_VAR item_to_delete = "rndtre.." END
  READ_LONG 0x2bc io
  FOR (i=0;i<LONG_AT 0x2c0;++i) BEGIN                                              //Make all items unstealable&undroppable
    WRITE_BYTE io + 0x14*i + 0x10 THIS BOR 10
  END
  PATCH_IF FILE_EXISTS_IN_GAME dw#sumfi.itm AND !FILE_CONTAINS_EVALUATED ("%SOURCE_FILE%" "dw#sumfi") BEGIN
    ADD_CRE_ITEM "dw#sumfi" #0 #0 #0 none boots
  END
  
///////////////////////////////////////////////////
//Add gating script blocks
///////////////////////////////////////////////////              
  
<<<<<<<< atweaks/fl-inlined/Placeholder4Gating.baf
IF
  False()
  Global("flPlaceholder4Gating","LOCALS",0)
THEN
  RESPONSE #100
    Continue()
END
>>>>>>>>
<<<<<<<< atweaks/fl-inlined/Placeholder24Gating.baf
IF
  False()
  Global("Placeholder24Gating","LOCALS",0)
THEN
  RESPONSE #100
    Continue()
END
>>>>>>>>

COPY_EXISTING rr#habis.bcs override
  REPLACE_BCS_BLOCK ~atweaks/fl-inlined/Placeholder4Gating.baf~ ~atweaks/baf/fiends/gate_abishai.baf~
  DECOMPILE_BCS_TO_BAF
    REPLACE 10000759 @759
    REPLACE 10000760 @760
  COMPILE_BAF_TO_BCS
  
COPY_EXISTING rr#hbalr.bcs override
  REPLACE_BCS_BLOCK ~atweaks/fl-inlined/Placeholder4Gating.baf~ ~atweaks/baf/fiends/gate_balor.baf~
  DECOMPILE_BCS_TO_BAF
    REPLACE 10000758 @758
  COMPILE_BAF_TO_BCS
  

COPY_EXISTING rr#hbfnd.bcs override
  REPLACE_BCS_BLOCK ~atweaks/fl-inlined/Placeholder4Gating.baf~ ~atweaks/baf/fiends/gate_osyluth.baf~
  DECOMPILE_BCS_TO_BAF
    REPLACE 10000759 @759
    REPLACE 10000760 @760
  COMPILE_BAF_TO_BCS

COPY_EXISTING rr#hcorn.bcs override
  REPLACE_BCS_BLOCK ~atweaks/fl-inlined/Placeholder4Gating.baf~ ~atweaks/baf/fiends/gate_cornugon.baf~
  DECOMPILE_BCS_TO_BAF
    REPLACE 10000759 @759
    REPLACE 10000760 @760
  COMPILE_BAF_TO_BCS
  
COPY_EXISTING rr#hgelu.bcs override
  REPLACE_BCS_BLOCK ~atweaks/fl-inlined/Placeholder4Gating.baf~ ~atweaks/baf/fiends/gate_gelugon.baf~
  DECOMPILE_BCS_TO_BAF
    REPLACE 10000759 @759
    REPLACE 10000760 @760
  COMPILE_BAF_TO_BCS
  
COPY_EXISTING rr#hglab.bcs override
  REPLACE_BCS_BLOCK ~atweaks/fl-inlined/Placeholder4Gating.baf~ ~atweaks/baf/fiends/gate_glabrezu.baf~
  DECOMPILE_BCS_TO_BAF
    REPLACE 10000758 @758
    REPLACE 10000760 @760
  COMPILE_BAF_TO_BCS
  
COPY_EXISTING rr#hmari.bcs override
  REPLACE_BCS_BLOCK ~atweaks/fl-inlined/Placeholder4Gating.baf~ ~atweaks/baf/fiends/gate_marilith.baf~
  DECOMPILE_BCS_TO_BAF
    REPLACE 10000758 @758
    REPLACE 10000760 @760
  COMPILE_BAF_TO_BCS
  
COPY_EXISTING rr#hpitf.bcs override
  REPLACE_BCS_BLOCK ~atweaks/fl-inlined/Placeholder4Gating.baf~ ~atweaks/baf/fiends/gate_pit_fiend.baf~
  DECOMPILE_BCS_TO_BAF
    REPLACE 10000759 @759
  COMPILE_BAF_TO_BCS
  
COPY_EXISTING rr#hsucc.bcs override
  REPLACE_BCS_BLOCK ~atweaks/fl-inlined/Placeholder4Gating.baf~ ~atweaks/baf/fiends/gate_succubus.baf~
  REPLACE_BCS_BLOCK ~atweaks/fl-inlined/Placeholder24Gating.baf~ ~atweaks/baf/fiends/gate_succubus2.baf~
  DECOMPILE_BCS_TO_BAF
    REPLACE 10000758 @758
    REPLACE 10000760 @760
  COMPILE_BAF_TO_BCS
  
  
