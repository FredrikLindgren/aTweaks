//Go after the wounded and weak
IF TRIGGER
  TargetBlock(PCsPreferringWeak)
  TargetBlock(EnemiesInOrder)
  TriggerBlock(WeaponImmunity|DamageRes|Stoneskin|MirrorImage|Disabled|Enemy)
  !HPPercentGT(scstarget,25)
  Range(scstarget,5)
THEN DO
  Action(AttackType|100|50)
END

//Go after the slightly less wounded but equally weak
IF TRIGGER
  TargetBlock(PCsPreferringWeak)
  //TargetBlock(EnemiesInOrder)
  TriggerBlock(WeaponImmunity|DamageRes|Stoneskin|MirrorImage|Disabled|Enemy)
  !HPPercentGT(scstarget,50)
  Range(scstarget,5)
THEN DO
  Action(AttackType|100|50)
END

//Look for wounded and weak and walk to them
IF TRIGGER
  TargetBlock(PCsPreferringWeak)
  //TargetBlock(EnemiesInOrder)
  TriggerBlock(WeaponImmunity|DamageRes|Stoneskin|MirrorImage|Disabled|Enemy)
  !HPPercentGT(scstarget,50)
  !TargetUnreachable(scstarget)
THEN DO
  Action(MoveIfEmpty|100|100)
END

//Go after the weak and unprotected
IF TRIGGER
  TargetBlock(PCsPreferringWeak)
  TriggerBlock(WeaponImmunity|DamageRes|Stoneskin|MirrorImage|Disabled|Enemy)
  Range(scstarget,5)
THEN DO
  Action(AttackType|100|50)
END

//Turn undead and paladins
INCLUDE FILE(general/turn.ssl)

//Lay into nearby enemies
IF TRIGGER
  TargetBlock(PCsInOrder)
  TriggerBlock(WeaponImmunity|DamageRes|Stoneskin|MirrorImage|Disabled|Enemy)
THEN DO
  Action(AttackType|100|100)
END

//Look for anyone unprotected and walk to them
IF TRIGGER
  TargetBlock(PCsPreferringWeak)
  //TargetBlock(EnemiesInOrder)
  TriggerBlock(WeaponImmunity|Disabled|DamageRes|Stoneskin|MirrorImage|Enemy)
  !TargetUnreachable(scstarget)
THEN DO
  Action(MoveIfEmpty|100|100)
END

//Start ignoring Mirror Images first
IF TRIGGER
  TargetBlock(PCsInOrder)
  TriggerBlock(WeaponImmunity|Disabled|DamageRes|Stoneskin|Enemy)
THEN DO
  Action(AttackType|100|50)
END

//Then Stoneskins
IF TRIGGER
  TargetBlock(PCsInOrder)
  TriggerBlock(WeaponImmunity|Disabled|DamageRes|Enemy)
THEN DO
  Action(AttackType|100|50)
END

//Stop ignoring those with !GT75% damage resistance
IF TRIGGER
  TargetBlock(PCsInOrder)
  TriggerBlock(WeaponImmunity|Disabled|DamageRes75|Enemy)
THEN DO
  Action(AttackType|100|100)
END

//Stop ignoring those with !GT99% damage resistance
IF TRIGGER
  TargetBlock(PCsInOrder)
  TriggerBlock(WeaponImmunity|Disabled|DamageResFull|Enemy)
THEN DO
  Action(AttackType|100|100)
END

//Clean out minions and stuff before we go for the disabled
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(WeaponImmunity|Disabled|DamageResFull|Enemy)
THEN DO
  Action(AttackType|100|100)
END

//Stop ignoring disabled
IF TRIGGER
  ConditionalTargetBlock(PCsPreferringWeak;Range(scstarget,5))
  TargetBlock(EnemiesInOrder)
  TriggerBlock(WeaponImmunity|DamageResFull|Enemy)
THEN DO
  Action(AttackType|100|100)
END