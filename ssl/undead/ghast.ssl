INCLUDE FILE(include/definitions.ssl)

IF
  RequireBlock(Ghast)
  !StateCheck(Myself,STATE_REALLY_DEAD)
  !GlobalTimerNotExpired("flAura","LOCALS")
THEN
  RESPONSE #100
    ReallyForceSpellRES("fl#ghaau",Myself)
    SetGlobalTimer("flAura","LOCALS",18)
    Continue()
END

IF
  RequireBlock(GhoulLord)
  !StateCheck(Myself,STATE_REALLY_DEAD)
  !GlobalTimerNotExpired("flAura","LOCALS")
THEN
  RESPONSE #100
    ReallyForceSpellRES("fl#glord",Myself)
    SetGlobalTimer("flAura","LOCALS",6)
    Continue()
END

INCLUDE FILE(include/baseinitial.ssl)

//Non-standard melee scripting because these guys do not particularly care about stoneskins or mirror images

//Weak and wounded
IF TRIGGER
  TargetBlock(PCsPreferringWeak)
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|Enemy|SlashRes|WeaponImmunity)
  !HPPercentGT(scstarget,25)
  Range(scstarget,5)
THEN DO
  Action(Attack|100|50)
END

IF TRIGGER
  TargetBlock(PCsPreferringWeak)
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|Enemy|SlashRes|WeaponImmunity)
  !HPPercentGT(scstarget,25)
  //!TargetUnreachable(scstarget)
THEN DO
  Action(MoveIfEmpty|100|50)
END

//Less weak and wounded
IF TRIGGER
  TargetBlock(PCsPreferringWeak)
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|Enemy|SlashRes|WeaponImmunity)
  !HPPercentGT(scstarget,50)
  Range(scstarget,5)
THEN DO
  Action(Attack|100|50)
END

IF TRIGGER
  TargetBlock(PCsPreferringWeak)
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|Enemy|SlashRes|WeaponImmunity)
  !HPPercentGT(scstarget,50)
  //!TargetUnreachable(scstarget)
THEN DO
  Action(MoveIfEmpty|100|50)
END

//Attack wounded beefy types (and wounded enemies in general)
IF TRIGGER 
  TargetBlock(PCArchers)
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|Enemy|SlashRes|WeaponImmunity)
  !HPPercentGT(scstarget,50)
  Range(scstarget,5)
THEN DO
  Action(Attack|100|100)
END

IF TRIGGER 
  TargetBlock(PCArchers)
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|Enemy|SlashRes|WeaponImmunity)
  !HPPercentGT(scstarget,50)
  //!TargetUnreachable(scstarget)
THEN DO
  Action(MoveIfEmpty|100|100)
END

//Attack enemies in general, favouring archers/beefy types and casters, also go easier on the DR
IF TRIGGER
  TargetBlock(PCArchers)
  TargetBlock(PCCasters)
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|Enemy|SlashRes75|WeaponImmunity)
  Range(scstarget,5)
THEN DO
  Action(Attack|100|100)
END

IF TRIGGER
  TargetBlock(PCArchers)
  TargetBlock(PCCasters)
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|Enemy|SlashRes75|WeaponImmunity)
  //!TargetUnreachable(scstarget)
THEN DO
  Action(MoveIfEmpty|100|100)
END

//Attack the first, best
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|Enemy|SlashRes75|WeaponImmunity)
  Range(scstarget,5)
THEN DO
  Action(Attack|100|100)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|Enemy|SlashRes75|WeaponImmunity)
  //!TargetUnreachable(scstarget)
THEN DO
  Action(MoveIfEmpty|100|100)
END

//Do not particularly care about DR at all
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|Enemy|SlashResFull|WeaponImmunity)
  ActionListEmpty()
THEN DO
  Action(Attack|100|100)
END

//Go after the disabled
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Enemy|SlashResFull|WeaponImmunity)
  ActionListEmpty()
THEN DO
  Action(Attack|100|100)
END

INCLUDE FILE(include/open_door.ssl)

INCLUDE FILE(include/baseend.ssl)
