ACTION_IF !GAME_IS ~bgee bg2ee~ AND
          !FILE_EXISTS_IN_GAME cdwi408.spl // Fixpack
BEGIN
  COPY_EXISTING ~stonskin.itm~ ~override~ // ring which grants the stoneskin effect while equipped (used by Mekrath, Rayic Gethras, Terminsel... etc.)
    LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete = 218 END
    LPF ADD_ITEM_EQEFFECT
      INT_VAR
        opcode = 146
        target = 1
        parameter2 = 1
        timing = 2
        resist_dispel = 3
      STR_VAR
        resource = ~RR#STONS~
    END
  BUT_ONLY

  // Clone stoneskin into a custom spell which will be used by the ring
  COPY_EXISTING ~spwi408.spl~  ~override/RR#STONS.SPL~
    SAY NAME1 #0 // null spell name to remove combat log feedback
    WRITE_SHORT   0x22  0    // remove casting graphics
    WRITE_ASCII   0x10 ~~ #8 // remove casting sound
    PATCH_FOR_EACH opcode_to_delete IN 215 174 50 BEGIN
      LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete END
    END
    LPF ALTER_SPELL_EFFECT
      INT_VAR
        check_headers = 1
        match_opcode = 218
        timing = 1
        duration = 0
    END
  BUT_ONLY
END

// Make the Gargoyle Boots cast a proper Stoneskin spell instead of mimicking its effect
ACTION_IF ENGINE_IS ~tob bgee bg2ee~ AND
          !MOD_IS_INSTALLED ITEM_REV.TP2 0
BEGIN
  COPY ~aTweaks/SPL/RR#BT12.SPL~ ~override~ // Empty Stoneskin spell shell (used for inheriting item effects)

  COPY_EXISTING ~boot12.itm~ ~override~
    LPF ITEM_EFFECT_TO_SPELL STR_VAR new_itm_spl = RR#BT12.SPL END
    LPF DELETE_ITEM_EFFECT INT_VAR opcode_to_delete = "-1" header = 1 END
    LPF ADD_ITEM_EFFECT
      INT_VAR
        opcode = 146       // effect: #146 (cast spell at creature)
        target = 1         // target: 1 (self)
        parameter2 = 1     // param2: 1 (cast instantly)
        timing = 1         // timing mode: 1 (instant/permanent)
        resist_dispel = 3  // dispel/resistance: 3 (dispellable and bypasses magic resistance)
      STR_VAR
        resource = RR#BT12 // resref: RR#BT12.SPL (newly created custom Stoneskin spell)
    END
  BUT_ONLY
END
