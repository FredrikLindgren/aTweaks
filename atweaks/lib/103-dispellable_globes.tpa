
// The Major and Minor Globes of Invulnerability should be dispellable by Dispel Magic, as per the spell's description and per PnP rules

ACTION_FOR_EACH ~file~ IN
                ~spwi406~                                                          // Minor Globe of Invulnrability
                ~spwi602~                                                          // Globe of Invulnrability
                ~RR#WI406~                                                         // Minor Globe of Invulnrability (used by RR for Selina's Amulet)
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%.spl~ BEGIN
    COPY_EXISTING ~%file%.spl~ ~override~
      PATCH_IF (%SOURCE_SIZE% > 0x71) THEN BEGIN                                   // file size sanity check (filters out 0 byte files i.e. ALLOW_MISSING)
        READ_LONG  0x64 "abil_off"
        READ_SHORT 0x68 "abil_num"
        READ_LONG  0x6a "fx_off"
        FOR (index = 0 ; index < abil_num ; index = index + 1 ) BEGIN              // cycle through abilities
          READ_SHORT  ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
          READ_SHORT  ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
          FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
            READ_BYTE ("%fx_off%" + 0x0d + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "dispel"
            PATCH_IF NOT ("%dispel%" = 3) BEGIN
              WRITE_BYTE ("%fx_off%" + 0x0d + (0x30 * ("%index2%" + "%abil_fx_idx%"))) 3 // make each effect dispellable and let it bypasses magic resistance
            END      
          END
        END
        PATCH_INCLUDE ~aTweaks/lib/fj_spl_itm_reindex.tpp~                         // Nythrun's SPL/ITM re-indexer macro (needed for Global Effects)
      END
    BUT_ONLY_IF_IT_CHANGES
  END
END
