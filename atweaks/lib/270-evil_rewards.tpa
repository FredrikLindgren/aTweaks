
ACTION_IF ENGINE_IS ~bgee bg2ee~ BEGIN
  OUTER_SPRINT bgee "/bgee"
END ELSE OUTER_SPRINT bgee ""

DEFINE_ACTION_FUNCTION work_around_function BEGIN
  //I had hoped LOAD_TRA would not modify the global environment inside a function, but it does,
  //so we need to undo it by loading the main tra at the end
  LOAD_TRA ~atweaks/tra%bgee%/english/evil_rewards.tra~ ~atweaks/tra%bgee%/%LANGUAGE%/evil_rewards.tra~
  
  COPY_EXISTING misc9v.itm override
    SAY IDENTIFIED_DESC @70
  BUT_ONLY

  COPY_EXISTING baldur.bcs override
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY ~\(SetGlobal("drowEraseJournal","GLOBAL",2)\)~ ~EraseJournalEntry(@58) EraseJournalEntry(@59) \1~
    END
  BUT_ONLY

  LOAD_TRA ~atweaks/tra%bgee%/english/atweaks.tra~ ~atweaks/tra%bgee%/%LANGUAGE%/atweaks.tra~
END

COMPILE ~atweaks/dlg/evil_rewards.d~ USING
        ~atweaks/tra%bgee%/english/evil_rewards.tra~
        ~atweaks/tra%bgee%/%LANGUAGE%/evil_rewards.tra~

//Trademeet
COPY_EXISTING bolt08.itm override
  WRITE_SHORT 0x42 55 //Lore to identify
BUT_ONLY

COPY_EXISTING misc3f.itm override
  WRITE_SHORT 0x42 60 //Lore to identify
  GET_OFFSET_ARRAY ab_array ITM_V10_HEADERS
  PHP_EACH ab_array AS i => off BEGIN
    WRITE_BYTE off + 0x1 1 //Identify before use
  END
BUT_ONLY


//Ust Natha
COPY_EXISTING udimp.cre ~override/fl#udimp.cre~
  WRITE_ASCIIL 0x248 ~~ ~~ ~~ ~~ ~~ 
  WRITE_ASCII  0x280 fl#udimp #20
  WRITE_ASCII  0x2cc fl#udimp #8

EXTEND_BOTTOM udsoltri.bcs "atweaks/baf/fake_eggs.baf"
LAF work_around_function END


//Saradush
ACTION_IF GAME_INCLUDES ~tob~ BEGIN
  COPY_EXISTING xplist.2da override //Change quest XP reward for Kiser's quest to 7000
    REPLACE_TEXTUALLY CASE_INSENSITIVE ~\(PLOT8B[%TAB% ]+Saved_Ardic_Bad_Sarson01\)[%TAB% ]+5000~
                                       ~\1 7000~
  BUT_ONLY

  COPY_EXISTING sarvie01.cre override
    WRITE_LONG 0x14 9000 //make the Viekang pi√±ata hold 9k xp instead of 6.3k
    WRITE_ASCII 0x248 ~~ #8 //Vampire courtesans use their own shout code, so why should Viekang shout the same as the commoners?
  BUT_ONLY
END
