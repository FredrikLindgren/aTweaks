IF
    Global("RR#Active","LOCALS",0)
    !Allegiance(Myself,GOODCUTOFF)
    OR(6)
      See(NearestEnemyOf(Myself))
      AttackedBy([ANYONE],DEFAULT)
      TookDamage()
      Help([EVILCUTOFF.0.ELEMENTAL.ELEMENTAL_EARTH])
	  Gender(Myself,SUMMONED) // summoned elemental
      Exists(LastSummonerOf(Myself)) // gated elemental
THEN
    RESPONSE #100
        SetGlobal("RR#Active","LOCALS",1)
        SetGlobalTimer("RR#Rest","LOCALS",2400) // 8 hours
        Enemy()
        Help()
        Continue()
END

IF
    Global("RR#Active","LOCALS",0)
    Allegiance(Myself,GOODCUTOFF)
THEN
    RESPONSE #100
        SetGlobal("RR#Active","LOCALS",1)
        Continue()
END

IF
    Global("rr#earth","LOCALS",0) // area type check (0 - contains earth, 1 - inappropriate area, 2 - check done)
    HaveSpellRES("rr#ecncl") // Elemental Concealment
    OR(4)
      AreaCheck("AR2800") // Suldanesselar
      AreaCheck("AR2806") // Tree of Life
      AreaCheck("AR0516") // Planar Prison
      AreaType(CITY)
THEN
    RESPONSE #100
        SetGlobal("rr#earth","LOCALS",1)
END

IF
    Global("rr#earth","LOCALS",1) // area type check (0 - contains earth, 1 - inappropriate area, 2 - check done)
    HaveSpellRES("rr#ecncl") // Elemental Concealment
THEN
    RESPONSE #100
        SetGlobal("rr#earth","LOCALS",2)
        RemoveSpellRES("rr#ecncl") // remove the spell from memory
END

IF
    OR(3)
      Global("RR#Active","LOCALS",0)
      StateCheck(Myself,STATE_IMMOBILE) // using this instead of STATE_DISABLED because the latter includes STATE_CHARMED
      StateCheck(Myself,STATE_REALLY_DEAD) // prevents contingencies and other ReallyForceSpell() stuff from popping up on dead creatures
THEN
    RESPONSE #100
        NoAction()
END

IF
    StateCheck(Myself,STATE_PANIC)  // needed because the AI can perform script actions (i.e. drink potions) while panicked, unlike the player
THEN
    RESPONSE #100
        RandomWalkContinuous() // using this instead of NoAction() because otherwise panicked characters would stand in place
END

IF
    Global("RR#Active","LOCALS",1)
    !InActiveArea(Myself)
    !See(NearestEnemyOf(Myself))
THEN
    RESPONSE #100
        NoAction()
END

IF
    !Allegiance(Myself,GOODCUTOFF)
    !GlobalTimerNotExpired("RR#Rest","LOCALS") // re-initialize the rest timer
THEN
    RESPONSE #100
        SetGlobalTimer("RR#Rest","LOCALS",2400) // 8 hours
        ApplySpell(Myself,RESTORE_FULL_HEALTH) // heal up
        Rest()
END

IF
    !Allegiance(Myself,GOODCUTOFF)
    !GlobalTimerNotExpired("RR#Help","LOCALS") // shout for help every two rounds while in combat, other elementals may hear it and join the battle
    See(NearestEnemyOf(Myself))
THEN
    RESPONSE #100
        SetGlobalTimer("RR#Help","LOCALS",12)
        Help()
        Continue()
END

IF
    Allegiance(Myself,EVILCUTOFF)
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    HaveSpellRES("rr#equa") // Earthquake
    NumCreatureGT([GOODCUTOFF],2) // at least three opponents are nearby (note: the NumCreatureGT() trigger doesn't work with NearestEnemyOf(Myself))
    Range([GOODCUTOFF],12)
    Range(SecondNearest([GOODCUTOFF]),12)
    Range(ThirdNearest([GOODCUTOFF]),12)
    OR(3)
      CheckStatGT([GOODCUTOFF],4,LEVEL) // don't waste Earthquake on low level opponents
      CheckStatGT([GOODCUTOFF],4,LEVEL2)
      CheckStatGT([GOODCUTOFF],4,LEVEL3)
   !Class([GOODCUTOFF],ELEMENTAL_AIR) // unaffected by Earthquake
   !Class([GOODCUTOFF],ELEMENTAL_EARTH) // unaffected by Earthquake
THEN
    RESPONSE #100
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#equa",Myself) // Earthquake
        RemoveSpellRES("rr#equa") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    Allegiance(Myself,EVILCUTOFF)
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    !GlobalTimerNotExpired("rr#equa","LOCALS") // Earthquake timer
    HaveSpellRES("rr#equa") // Earthquake
    NumCreatureGT([GOODCUTOFF],1) // at least two opponents are nearby (note: the NumCreatureGT() trigger doesn't work with NearestEnemyOf(Myself))
    Range([GOODCUTOFF],12)
    Range(SecondNearest([GOODCUTOFF]),12)
    OR(3)
      CheckStatGT([GOODCUTOFF],4,LEVEL) // don't waste Earthquake on low level opponents
      CheckStatGT([GOODCUTOFF],4,LEVEL2)
      CheckStatGT([GOODCUTOFF],4,LEVEL3)
   !Class([GOODCUTOFF],ELEMENTAL_AIR) // unaffected by Earthquake
   !Class([GOODCUTOFF],ELEMENTAL_EARTH) // unaffected by Earthquake
THEN
    RESPONSE #70
        SetGlobalTimer("rr#equa","LOCALS",12) // Earthquake timer
    RESPONSE #30
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#equa",Myself) // Earthquake
        RemoveSpellRES("rr#equa") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    Allegiance(Myself,EVILCUTOFF)
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    !GlobalTimerNotExpired("rr#equa","LOCALS") // Earthquake timer
    HaveSpellRES("rr#equa") // Earthquake
    Range([GOODCUTOFF],12) // at least one opponent is nearby
    OR(3)
      CheckStatGT([GOODCUTOFF],4,LEVEL) // don't waste Earthquake on low level opponents
      CheckStatGT([GOODCUTOFF],4,LEVEL2)
      CheckStatGT([GOODCUTOFF],4,LEVEL3)
   !Class([GOODCUTOFF],ELEMENTAL_AIR) // unaffected by Earthquake
   !Class([GOODCUTOFF],ELEMENTAL_EARTH) // unaffected by Earthquake
THEN
    RESPONSE #85
        SetGlobalTimer("rr#equa","LOCALS",12) // Earthquake timer
    RESPONSE #15
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#equa",Myself) // Earthquake
        RemoveSpellRES("rr#equa") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    Allegiance(Myself,EVILCUTOFF)
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    !GlobalTimerNotExpired("rr#equa","LOCALS") // Earthquake timer
    HaveSpellRES("rr#equa") // Earthquake
    Range([PC],12) // special block, for targeting singled-out party members (i.e. solo players)
    OR(2)
      NumCreatureGT([PC],1) // at least two party members are nearby (note: the NumCreatureGT() trigger doesn't work with NearestEnemyOf(Myself))
      NumInPartyAlive(1) // or a solo player has been detected
    See([PC])
    !StateCheck(LastSeenBy(Myself),STATE_DISABLED)
    CheckStatGT(LastSeenBy(Myself),-6,SAVEVSSPELL)
    !CheckStatGT(LastSeenBy(Myself),0,CLERIC_CHAOTIC_COMMANDS)
    !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE4) // Barbarian Rage, Berserker Enrage
    !CheckStat(LastSeenBy(Myself),8,WIZARD_SPELL_IMMUNITY)  // Spell Immunity: Alteration
THEN
    RESPONSE #100
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#equa",Myself) // Earthquake
        RemoveSpellRES("rr#equa") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    Allegiance(Myself,EVILCUTOFF)
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    HaveSpellRES("rr#ecncl") // Elemental Concealment
    !StateCheck(Myself,STATE_INVISIBLE)
    !Global("dvgldust","LOCALS",1)
    !CheckStatGT(Player1,0,TRUE_SIGHT)
    !CheckStatGT(Player2,0,TRUE_SIGHT)
    !CheckStatGT(Player3,0,TRUE_SIGHT)
    !CheckStatGT(Player4,0,TRUE_SIGHT)
    !CheckStatGT(Player5,0,TRUE_SIGHT)
    !CheckStatGT(Player6,0,TRUE_SIGHT)
    !CheckStatGT([GOODCUTOFF.0.PLANATAR],0,TRUE_SIGHT)
    !CheckStatGT([GOODCUTOFF.0.DARKPLANATAR],0,TRUE_SIGHT)
THEN
    RESPONSE #100
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#ecncl",Myself) // Elemental Concealment
        RemoveSpellRES("rr#ecncl") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    ActionListEmpty()
    Allegiance(Myself,GOODCUTOFF)
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    HaveSpellRES("rr#equa") // Earthquake
    NumCreatureGT([EVILCUTOFF],2) // at least three opponents are nearby (note: the NumCreatureGT() trigger doesn't work with NearestEnemyOf(Myself))
    Range([EVILCUTOFF],12)
    Range(SecondNearest([EVILCUTOFF]),12)
    Range(ThirdNearest([EVILCUTOFF]),12)
    OR(3)
      CheckStatGT([EVILCUTOFF],4,LEVEL) // don't waste Earthquake on low level opponents
      CheckStatGT([EVILCUTOFF],4,LEVEL2)
      CheckStatGT([EVILCUTOFF],4,LEVEL3)
   !Class([EVILCUTOFF],ELEMENTAL_AIR) // unaffected by Earthquake
   !Class([EVILCUTOFF],ELEMENTAL_EARTH) // unaffected by Earthquake
THEN
    RESPONSE #100
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#equa",Myself) // Earthquake
        RemoveSpellRES("rr#equa") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    ActionListEmpty()
    Allegiance(Myself,GOODCUTOFF)
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    !GlobalTimerNotExpired("rr#equa","LOCALS") // Earthquake timer
    HaveSpellRES("rr#equa") // Earthquake
    NumCreatureGT([EVILCUTOFF],1) // at least two opponents are nearby (note: the NumCreatureGT() trigger doesn't work with NearestEnemyOf(Myself))
    Range([EVILCUTOFF],12)
    Range(SecondNearest([EVILCUTOFF]),12)
    OR(3)
      CheckStatGT([EVILCUTOFF],4,LEVEL) // don't waste Earthquake on low level opponents
      CheckStatGT([EVILCUTOFF],4,LEVEL2)
      CheckStatGT([EVILCUTOFF],4,LEVEL3)
   !Class([EVILCUTOFF],ELEMENTAL_AIR) // unaffected by Earthquake
   !Class([EVILCUTOFF],ELEMENTAL_EARTH) // unaffected by Earthquake
THEN
    RESPONSE #70
        SetGlobalTimer("rr#equa","LOCALS",12) // Earthquake timer
    RESPONSE #30
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#equa",Myself) // Earthquake
        RemoveSpellRES("rr#equa") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    ActionListEmpty()
    Allegiance(Myself,GOODCUTOFF)
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    !GlobalTimerNotExpired("rr#equa","LOCALS") // Earthquake timer
    HaveSpellRES("rr#equa") // Earthquake
    Range([EVILCUTOFF],12) // at least one opponent is nearby
    OR(3)
      CheckStatGT([EVILCUTOFF],4,LEVEL) // don't waste Earthquake on low level opponents
      CheckStatGT([EVILCUTOFF],4,LEVEL2)
      CheckStatGT([EVILCUTOFF],4,LEVEL3)
   !Class([EVILCUTOFF],ELEMENTAL_AIR) // unaffected by Earthquake
   !Class([EVILCUTOFF],ELEMENTAL_EARTH) // unaffected by Earthquake
THEN
    RESPONSE #85
        SetGlobalTimer("rr#equa","LOCALS",12) // Earthquake timer
    RESPONSE #15
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#equa",Myself) // Earthquake
        RemoveSpellRES("rr#equa") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    ActionListEmpty()
    Allegiance(Myself,GOODCUTOFF)
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    HaveSpellRES("rr#ecncl") // Elemental Concealment
    !StateCheck(Myself,STATE_INVISIBLE)
    !Global("dvgldust","LOCALS",1)
    !CheckStatGT(NearestEnemyOf(Myself),0,TRUE_SIGHT)
    !CheckStatGT(SecondNearestEnemyOf(Myself),0,TRUE_SIGHT)
    !CheckStatGT(ThirdNearestEnemyOf(Myself),0,TRUE_SIGHT)
    !CheckStatGT(FourthNearestEnemyOf(Myself),0,TRUE_SIGHT)
    !CheckStatGT(SixthNearestEnemyOf(Myself),0,TRUE_SIGHT)
    !CheckStatGT(SeventhNearestEnemyOf(Myself),0,TRUE_SIGHT)
    !CheckStatGT(EighthNearestEnemyOf(Myself),0,TRUE_SIGHT)
    !CheckStatGT(NinthNearestEnemyOf(Myself),0,TRUE_SIGHT)
    !CheckStatGT(TenthNearestEnemyOf(Myself),0,TRUE_SIGHT)
    !CheckStatGT([EVILCUTOFF.0.PLANATAR],0,TRUE_SIGHT)
    !CheckStatGT([EVILCUTOFF.0.DARKPLANATAR],0,TRUE_SIGHT)
THEN
    RESPONSE #100
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#ecncl",Myself) // Elemental Concealment
        RemoveSpellRES("rr#ecncl") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    Allegiance(Myself,GOODCUTOFF) // only needed for summoned elementals
    !GlobalTimerNotExpired("rr#clear","LOCALS") // prevent the creature from getting stuck in an attack pattern because ActionListEmpty() is used above
    See(NearestEnemyOf(Myself))
THEN
    RESPONSE #100
        SetGlobalTimer("rr#clear","LOCALS",6)
        ClearActions(Myself)
END

IF
    Allegiance(Myself,EVILCUTOFF)
    OR(6)  // note: this block specifically checks for Players in order to target unconscious opponents (if any) since they can't be seen otherwise
      See(Player1)
      See(Player2)
      See(Player3)
      See(Player4)
      See(Player5)
      See(Player6)
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(NearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    CheckStatLT(NearestEnemyOf(Myself),100,RESISTCRUSHING)
    See(NearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(SixthNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    !StateCheck(SixthNearestEnemyOf(Myself),STATE_PANIC)
    CheckStatLT(SixthNearestEnemyOf(Myself),100,RESISTCRUSHING)
    CheckStatLT(SixthNearestEnemyOf(Myself),1,STONESKINS)
    !StateCheck(SixthNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    See(SixthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(FifthNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    !StateCheck(FifthNearestEnemyOf(Myself),STATE_PANIC)
    CheckStatLT(FifthNearestEnemyOf(Myself),100,RESISTCRUSHING)
    CheckStatLT(FifthNearestEnemyOf(Myself),1,STONESKINS)
    !StateCheck(FifthNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    See(FifthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(FourthNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    !StateCheck(FourthNearestEnemyOf(Myself),STATE_PANIC)
    CheckStatLT(FourthNearestEnemyOf(Myself),100,RESISTCRUSHING)
    CheckStatLT(FourthNearestEnemyOf(Myself),1,STONESKINS)
    !StateCheck(FourthNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    See(FourthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(ThirdNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    !StateCheck(ThirdNearestEnemyOf(Myself),STATE_PANIC)
    CheckStatLT(ThirdNearestEnemyOf(Myself),100,RESISTCRUSHING)
    CheckStatLT(ThirdNearestEnemyOf(Myself),1,STONESKINS)
    !StateCheck(ThirdNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    See(ThirdNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(SecondNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    !StateCheck(SecondNearestEnemyOf(Myself),STATE_PANIC)
    CheckStatLT(SecondNearestEnemyOf(Myself),100,RESISTCRUSHING)
    CheckStatLT(SecondNearestEnemyOf(Myself),1,STONESKINS)
    !StateCheck(SecondNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    See(SecondNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(NearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    !StateCheck(NearestEnemyOf(Myself),STATE_PANIC)
    CheckStatLT(NearestEnemyOf(Myself),100,RESISTCRUSHING)
    CheckStatLT(NearestEnemyOf(Myself),1,STONESKINS)
    !StateCheck(NearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    See(NearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    Allegiance(Myself,EVILCUTOFF)
    !StateCheck(WorstAC([PC]),STATE_PANIC)
    !StateCheck(WorstAC([PC]),STATE_MIRRORIMAGE)
    !CheckStatGT(WorstAC([PC]),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    CheckStatLT(WorstAC([PC]),100,RESISTCRUSHING)
    CheckStatLT(WorstAC([PC]),1,STONESKINS)
    See(WorstAC([PC]))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    AttackedBy(NearestEnemyOf(Myself),DEFAULT)
    !CheckStatGT(LastAttackerOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    !StateCheck(LastAttackerOf(Myself),STATE_PANIC)
    CheckStatLT(LastAttackerOf(Myself),100,RESISTCRUSHING)
    !StateCheck(LastAttackerOf(Myself),STATE_MIRRORIMAGE)
    CheckStatLT(LastAttackerOf(Myself),1,STONESKINS)
    Range(LastAttackerOf(Myself),8)
    RandomNum(3,1)
    See(LastAttackerOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    AttackedBy(NearestEnemyOf(Myself),DEFAULT)
    !CheckStatGT(LastAttackerOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    !StateCheck(LastAttackerOf(Myself),STATE_PANIC)
    CheckStatLT(LastAttackerOf(Myself),100,RESISTCRUSHING)
    !StateCheck(LastAttackerOf(Myself),STATE_MIRRORIMAGE)
    CheckStatLT(LastAttackerOf(Myself),1,STONESKINS)
    Range(LastAttackerOf(Myself),4)
    See(LastAttackerOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(SixthNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(SixthNearestEnemyOf(Myself),STATE_IMMOBILE)
    CheckStatLT(SixthNearestEnemyOf(Myself),100,RESISTCRUSHING)
    See(SixthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(FifthNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(FifthNearestEnemyOf(Myself),STATE_IMMOBILE)
    CheckStatLT(FifthNearestEnemyOf(Myself),100,RESISTCRUSHING)
    See(FifthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(FourthNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(FourthNearestEnemyOf(Myself),STATE_IMMOBILE)
    CheckStatLT(FourthNearestEnemyOf(Myself),100,RESISTCRUSHING)
    See(FourthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(ThirdNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(ThirdNearestEnemyOf(Myself),STATE_IMMOBILE)
    CheckStatLT(ThirdNearestEnemyOf(Myself),100,RESISTCRUSHING)
    See(ThirdNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(SecondNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(SecondNearestEnemyOf(Myself),STATE_IMMOBILE)
    CheckStatLT(SecondNearestEnemyOf(Myself),100,RESISTCRUSHING)
    See(SecondNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(NearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(NearestEnemyOf(Myself),STATE_IMMOBILE)
    CheckStatLT(NearestEnemyOf(Myself),100,RESISTCRUSHING)
    See(NearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    Allegiance(Myself,EVILCUTOFF) // note: special blocks for targeting unconscious PCs. The AI can See() an unconscious PlayerX, but not an unconscious [PC] etc.
    !CheckStatGT(Player6,3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(Player6,STATE_IMMOBILE)
    CheckStatLT(Player6,100,RESISTCRUSHING)
    See(Player6)
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    Allegiance(Myself,EVILCUTOFF) // note: special blocks for targeting unconscious PCs. The AI can See() an unconscious PlayerX, but not an unconscious [PC] etc.
    !CheckStatGT(Player5,3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(Player5,STATE_IMMOBILE)
    CheckStatLT(Player5,100,RESISTCRUSHING)
    See(Player5)
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    Allegiance(Myself,EVILCUTOFF) // note: special blocks for targeting unconscious PCs. The AI can See() an unconscious PlayerX, but not an unconscious [PC] etc.
    !CheckStatGT(Player4,3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(Player4,STATE_IMMOBILE)
    CheckStatLT(Player4,100,RESISTCRUSHING)
    See(Player4)
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    Allegiance(Myself,EVILCUTOFF) // note: special blocks for targeting unconscious PCs. The AI can See() an unconscious PlayerX, but not an unconscious [PC] etc.
    !CheckStatGT(Player3,3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(Player3,STATE_IMMOBILE)
    CheckStatLT(Player3,100,RESISTCRUSHING)
    See(Player3)
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    Allegiance(Myself,EVILCUTOFF) // note: special blocks for targeting unconscious PCs. The AI can See() an unconscious PlayerX, but not an unconscious [PC] etc.
    !CheckStatGT(Player2,3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(Player2,STATE_IMMOBILE)
    CheckStatLT(Player2,100,RESISTCRUSHING)
    See(Player2)
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    Allegiance(Myself,EVILCUTOFF) // note: special blocks for targeting unconscious PCs. The AI can See() an unconscious PlayerX, but not an unconscious [PC] etc.
    !CheckStatGT(Player1,3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(Player1,STATE_IMMOBILE)
    CheckStatLT(Player1,100,RESISTCRUSHING)
    See(Player1)
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    Allegiance(Myself,EVILCUTOFF)
    Global("Elementalcontrol","LOCALS",2) // summoned elemental out of control
    See(LastSummonerOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
	Global("RR#NOPND","GLOBAL",0) // opening doors is allowed
    Allegiance(Myself,EVILCUTOFF) // humanoids and some intelligent monsters can open unlocked doors
    !AreaType(OUTDOOR)
    !Range(NearestEnemyOf(Myself),30)
    Range(NearestDoor,15) // if the nearest door is in visual range. note: this seems to auto-skip locked doors
    OpenState(NearestDoor,FALSE)
THEN
    RESPONSE #100
        MoveToObject(NearestDoor)
        OpenDoor(NearestDoor)
END

IF
    Exists(LastSeenBy(Myself)) // safety check
    Allegiance(Myself,EVILCUTOFF)
    Allegiance(LastSeenBy(Myself),GOODCUTOFF) // safety check
    !StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
    !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)    
    CheckStatLT(LastSeenBy(Myself),100,RESISTCRUSHING)
THEN
    RESPONSE #100
        AttackReevaluate(LastSeenBy(Myself),15)
END

IF
    Exists(LastSeenBy(Myself)) // safety check
    Allegiance(Myself,GOODCUTOFF)
    ActionListEmpty()
    !InParty(LastSeenBy(Myself))
    Allegiance(LastSeenBy(Myself),EVILCUTOFF)
    !StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
    !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
    CheckStatLT(LastSeenBy(Myself),100,RESISTCRUSHING)
THEN
    RESPONSE #100
        AttackReevaluate(LastSeenBy(Myself),15)
END

IF
    Allegiance(Myself,EVILCUTOFF)
    !GlobalTimerNotExpired("RR#Move","LOCALS") // help -> move timer
    Help([EVILCUTOFF.0.ELEMENTAL.ELEMENTAL_EARTH])
    InMyArea(LastHelp(Myself))
    !StateCheck(LastHelp(Myself),STATE_REALLY_DEAD)
THEN
    RESPONSE #100
        SetGlobalTimer("RR#Move","LOCALS",6)
        MoveToObject(LastHelp(Myself))
END

IF
    Allegiance(Myself,EVILCUTOFF) // only RandomWalk if hostile
    !GlobalTimerNotExpired("RR#Move","LOCALS")
THEN
    RESPONSE #100
        RandomWalkContinuous()
END
