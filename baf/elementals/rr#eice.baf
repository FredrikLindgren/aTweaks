IF
    HasItem("RR#DINV",Myself) // invisibility item which prevents enemies from targeting not-yet-gated creatures
THEN
    RESPONSE #100
        DestroyItem("RR#DINV")
        Continue()
END

IF
    Global("RR#Active","LOCALS",0)
    !Allegiance(Myself,GOODCUTOFF)
    OR(6)
      See(NearestEnemyOf(Myself))
      AttackedBy([ANYONE],DEFAULT)
      TookDamage()
      Help([EVILCUTOFF.0.ELEMENTAL])
	  Gender(Myself,SUMMONED) // summoned elemental
      Exists(LastSummonerOf(Myself)) // gated elemental
THEN
    RESPONSE #100
        SetGlobal("RR#Active","LOCALS",1)
        SetGlobalTimer("RR#Rest","LOCALS",2400) // 8 hours
        Enemy()
        Help()
        Continue()
END

IF
    Global("RR#Active","LOCALS",0)
    Allegiance(Myself,EVILCUTOFF)
    Exists(LastSummonerOf(Myself)) // summoned by Cryonax
THEN
    RESPONSE #100
        SetGlobal("RR#Active","LOCALS",1)
        Continue()
END

IF
    Global("RR#Active","LOCALS",0)
    Allegiance(Myself,GOODCUTOFF)
THEN
    RESPONSE #100
        SetGlobal("RR#Active","LOCALS",1)
        Continue()
END

IF
    OR(3)
      Global("RR#Active","LOCALS",0)
      StateCheck(Myself,STATE_IMMOBILE) // using this instead of STATE_DISABLED because the latter includes STATE_CHARMED
      StateCheck(Myself,STATE_REALLY_DEAD) // prevents contingencies and other ReallyForceSpell() stuff from popping up on dead creatures
THEN
    RESPONSE #100
        NoAction()
END

IF
    StateCheck(Myself,STATE_PANIC)  // needed because the AI can perform script actions (i.e. drink potions) while panicked, unlike the player
THEN
    RESPONSE #100
        RandomWalkContinuous() // using this instead of NoAction() because otherwise panicked characters would stand in place
END

IF
    Global("RR#Active","LOCALS",1)
    !InActiveArea(Myself)
    !See(NearestEnemyOf(Myself))
THEN
    RESPONSE #100
        NoAction()
END

IF
    !Allegiance(Myself,GOODCUTOFF)
    !GlobalTimerNotExpired("RR#Rest","LOCALS") // re-initialize the rest timer
THEN
    RESPONSE #100
        SetGlobalTimer("RR#Rest","LOCALS",2400) // 8 hours
        ApplySpell(Myself,RESTORE_FULL_HEALTH) // heal up
        Rest()
END

IF
    !Allegiance(Myself,GOODCUTOFF)
    !GlobalTimerNotExpired("RR#Help","LOCALS") // shout for help every two rounds while in combat, other elementals may hear it and join the battle
    See(NearestEnemyOf(Myself))
THEN
    RESPONSE #100
        SetGlobalTimer("RR#Help","LOCALS",12)
        Help()
        Continue()
END

IF
    Allegiance(Myself,EVILCUTOFF)
    OR(6)  // note: this block specifically checks for Players in order to target unconscious opponents (if any) since they can't be seen otherwise
      See(Player1)
      See(Player2)
      See(Player3)
      See(Player4)
      See(Player5)
      See(Player6)
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(NearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    CheckStatLT(NearestEnemyOf(Myself),121,RESISTCOLD) // don't attack creatures that are healed by cold damage
    See(NearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(SixthNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    !StateCheck(SixthNearestEnemyOf(Myself),STATE_PANIC)
    CheckStatLT(SixthNearestEnemyOf(Myself),100,RESISTCOLD)
    !StateCheck(SixthNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    See(SixthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(FifthNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    !StateCheck(FifthNearestEnemyOf(Myself),STATE_PANIC)
    CheckStatLT(FifthNearestEnemyOf(Myself),100,RESISTCOLD)
    !StateCheck(FifthNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    See(FifthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(FourthNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    !StateCheck(FourthNearestEnemyOf(Myself),STATE_PANIC)
    CheckStatLT(FourthNearestEnemyOf(Myself),100,RESISTCOLD)
    !StateCheck(FourthNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    See(FourthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(ThirdNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    !StateCheck(ThirdNearestEnemyOf(Myself),STATE_PANIC)
    CheckStatLT(ThirdNearestEnemyOf(Myself),100,RESISTCOLD)
    !StateCheck(ThirdNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    See(ThirdNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(SecondNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    !StateCheck(SecondNearestEnemyOf(Myself),STATE_PANIC)
    CheckStatLT(SecondNearestEnemyOf(Myself),100,RESISTCOLD)
    !StateCheck(SecondNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    See(SecondNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(NearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    !StateCheck(NearestEnemyOf(Myself),STATE_PANIC)
    CheckStatLT(NearestEnemyOf(Myself),100,RESISTCOLD)
    !StateCheck(NearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    See(NearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    Allegiance(Myself,EVILCUTOFF)
    !StateCheck(WorstAC([PC]),STATE_PANIC)
    !StateCheck(WorstAC([PC]),STATE_MIRRORIMAGE)
    !CheckStatGT(WorstAC([PC]),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    CheckStatLT(WorstAC([PC]),100,RESISTCOLD)
    See(WorstAC([PC]))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    AttackedBy(NearestEnemyOf(Myself),DEFAULT)
    !CheckStatGT(LastAttackerOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    !StateCheck(LastAttackerOf(Myself),STATE_PANIC)
    CheckStatLT(LastAttackerOf(Myself),100,RESISTCOLD)
    !StateCheck(LastAttackerOf(Myself),STATE_MIRRORIMAGE)
    Range(LastAttackerOf(Myself),8)
    RandomNum(3,1)
    See(LastAttackerOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    AttackedBy(NearestEnemyOf(Myself),DEFAULT)
    !CheckStatGT(LastAttackerOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    !StateCheck(LastAttackerOf(Myself),STATE_PANIC)
    CheckStatLT(LastAttackerOf(Myself),100,RESISTCOLD)
    !StateCheck(LastAttackerOf(Myself),STATE_MIRRORIMAGE)
    Range(LastAttackerOf(Myself),4)
    See(LastAttackerOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(SixthNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(SixthNearestEnemyOf(Myself),STATE_IMMOBILE)
    CheckStatLT(SixthNearestEnemyOf(Myself),100,RESISTCOLD)
    See(SixthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(FifthNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(FifthNearestEnemyOf(Myself),STATE_IMMOBILE)
    CheckStatLT(FifthNearestEnemyOf(Myself),100,RESISTCOLD)
    See(FifthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(FourthNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(FourthNearestEnemyOf(Myself),STATE_IMMOBILE)
    CheckStatLT(FourthNearestEnemyOf(Myself),100,RESISTCOLD)
    See(FourthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(ThirdNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(ThirdNearestEnemyOf(Myself),STATE_IMMOBILE)
    CheckStatLT(ThirdNearestEnemyOf(Myself),100,RESISTCOLD)
    See(ThirdNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(SecondNearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(SecondNearestEnemyOf(Myself),STATE_IMMOBILE)
    CheckStatLT(SecondNearestEnemyOf(Myself),100,RESISTCOLD)
    See(SecondNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(NearestEnemyOf(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(NearestEnemyOf(Myself),STATE_IMMOBILE)
    CheckStatLT(NearestEnemyOf(Myself),100,RESISTCOLD)
    See(NearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    Allegiance(Myself,EVILCUTOFF) // note: special blocks for targeting unconscious PCs. The AI can See() an unconscious PlayerX, but not an unconscious [PC] etc.
    !CheckStatGT(Player6,3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(Player6,STATE_IMMOBILE)
    CheckStatLT(Player6,100,RESISTCOLD)
    See(Player6)
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    Allegiance(Myself,EVILCUTOFF) // note: special blocks for targeting unconscious PCs. The AI can See() an unconscious PlayerX, but not an unconscious [PC] etc.
    !CheckStatGT(Player5,3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(Player5,STATE_IMMOBILE)
    CheckStatLT(Player5,100,RESISTCOLD)
    See(Player5)
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    Allegiance(Myself,EVILCUTOFF) // note: special blocks for targeting unconscious PCs. The AI can See() an unconscious PlayerX, but not an unconscious [PC] etc.
    !CheckStatGT(Player4,3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(Player4,STATE_IMMOBILE)
    CheckStatLT(Player4,100,RESISTCOLD)
    See(Player4)
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    Allegiance(Myself,EVILCUTOFF) // note: special blocks for targeting unconscious PCs. The AI can See() an unconscious PlayerX, but not an unconscious [PC] etc.
    !CheckStatGT(Player3,3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(Player3,STATE_IMMOBILE)
    CheckStatLT(Player3,100,RESISTCOLD)
    See(Player3)
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    Allegiance(Myself,EVILCUTOFF) // note: special blocks for targeting unconscious PCs. The AI can See() an unconscious PlayerX, but not an unconscious [PC] etc.
    !CheckStatGT(Player2,3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(Player2,STATE_IMMOBILE)
    CheckStatLT(Player2,100,RESISTCOLD)
    See(Player2)
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    Allegiance(Myself,EVILCUTOFF) // note: special blocks for targeting unconscious PCs. The AI can See() an unconscious PlayerX, but not an unconscious [PC] etc.
    !CheckStatGT(Player1,3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
    StateCheck(Player1,STATE_IMMOBILE)
    CheckStatLT(Player1,100,RESISTCOLD)
    See(Player1)
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
	Global("RR#NOPND","GLOBAL",0) // opening doors is allowed
    Allegiance(Myself,EVILCUTOFF) // humanoids and some intelligent monsters can open unlocked doors
    !AreaType(OUTDOOR)
    !Range(NearestEnemyOf(Myself),30)
    Range(NearestDoor,15) // if the nearest door is in visual range. note: this seems to auto-skip locked doors
    OpenState(NearestDoor,FALSE)
THEN
    RESPONSE #100
        MoveToObject(NearestDoor)
        OpenDoor(NearestDoor)
END

IF
    Exists(LastSeenBy(Myself)) // safety check
    Allegiance(Myself,EVILCUTOFF)
    Allegiance(LastSeenBy(Myself),GOODCUTOFF) // safety check
    !StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
    !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
THEN
    RESPONSE #100
        AttackReevaluate(LastSeenBy(Myself),15)
END

IF
    Exists(LastSeenBy(Myself)) // safety check
    Allegiance(Myself,GOODCUTOFF)
    ActionListEmpty()
    !InParty(LastSeenBy(Myself))
    Allegiance(LastSeenBy(Myself),EVILCUTOFF)
    !StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
    !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
THEN
    RESPONSE #100
        AttackReevaluate(LastSeenBy(Myself),15)
END

IF
    Allegiance(Myself,EVILCUTOFF)
    !GlobalTimerNotExpired("RR#Move","LOCALS") // help -> move timer
    Help([EVILCUTOFF.0.ELEMENTAL])
    InMyArea(LastHelp(Myself))
    !StateCheck(LastHelp(Myself),STATE_REALLY_DEAD)
THEN
    RESPONSE #100
        SetGlobalTimer("RR#Move","LOCALS",6)
        MoveToObject(LastHelp(Myself))
END

IF
    Allegiance(Myself,EVILCUTOFF) // only RandomWalk if hostile
    !GlobalTimerNotExpired("RR#Move","LOCALS")
THEN
    RESPONSE #100
        RandomWalkContinuous()
END
