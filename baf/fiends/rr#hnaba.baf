
IF
  HasItem("dw#sumfi",Myself)
THEN
  RESPONSE #100
    DestroyItem("dw#sumfi")
    Continue()
END

IF
	Die()
        Global("RR#DGaze","LOCALS",1)
THEN
	RESPONSE #100
                SetGlobal("RR#DGaze","LOCALS",2)
                ReallyForceSpellDeadRES("RR#DGZRM",Player1) // Death Gaze removal
                ReallyForceSpellDeadRES("RR#DGZRM",Player2) // Death Gaze removal
                ReallyForceSpellDeadRES("RR#DGZRM",Player3) // Death Gaze removal
                ReallyForceSpellDeadRES("RR#DGZRM",Player4) // Death Gaze removal
                ReallyForceSpellDeadRES("RR#DGZRM",Player5) // Death Gaze removal
                ReallyForceSpellDeadRES("RR#DGZRM",Player6) // Death Gaze removal
END

IF
	Global("RR#Active","LOCALS",0)
	OR(2)
	  AttackedBy([ANYONE],DEFAULT)
	  TookDamage()
THEN
	RESPONSE #100
		SetGlobal("RR#Active","LOCALS",1)
		Enemy()
		Continue()
END

IF
	Global("RR#Active","LOCALS",0)
        Allegiance(Myself,EVILCUTOFF)
	See([GOODCUTOFF])
THEN
	RESPONSE #100
		SetGlobal("RR#Active","LOCALS",1)
		Continue()
END

IF
	OR(3)
	  Global("RR#Active","LOCALS",0)
	  StateCheck(Myself,STATE_REALLY_DEAD)
	  StateCheck(Myself,STATE_DISABLED)
THEN
	RESPONSE #100
		NoAction()
END

IF
	Global("RR#Active","LOCALS",1)
	!InActiveArea(Myself)
	!See([GOODCUTOFF])
THEN
	RESPONSE #100
		NoAction()
END

IF
        Global("RR#ETHPL","LOCALS",1) // Ethereal state marker (0 = Prime Material Plane, 1 = Ethereal Plane)
        HPPercent(Myself,100)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
                SetGlobal("RR#ETHPL","LOCALS",0) // Ethereal state marker (0 = Prime Material Plane, 1 = Ethereal Plane)
		ApplySpellRES("RR#UNETH",Myself) // Leaving the Ethereal Plane
		SetInterrupt(TRUE)
END

IF
        Global("RR#ETHPL","LOCALS",1) // Ethereal state marker (0 = Prime Material Plane, 1 = Ethereal Plane)
THEN
	RESPONSE #100
		RandomWalkContinuous()
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	GlobalLT("RR#Ethereal","LOCALS",2)
        Global("RR#ETHPL","LOCALS",0) // Ethereal state marker (0 = Prime Material Plane, 1 = Ethereal Plane)
        HPPercentLT(Myself,65)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		IncrementGlobal("RR#Ethereal","LOCALS",1)
                SetGlobal("RR#ETHPL","LOCALS",1) // Ethereal state marker (0 = Prime Material Plane, 1 = Ethereal Plane)
		SetGlobalTimer("RR#Cast","LOCALS",6)
		ForceSpellRES("RR#DNETH",Myself) // Etherealness + increased regeneration rate
		SetInterrupt(TRUE)
END

IF
        Global("RR#DGaze","LOCALS",1)
	Global("RR#GCtrl","LOCALS",0)
	See("RR#GHAST")
	Allegiance(LastSeenBy(Myself),NEUTRAL)
        Allegiance(Myself,EVILCUTOFF)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("RR#GCtrl","LOCALS",1)
		ChangeEnemyAlly(LastSeenBy(Myself),EVILCUTOFF)
		FaceObject(LastSeenBy(Myself))
		DisplayStringHead(Myself,@712) // Embrace the darkness fleshling! You now serve only me.
                DisplayStringHead(LastSeenBy(Myself),@713) // Yes, Master!
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	Exists(LastAttackerOf(Myself))
	!Range(LastAttackerOf(Myself),6)
        InMyArea(LastAttackerOf(Myself))
        Allegiance(LastAttackerOf(Myself),GOODCUTOFF)
	!StateCheck(LastAttackerOf(Myself),STATE_REALLY_DEAD)
        !StateCheck(LastAttackerOf(Myself),STATE_INVISIBLE)
	!CheckStatGT(LastAttackerOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
        CheckStatLT(LastAttackerOf(Myself),100,RESISTSLASHING)
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastAttackerOf(Myself)) // Teleport Without Error
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	StateCheck(NearestEnemyOf(Myself),STATE_IMMOBILE) // Note: these blocks are for high priority teleporting to immobile targets
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but they should not disrupt the Blood War
	!CheckStatGT(NearestEnemyOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
        CheckStatLT(NearestEnemyOf(Myself),100,RESISTSLASHING)
	!Range(NearestEnemyOf(Myself),6)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck(NearestEnemyOf(Myself),STATE_IMMOBILE) // Note: only teleport to other targets if the nearest enemy is not already immobile
	StateCheck(SecondNearestEnemyOf(Myself),STATE_IMMOBILE) // Note: these blocks are for high priority teleporting to immobile targets
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but they should not disrupt the Blood War
	!CheckStatGT(SecondNearestEnemyOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
        CheckStatLT(SecondNearestEnemyOf(Myself),100,RESISTSLASHING)
	!Range(SecondNearestEnemyOf(Myself),6)
	See(SecondNearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck(NearestEnemyOf(Myself),STATE_IMMOBILE) // Note: only teleport to other targets if the nearest enemy is not already immobile
	StateCheck(ThirdNearestEnemyOf(Myself),STATE_IMMOBILE) // Note: these blocks are for high priority teleporting to immobile targets
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but they should not disrupt the Blood War
	!CheckStatGT(ThirdNearestEnemyOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
        CheckStatLT(ThirdNearestEnemyOf(Myself),100,RESISTSLASHING)
	!Range(ThirdNearestEnemyOf(Myself),6)
	See(ThirdNearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck(NearestEnemyOf(Myself),STATE_IMMOBILE) // Note: only teleport to other targets if the nearest enemy is not already immobile
	StateCheck(FourthNearestEnemyOf(Myself),STATE_IMMOBILE) // Note: these blocks are for high priority teleporting to immobile targets
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but they should not disrupt the Blood War
	!CheckStatGT(FourthNearestEnemyOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
        CheckStatLT(FourthNearestEnemyOf(Myself),100,RESISTSLASHING)
	!Range(FourthNearestEnemyOf(Myself),6)
	See(FourthNearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck(NearestEnemyOf(Myself),STATE_IMMOBILE) // Note: only teleport to other targets if the nearest enemy is not already immobile
	StateCheck(FifthNearestEnemyOf(Myself),STATE_IMMOBILE) // Note: these blocks are for high priority teleporting to immobile targets
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but they should not disrupt the Blood War
	!CheckStatGT(FifthNearestEnemyOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
        CheckStatLT(FifthNearestEnemyOf(Myself),100,RESISTSLASHING)
	!Range(FifthNearestEnemyOf(Myself),6)
	See(FifthNearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck(NearestEnemyOf(Myself),STATE_IMMOBILE) // Note: only teleport to other targets if the nearest enemy is not already immobile
	StateCheck(SixthNearestEnemyOf(Myself),STATE_IMMOBILE) // Note: these blocks are for high priority teleporting to immobile targets
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but they should not disrupt the Blood War
	!CheckStatGT(SixthNearestEnemyOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
        CheckStatLT(SixthNearestEnemyOf(Myself),100,RESISTSLASHING)
	!Range(SixthNearestEnemyOf(Myself),6)
	See(SixthNearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		SetInterrupt(TRUE)
END

IF
	Global("RR#Paralyze","LOCALS",0)
	See([PC])
	Range(LastSeenBy(Myself),10)
THEN
	RESPONSE #100
		SetGlobal("RR#Paralyze","LOCALS",1)
                ForceSpellRES("RR#DPARL",Myself) // Aura of Paralysis
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        Global("RR#DGaze","LOCALS",0)
	See([PC.HUMANOID])
        CheckStatGT(LastSeenBy(Myself),0,SAVEVSSPELL)
	!HasItemEquiped("RR#HEL03",LastSeenBy(Myself)) // Darkveil (RR)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                SetGlobal("RR#DGaze","LOCALS",1)
		FaceObject(LastSeenBy(Myself))
                ForceSpellRES("RR#DGAZE",LastSeenBy(Myself)) // Death Gaze
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        Global("RR#DGaze","LOCALS",0)
	See(SecondNearest([PC.HUMANOID]))
        CheckStatGT(LastSeenBy(Myself),0,SAVEVSSPELL)
	!HasItemEquiped("RR#HEL03",LastSeenBy(Myself)) // Darkveil (RR)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                SetGlobal("RR#DGaze","LOCALS",1)
		FaceObject(LastSeenBy(Myself))
                ForceSpellRES("RR#DGAZE",LastSeenBy(Myself)) // Death Gaze
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        Global("RR#DGaze","LOCALS",0)
	See(ThirdNearest([PC.HUMANOID]))
        CheckStatGT(LastSeenBy(Myself),0,SAVEVSSPELL)
	!HasItemEquiped("RR#HEL03",LastSeenBy(Myself)) // Darkveil (RR)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                SetGlobal("RR#DGaze","LOCALS",1)
		FaceObject(LastSeenBy(Myself))
                ForceSpellRES("RR#DGAZE",LastSeenBy(Myself)) // Death Gaze
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        Global("RR#DGaze","LOCALS",0)
	See(FourthNearest([PC.HUMANOID]))
        CheckStatGT(LastSeenBy(Myself),0,SAVEVSSPELL)
	!HasItemEquiped("RR#HEL03",LastSeenBy(Myself)) // Darkveil (RR)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                SetGlobal("RR#DGaze","LOCALS",1)
		FaceObject(LastSeenBy(Myself))
                ForceSpellRES("RR#DGAZE",LastSeenBy(Myself)) // Death Gaze
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        Global("RR#DGaze","LOCALS",0)
	See(FifthNearest([PC.HUMANOID]))
        CheckStatGT(LastSeenBy(Myself),0,SAVEVSSPELL)
	!HasItemEquiped("RR#HEL03",LastSeenBy(Myself)) // Darkveil (RR)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                SetGlobal("RR#DGaze","LOCALS",1)
		FaceObject(LastSeenBy(Myself))
                ForceSpellRES("RR#DGAZE",LastSeenBy(Myself)) // Death Gaze
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        Global("RR#DGaze","LOCALS",0)
	See(SixthNearest([PC.HUMANOID]))
        CheckStatGT(LastSeenBy(Myself),0,SAVEVSSPELL)
	!HasItemEquiped("RR#HEL03",LastSeenBy(Myself)) // Darkveil (RR)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                SetGlobal("RR#DGaze","LOCALS",1)
		FaceObject(LastSeenBy(Myself))
                ForceSpellRES("RR#DGAZE",LastSeenBy(Myself)) // Death Gaze
END

IF
	!GlobalTimerNotExpired("RR#Tactic","LOCALS")
	RandomNum(6,1)
THEN
	RESPONSE #100
                SetGlobal("RR#Bhave","LOCALS",9) // Teleport
		SetGlobalTimer("RR#Tactic","LOCALS",12)
END

IF
	!GlobalTimerNotExpired("RR#Tactic","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Tactic","LOCALS",12)
                SetGlobal("RR#Bhave","LOCALS",0) // Default (Silence or Vampiric Touch)
END

IF
        Global("RR#Bhave","LOCALS",9) // Teleport to the optimal target
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but do not disrupt the Blood War
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck([GOODCUTOFF.0.0.MAGE],STATE_PANIC)
	!StateCheck([GOODCUTOFF.0.0.MAGE],STATE_MIRRORIMAGE)
        CheckStatLT([GOODCUTOFF.0.0.MAGE],1,STONESKINS)
	!CheckStatGT([GOODCUTOFF.0.0.MAGE],1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
        CheckStatLT([GOODCUTOFF.0.0.MAGE],100,RESISTCRUSHING)
	!Range([GOODCUTOFF.0.0.MAGE],6)
	See([GOODCUTOFF.0.0.MAGE])
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		AttackOneRound(LastSeenBy(Myself))
		SetInterrupt(TRUE)
END

IF
        Global("RR#Bhave","LOCALS",9) // Teleport to the optimal target
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but do not disrupt the Blood War
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck([GOODCUTOFF.0.0.MAGE_THIEF],STATE_PANIC)
	!StateCheck([GOODCUTOFF.0.0.MAGE_THIEF],STATE_MIRRORIMAGE)
        CheckStatLT([GOODCUTOFF.0.0.MAGE_THIEF],1,STONESKINS)
	!CheckStatGT([GOODCUTOFF.0.0.MAGE_THIEF],1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
        CheckStatLT([GOODCUTOFF.0.0.MAGE_THIEF],100,RESISTCRUSHING)
	!Range([GOODCUTOFF.0.0.MAGE_THIEF],6)
	See([GOODCUTOFF.0.0.MAGE_THIEF])
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		AttackOneRound(LastSeenBy(Myself))
		SetInterrupt(TRUE)
END

IF
        Global("RR#Bhave","LOCALS",9) // Teleport to the optimal target
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but do not disrupt the Blood War
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck([GOODCUTOFF.0.0.CLERIC_MAGE],STATE_PANIC)
	!StateCheck([GOODCUTOFF.0.0.CLERIC_MAGE],STATE_MIRRORIMAGE)
        CheckStatLT([GOODCUTOFF.0.0.CLERIC_MAGE],1,STONESKINS)
	!CheckStatGT([GOODCUTOFF.0.0.CLERIC_MAGE],1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
        CheckStatLT([GOODCUTOFF.0.0.CLERIC_MAGE],100,RESISTCRUSHING)
	!Range([GOODCUTOFF.0.0.CLERIC_MAGE],6)
	See([GOODCUTOFF.0.0.CLERIC_MAGE])
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		AttackOneRound(LastSeenBy(Myself))
		SetInterrupt(TRUE)
END

IF
        Global("RR#Bhave","LOCALS",9) // Teleport to the optimal target
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but do not disrupt the Blood War
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck([GOODCUTOFF.0.0.BARD_ALL],STATE_PANIC)
	!StateCheck([GOODCUTOFF.0.0.BARD_ALL],STATE_MIRRORIMAGE)
        CheckStatLT([GOODCUTOFF.0.0.BARD_ALL],1,STONESKINS)
	!CheckStatGT([GOODCUTOFF.0.0.BARD_ALL],1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
        CheckStatLT([GOODCUTOFF.0.0.BARD_ALL],100,RESISTCRUSHING)
	!Range([GOODCUTOFF.0.0.BARD_ALL],6)
	See([GOODCUTOFF.0.0.BARD_ALL])
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		AttackOneRound(LastSeenBy(Myself))
		SetInterrupt(TRUE)
END

IF
        Global("RR#Bhave","LOCALS",9) // Teleport to the optimal target
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but do not disrupt the Blood War
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck(WorstAC([PC]),STATE_PANIC)
	!StateCheck(WorstAC([PC]),STATE_MIRRORIMAGE)
        CheckStatLT(WorstAC([PC]),1,STONESKINS)
	!CheckStatGT(WorstAC([PC]),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
        CheckStatLT(WorstAC([PC]),100,RESISTCRUSHING)
	!Range(WorstAC([PC]),6)
	See(WorstAC([PC]))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		AttackOneRound(LastSeenBy(Myself))
		SetInterrupt(TRUE)
END

IF
        Global("RR#Bhave","LOCALS",9) // Teleport to the optimal target
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck(MostDamagedOf([PC]),STATE_PANIC)
	!StateCheck(MostDamagedOf([PC]),STATE_MIRRORIMAGE)
        CheckStatLT(MostDamagedOf([PC]),1,STONESKINS)
	!CheckStatGT(MostDamagedOf([PC]),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
        CheckStatLT(MostDamagedOf([PC]),100,RESISTCRUSHING)
	!Range(MostDamagedOf([PC]),6)
	See(MostDamagedOf([PC]))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		AttackOneRound(LastSeenBy(Myself))
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        !GlobalTimerNotExpired("RR#Silence","LOCALS")
	See([GOODCUTOFF.0.0.MAGE_ALL])
	!StateCheck(LastSeenBy(Myself),STATE_SILENCED)
	!StateCheck(LastSeenBy(Myself),STATE_HARMLESS)
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStat(LastSeenBy(Myself),8,WIZARD_SPELL_IMMUNITY)  // Spell Immunity: Alteration
	!HasItemEquiped("AMUL21",LastSeenBy(Myself)) // Amulet of Power
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
		SetGlobalTimer("RR#Silence","LOCALS",18)
                ForceSpellRES("RR#PR211",LastSeenBy(Myself)) // Silence 15' Radius
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        !GlobalTimerNotExpired("RR#Silence","LOCALS")
	See(SecondNearest([GOODCUTOFF.0.0.MAGE_ALL]))
	!StateCheck(LastSeenBy(Myself),STATE_SILENCED)
	!StateCheck(LastSeenBy(Myself),STATE_HARMLESS)
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStat(LastSeenBy(Myself),8,WIZARD_SPELL_IMMUNITY)  // Spell Immunity: Alteration
	!HasItemEquiped("AMUL21",LastSeenBy(Myself)) // Amulet of Power
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
		SetGlobalTimer("RR#Silence","LOCALS",18)
                ForceSpellRES("RR#PR211",LastSeenBy(Myself)) // Silence 15' Radius
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        !GlobalTimerNotExpired("RR#Silence","LOCALS")
	See([GOODCUTOFF.0.0.CLERIC_ALL])
	!StateCheck(LastSeenBy(Myself),STATE_SILENCED)
	!StateCheck(LastSeenBy(Myself),STATE_HARMLESS)
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStat(LastSeenBy(Myself),8,WIZARD_SPELL_IMMUNITY)  // Spell Immunity: Alteration
	!HasItemEquiped("AMUL21",LastSeenBy(Myself)) // Amulet of Power
        !SpellCast(LastSeenBy(Myself),WIZARD_VOCALIZE)
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
		SetGlobalTimer("RR#Silence","LOCALS",18)
                ForceSpellRES("RR#PR211",LastSeenBy(Myself)) // Silence 15' Radius
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        !GlobalTimerNotExpired("RR#Silence","LOCALS")
	See(SecondNearest([GOODCUTOFF.0.0.CLERIC_ALL]))
	!StateCheck(LastSeenBy(Myself),STATE_SILENCED)
	!StateCheck(LastSeenBy(Myself),STATE_HARMLESS)
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStat(LastSeenBy(Myself),8,WIZARD_SPELL_IMMUNITY)  // Spell Immunity: Alteration
	!HasItemEquiped("AMUL21",LastSeenBy(Myself)) // Amulet of Power
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
		SetGlobalTimer("RR#Silence","LOCALS",18)
                ForceSpellRES("RR#PR211",LastSeenBy(Myself)) // Silence 15' Radius
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        !GlobalTimerNotExpired("RR#Silence","LOCALS")
	See([GOODCUTOFF.0.0.DRUID_ALL])
	!StateCheck(LastSeenBy(Myself),STATE_SILENCED)
	!StateCheck(LastSeenBy(Myself),STATE_HARMLESS)
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStat(LastSeenBy(Myself),8,WIZARD_SPELL_IMMUNITY)  // Spell Immunity: Alteration
	!HasItemEquiped("AMUL21",LastSeenBy(Myself)) // Amulet of Power
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
		SetGlobalTimer("RR#Silence","LOCALS",18)
                ForceSpellRES("RR#PR211",LastSeenBy(Myself)) // Silence 15' Radius
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        !GlobalTimerNotExpired("RR#Silence","LOCALS")
	See([GOODCUTOFF.0.0.BARD_ALL])
	!StateCheck(LastSeenBy(Myself),STATE_SILENCED)
	!StateCheck(LastSeenBy(Myself),STATE_HARMLESS)
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStat(LastSeenBy(Myself),8,WIZARD_SPELL_IMMUNITY)  // Spell Immunity: Alteration
	!HasItemEquiped("AMUL21",LastSeenBy(Myself)) // Amulet of Power
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
		SetGlobalTimer("RR#Silence","LOCALS",18)
                ForceSpellRES("RR#PR211",LastSeenBy(Myself)) // Silence 15' Radius
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	See([GOODCUTOFF])
	Range(LastSeenBy(Myself),6)
	!StateCheck(LastSeenBy(Myself),STATE_NOT_TARGETABLE)
	!General(LastSeenBy(Myself),UNDEAD)
	!Race(LastSeenBy(Myself),GOLEM)
        !Race(LastSeenBy(Myself),SLIME)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TURNING)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	!CheckStat(LastSeenBy(Myself),7,WIZARD_SPELL_IMMUNITY)  // Spell Immunity: Necromancy
	CheckStatLT(LastSeenBy(Myself),55,RESISTMAGIC)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
		ForceSpellRES("RR#WI314",LastSeenBy(Myself)) // Vampiric Touch
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	See(SecondNearest([GOODCUTOFF]))
	Range(LastSeenBy(Myself),6)
	!StateCheck(LastSeenBy(Myself),STATE_NOT_TARGETABLE)
	!General(LastSeenBy(Myself),UNDEAD)
        !Race(LastSeenBy(Myself),SLIME)
	!Race(LastSeenBy(Myself),GOLEM)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TURNING)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	!CheckStat(LastSeenBy(Myself),7,WIZARD_SPELL_IMMUNITY)  // Spell Immunity: Necromancy
	CheckStatLT(LastSeenBy(Myself),55,RESISTMAGIC)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
		ForceSpellRES("RR#WI314",LastSeenBy(Myself)) // Vampiric Touch
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	See(ThirdNearest([GOODCUTOFF]))
	Range(LastSeenBy(Myself),6)
	!StateCheck(LastSeenBy(Myself),STATE_NOT_TARGETABLE)
	!General(LastSeenBy(Myself),UNDEAD)
        !Race(LastSeenBy(Myself),SLIME)
	!Race(LastSeenBy(Myself),GOLEM)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TURNING)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	!CheckStat(LastSeenBy(Myself),7,WIZARD_SPELL_IMMUNITY)  // Spell Immunity: Necromancy
	CheckStatLT(LastSeenBy(Myself),55,RESISTMAGIC)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
		ForceSpellRES("RR#WI314",LastSeenBy(Myself)) // Vampiric Touch
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	See(FourthNearest([GOODCUTOFF]))
	Range(LastSeenBy(Myself),6)
	!StateCheck(LastSeenBy(Myself),STATE_NOT_TARGETABLE)
	!General(LastSeenBy(Myself),UNDEAD)
        !Race(LastSeenBy(Myself),SLIME)
	!Race(LastSeenBy(Myself),GOLEM)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TURNING)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	!CheckStat(LastSeenBy(Myself),7,WIZARD_SPELL_IMMUNITY)  // Spell Immunity: Necromancy
	CheckStatLT(LastSeenBy(Myself),55,RESISTMAGIC)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
		ForceSpellRES("RR#WI314",LastSeenBy(Myself)) // Vampiric Touch
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	See(FifthNearest([GOODCUTOFF]))
	Range(LastSeenBy(Myself),6)
	!StateCheck(LastSeenBy(Myself),STATE_NOT_TARGETABLE)
	!General(LastSeenBy(Myself),UNDEAD)
        !Race(LastSeenBy(Myself),SLIME)
	!Race(LastSeenBy(Myself),GOLEM)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TURNING)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	!CheckStat(LastSeenBy(Myself),7,WIZARD_SPELL_IMMUNITY)  // Spell Immunity: Necromancy
	CheckStatLT(LastSeenBy(Myself),55,RESISTMAGIC)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
		ForceSpellRES("RR#WI314",LastSeenBy(Myself)) // Vampiric Touch
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	See(SixthNearest([GOODCUTOFF]))
	Range(LastSeenBy(Myself),6)
	!StateCheck(LastSeenBy(Myself),STATE_NOT_TARGETABLE)
	!General(LastSeenBy(Myself),UNDEAD)
        !Race(LastSeenBy(Myself),SLIME)
	!Race(LastSeenBy(Myself),GOLEM)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TURNING)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TRAP)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
	!CheckStat(LastSeenBy(Myself),7,WIZARD_SPELL_IMMUNITY)  // Spell Immunity: Necromancy
	CheckStatLT(LastSeenBy(Myself),55,RESISTMAGIC)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
		ForceSpellRES("RR#WI314",LastSeenBy(Myself)) // Vampiric Touch
		SetInterrupt(TRUE)
END

IF
	!CheckStatGT(NearestEnemyOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
        CheckStatLT(NearestEnemyOf(Myself),100,RESISTSLASHING)
	See(NearestEnemyOf(Myself)) // note: this block is intended to check for opponents who may be panicked or under stoneskin/mirror image
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(SixthNearestEnemyOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
	!StateCheck(SixthNearestEnemyOf(Myself),STATE_PANIC)
        CheckStatLT(SixthNearestEnemyOf(Myself),100,RESISTSLASHING)
	!StateCheck(SixthNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
        CheckStatLT(SixthNearestEnemyOf(Myself),1,STONESKINS)
	See(SixthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(FifthNearestEnemyOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
	!StateCheck(FifthNearestEnemyOf(Myself),STATE_PANIC)
        CheckStatLT(FifthNearestEnemyOf(Myself),100,RESISTSLASHING)
	!StateCheck(FifthNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
        CheckStatLT(FifthNearestEnemyOf(Myself),1,STONESKINS)
	See(FifthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(FourthNearestEnemyOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
	!StateCheck(FourthNearestEnemyOf(Myself),STATE_PANIC)
        CheckStatLT(FourthNearestEnemyOf(Myself),100,RESISTSLASHING)
	!StateCheck(FourthNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
        CheckStatLT(FourthNearestEnemyOf(Myself),1,STONESKINS)
	See(FourthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(ThirdNearestEnemyOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
	!StateCheck(ThirdNearestEnemyOf(Myself),STATE_PANIC)
        CheckStatLT(ThirdNearestEnemyOf(Myself),100,RESISTSLASHING)
	!StateCheck(ThirdNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
        CheckStatLT(ThirdNearestEnemyOf(Myself),1,STONESKINS)
	See(ThirdNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(SecondNearestEnemyOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
	!StateCheck(SecondNearestEnemyOf(Myself),STATE_PANIC)
        CheckStatLT(SecondNearestEnemyOf(Myself),100,RESISTSLASHING)
	!StateCheck(SecondNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
        CheckStatLT(SecondNearestEnemyOf(Myself),1,STONESKINS)
	See(SecondNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(NearestEnemyOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
        !StateCheck(NearestEnemyOf(Myself),STATE_PANIC)
        CheckStatLT(NearestEnemyOf(Myself),100,RESISTSLASHING)
	!StateCheck(NearestEnemyOf(Myself),STATE_MIRRORIMAGE)
        CheckStatLT(NearestEnemyOf(Myself),1,STONESKINS)
	See(NearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	InWeaponRange([GOODCUTOFF.0.0.DRUID_ALL]) // special case, nearby spellcaster priority targeting
	!CheckStatGT([GOODCUTOFF.0.0.DRUID_ALL],1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
	!StateCheck([GOODCUTOFF.0.0.DRUID_ALL],STATE_PANIC)
        CheckStatLT([GOODCUTOFF.0.0.DRUID_ALL],100,RESISTSLASHING)
        CheckStatLT([GOODCUTOFF.0.0.DRUID_ALL],1,STONESKINS)
	See([GOODCUTOFF.0.0.DRUID_ALL])
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	InWeaponRange([GOODCUTOFF.0.0.CLERIC_ALL]) // special case, nearby spellcaster priority targeting
	!CheckStatGT([GOODCUTOFF.0.0.CLERIC_ALL],1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
	!StateCheck([GOODCUTOFF.0.0.CLERIC_ALL],STATE_PANIC)
        CheckStatLT([GOODCUTOFF.0.0.CLERIC_ALL],100,RESISTSLASHING)
        CheckStatLT([GOODCUTOFF.0.0.CLERIC_ALL],1,STONESKINS)
	See([GOODCUTOFF.0.0.CLERIC_ALL])
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	InWeaponRange([GOODCUTOFF.0.0.MAGE_ALL]) // special case, nearby spellcaster priority targeting
	!CheckStatGT([GOODCUTOFF.0.0.MAGE_ALL],1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
	!StateCheck([GOODCUTOFF.0.0.MAGE_ALL],STATE_PANIC)
        CheckStatLT([GOODCUTOFF.0.0.MAGE_ALL],100,RESISTSLASHING)
        CheckStatLT([GOODCUTOFF.0.0.MAGE_ALL],1,STONESKINS)
	See([GOODCUTOFF.0.0.MAGE_ALL])
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(SixthNearestEnemyOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
	StateCheck(SixthNearestEnemyOf(Myself),STATE_IMMOBILE)
        CheckStatLT(SixthNearestEnemyOf(Myself),100,RESISTSLASHING)
	See(SixthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(FifthNearestEnemyOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
	StateCheck(FifthNearestEnemyOf(Myself),STATE_IMMOBILE)
        CheckStatLT(FifthNearestEnemyOf(Myself),1,STONESKINS)
	See(FifthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(FourthNearestEnemyOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
	StateCheck(FourthNearestEnemyOf(Myself),STATE_IMMOBILE)
        CheckStatLT(FourthNearestEnemyOf(Myself),100,RESISTSLASHING)
	See(FourthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(ThirdNearestEnemyOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
	StateCheck(ThirdNearestEnemyOf(Myself),STATE_IMMOBILE)
        CheckStatLT(ThirdNearestEnemyOf(Myself),100,RESISTSLASHING)
	See(ThirdNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(SecondNearestEnemyOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
	StateCheck(SecondNearestEnemyOf(Myself),STATE_IMMOBILE)
        CheckStatLT(SecondNearestEnemyOf(Myself),100,RESISTSLASHING)
	See(SecondNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(NearestEnemyOf(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
	StateCheck(NearestEnemyOf(Myself),STATE_IMMOBILE)
        CheckStatLT(NearestEnemyOf(Myself),100,RESISTSLASHING)
	See(NearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	HitBy([GOODCUTOFF],0)
	!CheckStatGT(LastHitter(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
        !StateCheck(LastHitter(Myself),STATE_PANIC)
        CheckStatLT(LastHitter(Myself),100,RESISTSLASHING)
	!StateCheck(LastHitter(Myself),STATE_MIRRORIMAGE)
        CheckStatLT(LastHitter(Myself),1,STONESKINS)
	RandomNum(3,1)
	See(LastHitter(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT([0.0.DEMONIC.0.0.0.MASK_LAWFUL],1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
        CheckStatLT([0.0.DEMONIC.0.0.0.MASK_LAWFUL],100,RESISTSLASHING)
	See([0.0.DEMONIC.0.0.0.MASK_LAWFUL])
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	Name("RR#GHAST",LastSeenBy(Myself))
THEN
	RESPONSE #100
		RandomWalkContinuous()
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!Range(LastSeenBy(Myself),6)
        InMyArea(LastSeenBy(Myself))
	!StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
        !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
	!CheckStatGT(LastSeenBy(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
        CheckStatLT(LastSeenBy(Myself),100,RESISTSLASHING)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		SetInterrupt(TRUE)
END

IF
	!StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
        !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
	!CheckStatGT(LastSeenBy(Myself),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Improved Mantle or higher
        CheckStatLT(LastSeenBy(Myself),100,RESISTSLASHING)
        ActionListEmpty()
THEN
	RESPONSE #100
		AttackOneRound(LastSeenBy(Myself))
END

IF
	ActionListEmpty()
THEN
	RESPONSE #100
		RandomWalkContinuous()
END
