/*************************************************************************
 *                        aVENGER's Tweak Pack (aTweaks)
 *                              -=by aVENGER=-
 *                 http://www.spellholdstudios.net/ie/atweaks
 *************************************************************************/



BACKUP ~atweaks/backup~
AUTHOR "Wisp, shsforums.net"                                                       // Author and e-mail address
VERSION ~v3.82~                                                                    // Mod version
ALWAYS                                                                             // Always do this block
  INCLUDE ~aTweaks/LIB/RR#AGLEF.TPH~                                               // Include my custom "Add Global Effect" function
  INCLUDE ~aTweaks/LIB/RR#SP2IN.TPH~                                               // Include my custom "Spell to Innate" function
  INCLUDE ~aTweaks/LIB/REGEXP.TPH~                                                 // allows regexp to match tabs and newlines (from the G3 BG2 Fixpack)
  INCLUDE ~aTweaks/lib/tobexfix.tph~                                               // if TobEx is installed, this disables some of its currently buggy options
  INCLUDE "atweaks/lib/rr#mdcre.tph"
  INCLUDE "atweaks/lib/rr#mdcsc.tph"
  INCLUDE "atweaks/lib/lib.tpa"
  INCLUDE "atweaks/lib/comp_vars.tpa"
  RANDOM_SEED go
  
  OUTER_PATCH ~12~ BEGIN
    WRITE_BYTE 1 0x09
    READ_ASCII 1 tab (1)  // 0x09, tab
    WRITE_BYTE 1 0x0a
    READ_ASCII 1 lnl (1)  // 0x0a, Linux
    WRITE_BYTE 0 0x0d
    READ_ASCII 0 mnl (1)  // 0x0d, Mac
    READ_ASCII 0 wnl (2)  // 0x0d0a, Windows
  END
<<<<<<<< ...blank
>>>>>>>>
END                                                                                // End the ALWAYS block

README ~atweaks/readme_atweaks.html~                                               // Offer to display the main readme
ASK_EVERY_COMPONENT                                                                // Disallow installing all components at once


LANGUAGE ~English~ ~ENGLISH~ ~aTweaks/TRA/ENGLISH/aTweaks.tra~
LANGUAGE ~Francais (by the d'Oghmatiques)~ ~FRENCH~ ~aTweaks/TRA/ENGLISH/aTweaks.tra~ ~aTweaks/TRA/FRENCH/aTweaks.tra~
LANGUAGE ~Espanol (by Immortality, William Imm and Lisandro)~ ~SPANISH~ ~aTweaks/TRA/ENGLISH/aTweaks.tra~ ~aTweaks/TRA/SPANISH/aTweaks.tra~
LANGUAGE ~Deutsch (by Cronox and Leomar)~ ~GERMAN~ ~aTweaks/TRA/ENGLISH/aTweaks.tra~ ~aTweaks/TRA/GERMAN/aTweaks.tra~
LANGUAGE ~Italian (by Andrea C. and ilot)~ ~ITALIAN~ ~aTweaks/TRA/ENGLISH/aTweaks.tra~ ~aTweaks/TRA/ITALIAN/aTweaks.tra~
LANGUAGE ~Russian (by the Aerie.ru team and the Arcanecoast.ru team)~ ~RUSSIAN~ ~aTweaks/TRA/ENGLISH/aTweaks.tra~ ~aTweaks/TRA/RUSSIAN/aTweaks.tra~



// ******************************************************* PnP tweaks *************************************************************************************
////////////////////////////////////////////////////
// Restore innate infravision to Half-Orc characters
////////////////////////////////////////////////////

BEGIN @100 DESIGNATED 100 // Restore innate infravision to Half-Orc characters
GROUP @1   // PnP tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required


// Add ToB scripting actions/triggers to SoA (borrowed from the G3 BG2 Fixpack with thanks) 
INCLUDE ~atweaks/lib/tob2soa.tph~

COPY    ~aTweaks/SPL/RR#HOINF.SPL~ ~override~                                      // spell which grants permament infravision
EXTEND_BOTTOM ~dplayer3.bcs~  ~aTweaks/BAF/DPLAYER.BAF~                            // extend the party script for Player1
EXTEND_BOTTOM ~dplayer2.bcs~  ~aTweaks/BAF/DPLAYER.BAF~                            // extend the party script for other party members




/////////////////////////////////////////////////////////////////////////////////////////////////
// Prevent skeletal undead from being affected by Illithids' Devour Brain attack
/////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN @101 DESIGNATED 101 // Prevent skeletal undead from being affected by Illithids' Devour Brain attack
GROUP @1   // PnP tweaks 
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required
REQUIRE_PREDICATE !GAME_IS ~iwd-in-bg2~ @92                                        // Do not install this component on IWD-in-BG2


INCLUDE "atweaks/lib/c101.tpa"


/////////////////////////////////////////////////////
// Change Spiritual Hammer into a ranged force weapon
/////////////////////////////////////////////////////

BEGIN @102 DESIGNATED 102 // Make Spiritual Hammer into a ranged force weapon and remove its strength bonus to damage
GROUP @1   // PnP tweaks 
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                             // SoA or ToB engine required
REQUIRE_PREDICATE !MOD_IS_INSTALLED spell_rev.tp2 0 @98


COPY_EXISTING ~shammr.itm~ ~override~                                                 // Spiritual Hammer at level 1
             ~shammr2.itm~ ~override~                                                 // Spiritual Hammer at level 7
             ~shammr3.itm~ ~override~                                                 // Spiritual Hammer at level 13
  READ_LONG 0x64 abil_off
  READ_SHORT 0x68 abil_num
  READ_LONG 0x6a fx_off
  FOR (i = 0; i < "%abil_num%"; i += 1) BEGIN                                         // parse each ability
    READ_BYTE (%abil_off% + %i% * 0x38) abil_type                                     // read ability type
    PATCH_IF (%abil_type% = 1) BEGIN                                                  // only patch the melee ability header
      WRITE_SHORT (0x0e + "%abil_off%") 100                                           // increase attack range
      WRITE_BYTE  ("%abil_off%" + 0x26) 0                                             // disallow strength bonus
      WRITE_SHORT ("%abil_off%" + 0x1a) 0                                             // damage bonus
      WRITE_SHORT ("%abil_off%" + 0x1c) 0                                             // damage type
      WRITE_SHORT ("%abil_off%" + 0x16) 0                                             // damage dice
      WRITE_SHORT ("%abil_off%" + 0x18) 0                                             // dice rolls
      READ_SHORT (%abil_off% + %i% * 0x38 + 0x1e) abil_fx_num
      READ_SHORT (%abil_off% + %i% * 0x38 + 0x20) abil_fx_idx
        INSERT_BYTES (%fx_off% + %abil_fx_idx% * 0x30) 0x30                           // add effect
        WRITE_SHORT (%fx_off% + %abil_fx_idx% * 0x30) 50                              // effect: #50 (color glow by RGB)
        WRITE_BYTE (%fx_off% + %abil_fx_idx% * 0x30 + 0x2) 2                          // target: preset target (2)
        WRITE_LONG (%fx_off% + %abil_fx_idx% * 0x30 + 0x4) "-1771831296"              // param1: RGB color
        WRITE_LONG (%fx_off% + %abil_fx_idx% * 0x30 + 0x8) 983040                     // param2: location and speed
        WRITE_BYTE (%fx_off% + %abil_fx_idx% * 0x30 + 0xc) 1                          // timing: permanent (1)
        WRITE_BYTE (%fx_off% + %abil_fx_idx% * 0x30 + 0xd) 1                          // dispel: dispellable and allows magic resistance (1)
        WRITE_BYTE (%fx_off% + %abil_fx_idx% * 0x30 + 0x12) 100                       // probability: 100%
      SET %abil_fx_num% += 1                                                          // update number of ability effects
        INSERT_BYTES (%fx_off% + %abil_fx_idx% * 0x30) 0x30                           // add effect
        WRITE_SHORT (%fx_off% + %abil_fx_idx% * 0x30) 12                              // effect: #12 (HP damage)
        WRITE_BYTE (%fx_off% + %abil_fx_idx% * 0x30 + 0x2) 2                          // target: preset target (2)
         PATCH_IF ("%SOURCE_RES%" STRING_EQUAL_CASE ~shammr~) BEGIN                   // Spiritual Hammer at level 1
          WRITE_LONG (%fx_off% + %abil_fx_idx% * 0x30 + 0x4) 1                        // param1: fixed damage ammount
         END
         PATCH_IF ("%SOURCE_RES%" STRING_EQUAL_CASE ~shammr2~) BEGIN                  // Spiritual Hammer at level 7
          WRITE_LONG (%fx_off% + %abil_fx_idx% * 0x30 + 0x4) 2                        // param1: fixed damage ammount
         END
         PATCH_IF ("%SOURCE_RES%" STRING_EQUAL_CASE ~shammr3~) BEGIN                  // Spiritual Hammer at level 13
          WRITE_LONG (%fx_off% + %abil_fx_idx% * 0x30 + 0x4) 3                        // param1: fixed damage ammount
         END
        WRITE_LONG (%fx_off% + %abil_fx_idx% * 0x30 + 0x8) 4194304                    // param2: magical damage (4194304)
        WRITE_BYTE (%fx_off% + %abil_fx_idx% * 0x30 + 0xc) 1                          // timing: permanent (1)
        WRITE_BYTE (%fx_off% + %abil_fx_idx% * 0x30 + 0xd) 1                          // dispel: dispellable and allows magic resistance (1)
        WRITE_BYTE (%fx_off% + %abil_fx_idx% * 0x30 + 0x12) 100                       // probability: 100%
        WRITE_LONG (%fx_off% + %abil_fx_idx% * 0x30 + 0x1c) 1                         // dice rolls
        WRITE_LONG (%fx_off% + %abil_fx_idx% * 0x30 + 0x20) 4                         // dice size
      SET %abil_fx_num% += 1                                                          // update number of ability effects
      WRITE_SHORT (%abil_off% + %i% * 0x38 + 0x1e) %abil_fx_num%
      FOR (j = i + 1; j < "%abil_num%"; j += 1) BEGIN                                 // update ability effect indices for abilities after current one
        READ_SHORT (%abil_off% + %j% * 0x38 + 0x20) abil_fx_idx
        WRITE_SHORT (%abil_off% + %j% * 0x38 + 0x20) (%abil_fx_idx% + 1)
      END
      FOR (k = %abil_fx_idx%; k < "%abil_fx_idx%" + "%abil_fx_num%"; k += 1) BEGIN    // parse each ability effect specific to current ability
        READ_SHORT (%fx_off% + %k% * 0x30) opcode
        READ_LONG  (%fx_off% + %k% * 0x30 + 0x8) param2
         PATCH_IF (%opcode% = 141 AND %param2% = 5) BEGIN                             // effect: #141 (lighting effects)
          WRITE_LONG (%fx_off% + %k% * 0x30 + 0x8) 1                                  // param2: 1 (aqua SHEARTH)
	 END
       END
      FOR (l = %abil_fx_idx%; l < "%abil_fx_idx%" + "%abil_fx_num%"; l += 1) BEGIN    // parse each ability effect specific to current ability
        READ_BYTE (%fx_off% + %l% * 0x30 + 0xd) dispel
         PATCH_IF NOT (%dispel% = 1) BEGIN                                            // check if the ability is dispellable and allows magic resistance
        WRITE_BYTE (%fx_off% + %l% * 0x30 + 0xd) 1                                    // make all abilities dispellable and allow magic resistance
	 END
      END
    END
  END
BUT_ONLY




////////////////////////////////////////////////////////////////////
// Allow Dispel/Remove Magic to take down Globes of Invulnerability
////////////////////////////////////////////////////////////////////

BEGIN @103 DESIGNATED 103 // Allow Dispel/Remove Magic to take down Globes of Invulnerability
GROUP @1   // PnP tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required
REQUIRE_PREDICATE !MOD_IS_INSTALLED spell_rev.tp2 0 @98

// The Major and Minor Globes of Invulnerability should be dispellable by Dispel Magic, as per the spell's description and per PnP rules

ACTION_FOR_EACH ~file~ IN                                                          // for each of the following files
              ~spwi406~                                                            // Minor Globe of Invulnrability
              ~spwi602~                                                            // Globe of Invulnrability
             ~RR#WI406~                                                            // Minor Globe of Invulnrability (used by RR for Selina's Amulet)
BEGIN                                                                              // execute the following
ACTION_IF FILE_EXISTS_IN_GAME ~%file%.spl~ BEGIN                                   // if the designated file with a SPL extension exists
COPY_EXISTING ~%file%.spl~ ~override~
PATCH_IF (%SOURCE_SIZE% > 0x71) THEN BEGIN  // file size sanity check (filters out 0 byte files i.e. ALLOW_MISSING)
// =============================================================================== // the actual work starts from here
  READ_LONG  0x64 "abil_off"
  READ_SHORT 0x68 "abil_num"
  READ_LONG  0x6a "fx_off"
  FOR (index = 0 ; index < abil_num ; index = index + 1 ) BEGIN // cycle through abilities
    READ_SHORT  ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
    READ_SHORT  ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
    FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
      READ_BYTE ("%fx_off%" + 0x0d + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "dispel"
      PATCH_IF NOT ("%dispel%" = 3) BEGIN
        WRITE_BYTE ("%fx_off%" + 0x0d + (0x30 * ("%index2%" + "%abil_fx_idx%"))) 3 // make each effect dispellable and let it bypasses magic resistance
      END      
    END
  END
PATCH_INCLUDE ~aTweaks/lib/fj_spl_itm_reindex.tpp~                                 // Nythrun's SPL/ITM re-indexer macro (needed for Global Effects)
// =============================================================================== // the actual work ends here
END                                                                                // ends file size check
BUT_ONLY_IF_IT_CHANGES
END                                                                                // ends ACTION_IF FILE_EXISTS_IN_GAME block
END                                                                                // ends ACTION_FOR_EACH block



///////////////////
// PnP Color Spray
///////////////////

BEGIN @104 DESIGNATED 104 // PnP Color Spray
GROUP @1   // PnP tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required

COPY ~atweaks/spl/spwi105.spl~ ~override~                                          // Color Spray
  SAY UNIDENTIFIED_DESC @1890 SAY DESC @1890
COPY ~atweaks/spl/mephits/rr#mracs.spl~ ~override/spin937.spl~                     // Color Spray (Radiant Mephit version)
  SAY UNIDENTIFIED_DESC @1890 SAY DESC @1890

// Patch immunity to both Color Spray versions into spells, items that cause blindness
// Note: Color Spray should not protect the target from its own effects. It's clunky and can often cause more harm than good. 
// The drawbacks outweigh the benefits in this case.

ACTION_FOR_EACH ~file~ IN                                                          // for each of the following files
                ~spdr101.spl~
                ~spin595.spl~
                ~spin878.spl~
                ~spin893.spl~
                ~spin929.spl~
                ~spin931.spl~
                ~sppr704.spl~
                ~sppr707.spl~
                ~spwi106.spl~
                ~spwi118.spl~
                ~spwi224.spl~
                ~spwi714.spl~
                ~spwi815.spl~
                ~spwi958.spl~
                ~spwm178.spl~
                ~chalcy2.itm~
                ~gorwom4.itm~
                 ~halb06.itm~
                   ~sorb.itm~
                 ~sw1h51.itm~
                 ~wand19.itm~
BEGIN                                                                              // execute the following
ACTION_IF FILE_EXISTS_IN_GAME ~%file%~ BEGIN                                       // if the designated file with a SPL extension exists
COPY_EXISTING ~%file%~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN                                         // protects against invalid files
// =============================================================================== // the actual work starts from here
  LPF add_conditional_immunity INT_VAR f_Condition = 74 STR_VAR f_Spell = spwi105 END
  LPF add_conditional_immunity INT_VAR f_Condition = 74 STR_VAR f_Spell = spin937 END
// =============================================================================== // the actual work ends here
END                                                                                // ends file size check
BUT_ONLY_IF_IT_CHANGES
END                                                                                // ends ACTION_IF FILE_EXISTS_IN_GAME block
END                                                                                // ends ACTION_FOR_EACH block



//////////////////////
// PnP Dimension Door
//////////////////////

BEGIN @105 DESIGNATED 105 // PnP Dimension Door
GROUP @1   // PnP tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required

ACTION_IF NOT FILE_EXISTS_IN_GAME ~RR#DDOOR.BAM~ THEN BEGIN                        // Checks for the Icewind Dale's Dimension Door animation component
  COPY    ~aTweaks/BAM/SPDIMNDR.BAM~   ~override/RR#DDOOR.BAM~                     // IWD1's Dimension Door animation (custom file name)
  COPY    ~aTweaks/WAV/EFF_M09.WAV~    ~override/RR#DDOOR.WAV~                     // IWD1's Dimension Door sound effect (custom file name)
  COPY    ~aTweaks/VVC/RR#DDOOR.VVC~   ~override~                                  // Custom Dimension Door VVC
END                                                                                // ends ACTION_IF block

COPY ~atweaks/spl/spwi402.spl~ ~override~                                          // Dimension Door
  SAY UNIDENTIFIED_DESC @1891 SAY DESC @1891

COPY ~atweaks/itm/rr#scr1v.itm~ ~override~                                         // Dimension Door scroll
  SAY DESC @1891

ACTION_FOR_EACH ~file~ IN                                                          // for each of the following files
              ~BGSCRL1V~                                                           // Dimension Door scroll (BGT)
              ~D0SCRLDD~                                                           // Dimension Door scroll (D0 Tweaks)
BEGIN                                                                              // execute the following
ACTION_IF FILE_EXISTS_IN_GAME ~%file%.itm~ BEGIN                                   // if the designated file with an ITM extension exists
COPY_EXISTING ~%file%.itm~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN                                         // protects against invalid files
// =============================================================================== // the actual work starts from here
  SAY DESC @1891
// =============================================================================== // the actual work ends here
END                                                                                // ends file size check
BUT_ONLY_IF_IT_CHANGES
END                                                                                // ends ACTION_IF FILE_EXISTS_IN_GAME block
END                                                                                // ends ACTION_FOR_EACH block

ACTION_IF MOD_IS_INSTALLED SPELL_REV.TP2 0 THEN BEGIN                              // Checks for the main component of Spell Revisions
COPY_EXISTING ~scrl1v.itm~ ~override~                                              // Dimension Door scroll (SR)
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN                                         // protects against invalid files
    SAY DESC @1891
  END
BUT_ONLY_IF_IT_CHANGES
END                                                                                // ends SR check

ACTION_IF FILE_EXISTS_IN_GAME ~d0scrldd.itm~ BEGIN                                 // Check for D0Tweaks' Dimension Door component
COPY ~atweaks/spl/spwi402.spl~ ~override/spwi450.spl~                              // Dimension Door (D0 Tweaks)
    SAY DESC @1891
COPY ~atweaks/itm/rr#scr1v.itm~ ~override/d0scrldd.itm~                            // Dimension Door scroll (D0 Tweaks)
  SAY DESC @1891
END                                                                                // ends D0Tweaks' check

ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN                                     // Tutu check
COPY ~atweaks/spl/spwi402.spl~ ~override/spwi450.spl~                              // Dimension Door (Tutu)
    SAY DESC @1891
COPY ~atweaks/itm/rr#scr1v.itm~ ~override/_scrl1v.itm~                             // Dimension Door scroll (Tutu)
  SAY DESC @1891
END                                                                                // ends Tutu check

COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~                                   // parses through all stores in the game (even those added by mods)
PATCH_IF (%SOURCE_SIZE% > 0x9b) THEN BEGIN                                         // file size sanity check (filters out 0 byte files i.e. ALLOW_MISSING)
  READ_LONG 0x08 "itemtype" ELSE 0                                                 // check store type
  PATCH_IF ("%itemtype%" != 5) BEGIN                                               // if the store isn't a container
// =============================================================================== // the actual work starts from here
PATCH_IF NOT FILE_CONTAINS_EVALUATED (~%SOURCE_FILE%~ ~d0scrldd~) AND NOT FILE_CONTAINS_EVALUATED (~%SOURCE_FILE%~ ~BGSCRL1V~) AND NOT FILE_CONTAINS_EVALUATED (~%SOURCE_FILE%~ ~_SCRL1V~) AND FILE_CONTAINS_EVALUATED (~%SOURCE_FILE%~ ~SCRL1U~) BEGIN
  ADD_STORE_ITEM ~rr#scr1v~ AFTER ~SCRL1U~ #1 #0 #0 ~IDENTIFIED~ #2                // Add 2x Dimension Door scroll
END
// =============================================================================== // the actual work ends here
END                                                                                // ends store type check
END                                                                                // ends file size check
BUT_ONLY_IF_IT_CHANGES


// If TobEx is present restore Dimension Door to the mage/sorcerer spell selection screen

ACTION_IF FILE_EXISTS_IN_GAME ~tobex_ini/tobexcore.ini~ BEGIN                      // if TobEx is installed
  COPY ~tobex_ini/tobexcore.ini~ ~tobex_ini~
    REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Externalise Mage Spell Hiding=0~ ~Externalise Mage Spell Hiding=1~
  BUT_ONLY_IF_IT_CHANGES
END

ACTION_IF FILE_EXISTS_IN_GAME ~hidespl.2da~ BEGIN                                  // if TobEx or Tob Hacks are installed
COPY_EXISTING ~hidespl.2da~ ~override~                                             // Swashbuckler's kit ability table
  REPLACE_TEXTUALLY ~SPWI402~ ~RR#BLNK~                                            // Remove Dimension Door from the hidden spells list
BUT_ONLY_IF_IT_CHANGES
END



///////////////////////////////////////////////////////////////////////////////
// Magical arrows and bolts deal bonus damage equal to their enchantment level
///////////////////////////////////////////////////////////////////////////////

BEGIN @110 DESIGNATED 110 // Magical arrows and bolts deal bonus damage equal to their enchantment level
GROUP @1   // PnP tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required
REQUIRE_PREDICATE !GAME_IS ~tutu tutu_totsc~ @91                                   // Forbid installation on Tutu
REQUIRE_PREDICATE !GAME_IS ~iwd-in-bg2~ @92                                        // Do not install this component on IWD-in-BG2
REQUIRE_PREDICATE !MOD_IS_INSTALLED item_rev.tp2 0 
              AND !MOD_IS_INSTALLED bgttweak.tp2 2204
              AND !GAME_IS ca                         @98                          //Avoid content duplication

// This tweak adds a damage bonus to magical arrows and bolts equal to their enchantment level, as per PnP rules and as in BG1/IWD1


ACTION_FOR_EACH ~file~ IN                                                          // for each of the following files
                ~AROW02~                                                           // Arrow +1
                ~AROW11~                                                           // Arrow +2
                ~AROW15~                                                           // Arrow +3
                ~BOLT02~                                                           // Bolt +1
                ~BOLT06~                                                           // Bolt +2
                ~BOLT09~                                                           // Bolt +3
              ~QUIVER01~                                                           // Quiver of Plenty +1 (arrows)
              ~QUIVER02~                                                           // Case of Plenty +1 (bolts)
              ~QUIVER03~                                                           // Quiver of Plenty +2 (arrows)
              ~QUIVER04~                                                           // Case of Plenty +2 (bolts)
BEGIN                                                                              // execute the following
ACTION_IF FILE_EXISTS_IN_GAME ~%file%.itm~ BEGIN                                   // if the designated file with a ITM extension exists
COPY_EXISTING ~%file%.itm~ ~override~
PATCH_IF (%SOURCE_SIZE% > 0x71) THEN BEGIN                                         // file size sanity check (filters out 0 byte files i.e. ALLOW_MISSING)
// =============================================================================== // the actual work starts from here
READ_LONG 0x60 enchantment                                                         // enchantment level
READ_LONG 0x64 abil_off                                                            // ability offset
READ_SHORT 0x68 abil_num                                                           // ability number
READ_LONG 0x6a fx_off
  FOR (i = 0; i < "%abil_num%"; i += 1) BEGIN                                      // parse each ability
    READ_BYTE (%abil_off% + %i% * 0x38) abil_type                                  // read ability type
    PATCH_IF (%abil_type% = 2) BEGIN                                               // only patch the ranged ability header (2)
      READ_SHORT ("%abil_off%" + 0x1a) dmg_bonus                                   // damage bonus
      PATCH_IF (%dmg_bonus% < %enchantment%) BEGIN                                 // if the damage bonus is lower than the enchantment bonus       
        WRITE_SHORT ("%abil_off%" + 0x1a) %enchantment%                            // raise the damage bonus to the enchantment level
        READ_STRREF IDENTIFIED_DESC temp_desc                                      // description update
        INNER_PATCH_SAVE new_desc "%temp_desc%" BEGIN
          REPLACE_TEXTUALLY ~1D6~ ~1D6 + %enchantment%~                            // change arrow description to include enchantment damage bonus
          REPLACE_TEXTUALLY ~1D8~ ~1D8 + %enchantment%~                            // change bolt description to include enchantment damage bonus
        END
        SAY_EVALUATED IDENTIFIED_DESC "%new_desc%"
      END
    END
  END
// =============================================================================== // the actual work ends here
END                                                                                // ends file size check
BUT_ONLY_IF_IT_CHANGES
END                                                                                // ends ACTION_IF FILE_EXISTS_IN_GAME block
END                                                                                // ends ACTION_FOR_EACH block



//////////////////////////////////////////////////////
// Allow Mages to scribe memorized spells onto scrolls
//////////////////////////////////////////////////////

BEGIN @116 SUBCOMPONENT @115 DESIGNATED 115 // Scrolls can be scribed everywhere
GROUP @1   // PnP tweaks 
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required

OUTER_SPRINT AreaType ~True()~
OUTER_SET Restrictions = 0

INCLUDE "atweaks/lib/c115.tpa"



BEGIN @117 SUBCOMPONENT @115 DESIGNATED 117 // Scrolls can only be scribed at inns and strongholds
GROUP @1   // PnP tweaks 
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required

OUTER_SET Restrictions = 1

INCLUDE "atweaks/lib/c115.tpa"



//////////////////////////////////////////////
// Restore innate disease immunity to Paladins
//////////////////////////////////////////////

BEGIN @120 DESIGNATED 120 // Restore innate disease immunity to Paladins
GROUP @1   // PnP tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required
REQUIRE_PREDICATE !FILE_EXISTS_IN_GAME k0pa001.spl @98

COPY_EXISTING ~SPPR317.SPL~ ~override/RR#PALCD.SPL~                                // Clone Cure Disease into an innate ability
READ_BYTE  0x19 "flags"
READ_LONG  0x64 "abil_off"
READ_SHORT 0x68 "abil_num"
WRITE_SHORT 0x1c 0x04                                                              // Set ability type to Innate (4)
WRITE_BYTE 0x19 ("%flags%" BOR 0b01000000)                                         // add the "simplified duration" flag (this actually marks the ability as non-magical)
WRITE_LONG 0x34 1                                                                  // Set spell level to 1 to avoid scripting issues
 FOR ("index" = 0; "%index%" < "%abil_num%"; "index" = ("%index%" + 1)) BEGIN      // cycle through headers
  WRITE_BYTE  ("%abil_off%" + 0x02 + (0x28 * "%index%")) 4                         // change ability icon location to Innate (4)
  WRITE_SHORT ("%abil_off%" + 0x12 + ("%index%" * 0x28)) 0                         // set casting time to 0 (in PnP, Paladins can cure diseases by simply touching the recipient)
 END
BUT_ONLY_IF_IT_CHANGES

COPY    ~aTweaks/SPL/RR#PALDI.SPL~ ~override~                                      // spell which grants disease immunity


// Add innate disease immunity to various Paladin kits

ACTION_FOR_EACH ~file~ IN                                                          // for each of the following files
              ~clabpa01~                                                           // True Paladin
              ~clabpa02~                                                           // Cavalier
              ~clabpa03~                                                           // Inquisitor
              ~clabpa04~                                                           // Undead Hunter
                ~KishHL~                                                           // Holy Liberator (from Oversight)
              ~tcselune~                                                           // Paladin of Selune (from Classic Adventures)
BEGIN                                                                              // execute the following
ACTION_IF FILE_EXISTS_IN_GAME ~%file%.2da~ BEGIN                                   // if the designated file with a 2DA extension exists
  APPEND ~%file%.2da~ ~ABILITYV AP_RR#PALDI~ UNLESS ~ABILITYV~                     // grant innate disease immunity
END                                                                                // ends ACTION_IF FILE_EXISTS_IN_GAME block
END                                                                                // ends ACTION_FOR_EACH block


// Add Cure Disease ability to various Paladin kits

ACTION_FOR_EACH ~file~ IN                                                          // for each of the following files
              ~clabpa01~                                                           // True Paladin
              ~clabpa02~                                                           // Cavalier
                ~KishHL~                                                           // Holy Liberator (from Oversight)
              ~tcselune~                                                           // Paladin of Selune (from Classic Adventures)
BEGIN                                                                              // execute the following
ACTION_IF FILE_EXISTS_IN_GAME ~%file%.2da~ BEGIN                                   // if the designated file with a 2DA extension exists
  APPEND ~%file%.2da~ ~ABILITYW GA_RR#PALCD~ UNLESS ~ABILITYW~                     // grant Cure Disease ability
END                                                                                // ends ACTION_IF FILE_EXISTS_IN_GAME block
END                                                                                // ends ACTION_FOR_EACH block


ACTION_FOR_EACH ~file~ IN                                                          // for each of the following files
              ~clabpa01~                                                           // True Paladin
              ~clabpa02~                                                           // Cavalier
              ~clabpa03~                                                           // Inquisitor
              ~clabpa04~                                                           // Undead Hunter
                ~KishHL~                                                           // Holy Liberator (from Oversight)
              ~tcselune~                                                           // Paladin of Selune (from Classic Adventures)
BEGIN                                                                              // execute the following
ACTION_IF FILE_EXISTS_IN_GAME ~%file%.2da~ BEGIN                                   // if the designated file with a 2DA extension exists
COPY_EXISTING ~%file%.2da~ ~override~
PATCH_IF (%SOURCE_SIZE% > 0) THEN BEGIN                                            // file size sanity check (filters out 0 byte files i.e. ALLOW_MISSING)
// =============================================================================== // the actual work starts from here
REPLACE_TEXTUALLY ~AP_RR#PALDI~ ~AP_RR#PALDI **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** ****~ // Add empty designations just to be safe
REPLACE_TEXTUALLY ~GA_RR#PALCD~ ~GA_RR#PALCD **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** **** ****~ // Add empty designations just to be safe
PRETTY_PRINT_2DA
// =============================================================================== // the actual work ends here
END                                                                                // ends file size check
BUT_ONLY_IF_IT_CHANGES
END                                                                                // ends ACTION_IF FILE_EXISTS_IN_GAME block
END                                                                                // ends ACTION_FOR_EACH block




////////////////////////////////////////////////////
// Rangers' Animal Empathy improves with experience
////////////////////////////////////////////////////

BEGIN @125 DESIGNATED 125 // Rangers' Animal Empathy improves with experience
GROUP @1   // PnP tweaks

REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required


INCLUDE "atweaks/lib/c125.tpa"


////////////////////////////////////////
// Additional racial traits for Dwarves 
////////////////////////////////////////

BEGIN @130 DESIGNATED 130 // Additional racial traits for Dwarves
GROUP @1   // PnP tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required

// Add ToB scripting actions/triggers to SoA (borrowed from the G3 BG2 Fixpack with thanks) 
INCLUDE ~atweaks/lib/tob2soa.tph~

INCLUDE "atweaks/lib/c130.tpa"


///////////////////////////////////////
// Additional racial traits for Gnomes
///////////////////////////////////////

BEGIN @140 DESIGNATED 140 // Additional racial traits for Gnomes
GROUP @1   // PnP tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required

// Add ToB scripting actions/triggers to SoA (borrowed from the G3 BG2 Fixpack with thanks) 
INCLUDE ~atweaks/lib/tob2soa.tph~

INCLUDE "atweaks/lib/c140.tpa"


/* //Too much work and too likely to break for too little gain
////////////////////////
//Revised saving throws
////////////////////////

BEGIN @170 DESIGNATED 170 //Revised saving throws
GROUP @1 //PnP Tweaks
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required

INCLUDE "atweaks/lib/c170.tpa"
*/


//////////////
// PnP Fiends
//////////////

BEGIN @151 DESIGNATED 150 // PnP Fiends, Mod-added fiends are not affected
GROUP @1   // PnP tweaks 
SUBCOMPONENT @150
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required

COPY_EXISTING sw1h01.itm "override/fl#atfiends1.mrk"                               // Installation marker 
OUTER_SET FiendsOriginal = 1
OUTER_SET FiendsScripts = 0

INCLUDE ~aTweaks/LIB/RR#FIEND.TPH~                                                 // Include the shared Fiend content macro
INCLUDE ~aTweaks/LIB/RR#MDCRE.TPH~                                                 // Include the Creature Modification function
INCLUDE ~aTweaks/LIB/RR#MDCSC.TPH~                                                 // Include the Creature Script Modification function
LAUNCH_ACTION_MACRO ~RR#FIEND~                                                     // Launch the Fiend setup macro

INCLUDE "atweaks/lib/c150.tpa"                                                   //Cre and script stuff, spl



BEGIN @152 DESIGNATED 152 //PnP Fiends, mod-added fiends are also affected
GROUP @1   //PnP Tweaks
SUBCOMPONENT @150
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required

COPY_EXISTING sw1h01.itm "override/fl#atfiends1.mrk"                               //Installation marker 
OUTER_SET FiendsOriginal = 0
OUTER_SET FiendsScripts = 0

INCLUDE ~aTweaks/LIB/RR#FIEND.TPH~                                                 // Include the shared Fiend content macro
INCLUDE ~aTweaks/LIB/RR#MDCRE.TPH~                                                 // Include the Creature Modification function
INCLUDE ~aTweaks/LIB/RR#MDCSC.TPH~                                                 // Include the Creature Script Modification function
LAUNCH_ACTION_MACRO ~RR#FIEND~                                                     // Launch the Fiend setup macro

INCLUDE "atweaks/lib/c150.tpa"                                                   //Cre and script stuff, spl


BEGIN @153 DESIGNATED 153 //PnP Fiends, only scripts
GROUP @1   //PnP Tweaks
SUBCOMPONENT @150
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required

COPY_EXISTING sw1h01.itm "override/fl#atfiends1.mrk"                               //Installation marker 
OUTER_SET FiendsOriginal = 0
OUTER_SET FiendsScripts = 1

INCLUDE ~aTweaks/LIB/RR#FIEND.TPH~                                                 // Include the shared Fiend content macro
INCLUDE ~aTweaks/LIB/RR#MDCRE.TPH~                                                 // Include the Creature Modification function
INCLUDE ~aTweaks/LIB/RR#MDCSC.TPH~                                                 // Include the Creature Script Modification function
LAUNCH_ACTION_MACRO ~RR#FIEND~                                                     // Launch the Fiend setup macro

INCLUDE "atweaks/lib/c150.tpa"                                                     //Cre and script stuff, spl



///////////////////////////
// Revised Fiend summoning 
///////////////////////////


BEGIN @155 DESIGNATED 155 // Revised Fiend summoning
GROUP @1   // PnP tweaks 
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME fl#atfiends1.mrk @94

INCLUDE ~aTweaks/LIB/RR#MDCRE.TPH~                                                 // Include the Creature Modification function
INCLUDE ~aTweaks/LIB/RR#MDCSC.TPH~                                                 // Include the Creature Script Modification function

INCLUDE "atweaks/lib/c155.tpa"


//////////////////
//Fiendish Gating
//////////////////


BEGIN @156 DESIGNATED 156 //Fiendish Gating
GROUP @1
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME fl#atfiends1.mrk @94

INCLUDE "atweaks/lib/c156.tpa"



/*
//////////////
// PnP Undead
//////////////


BEGIN @160 DESIGNATED 160 // PnP Undead
GROUP @1
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required


INCLUDE "atweaks/lib/c160.tpa"
*/



////////////////
// PnP Mephits
////////////////


BEGIN @180 DESIGNATED 180 // PnP Mephits
GROUP @1 // PnP Tweaks
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required
REQUIRE_PREDICATE !GAME_IS ~iwd-in-bg2~ @92                                        // Do not install this component on IWD-in-BG2

INCLUDE "atweaks/lib/c180.tpa"



/////////////////////
// PnP Fey creatures
/////////////////////


BEGIN @185 DESIGNATED 185 // PnP Fey creatures
GROUP @1 // PnP Tweaks
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required

INCLUDE "atweaks/lib/c185.tpa"




//////////////////////////////////////
// Revised Call Woodland Beings spell
//////////////////////////////////////

BEGIN @186 DESIGNATED 186 // Revised Call Woodland Beings spell
GROUP @1 // PnP Tweaks
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~rr#fdrya.bcs~ @1908                         // PnP Fey creatures required

COPY ~atweaks/itm/rr#dinv.itm~  ~override~                                         // item which makes creatures untargetable until the gating animation finishes

COPY    ~atweaks/spl/fey/sppr410.spl~   ~override~                                 // Call Woodland Beings spell
SAY DESC @1895 SAY UNIDENTIFIED_DESC @1895

COPY   ~atweaks/eff/fey/rr#sdrya.eff~   ~override~                                 // Summon Dryad EFF
COPY   ~atweaks/eff/fey/rr#shama.eff~   ~override~                                 // Summon Hamadryad EFF
COPY   ~atweaks/eff/fey/rr#snymp.eff~   ~override~                                 // Summon Nymph EFF

COPY_EXISTING ~dry01.cre~    ~override/rr#sdrya.cre~                               // Summoned Dryad
              ~hamasu.cre~   ~override/rr#shama.cre~                               // Summoned Hamadryad
             ~nymphsu.cre~   ~override/rr#snymp.cre~                               // Summoned Nymph
  ADD_CRE_ITEM ~RR#DINV~ #0 #0 #0 ~UNSTEALABLE&UNDROPPABLE~ ~AMULET~               // item which makes creatures invisible and untargetable while they gate in
  ADD_MEMORIZED_SPELL ~rr#usumn~ #0 ~innate~ ( 1 )                                 // adds 1x Unsummon
  WRITE_LONG 0x10 2                                                                // clear flags, leave only "no corpse"
  WRITE_ASCIIE 0x248 "%DEST_RES%" #8                                               // Override script
  WRITE_ASCII  0x250 ~~ #8                                                         // disable class AI script
  WRITE_ASCII  0x258 ~~ #8                                                         // disable race AI script
  WRITE_ASCII  0x260 ~~ #8                                                         // disable general AI script
  WRITE_ASCII  0x268 ~~ #8                                                         // disable default AI script
  WRITE_BYTE   0x270 128                                                           // set intial allegiance to Neutral
  WRITE_BYTE   0x275 2                                                             // set gender to Female (treated as gated creatures)
  WRITE_LONG   0x001c 0                                                            // set gold carried to 0
  WRITE_ASCIIE 0x280 ~%DEST_RES%~ #32                                              // death variable
  WRITE_ASCII  0x2cc ~~ #8                                                         // dialogue file
  READ_LONG 0x2bc io
  FOR (i=0;i<LONG_AT 0x2c0;++i) BEGIN                                              // Make all items unstealable&undroppable
    WRITE_BYTE io + 0x14*i + 0x10 THIS BOR 10
  END

COMPILE ~atweaks/baf/fey/rr#sdrya.baf~                                             // Summoned Dryad AI script
COMPILE ~atweaks/baf/fey/rr#shama.baf~                                             // Summoned Hamadryad AI script
COMPILE ~atweaks/baf/fey/rr#snymp.baf~                                             // Summoned Nymph AI script



/*

/////////////////////////////////////
// PnP Spiders, Snakes and Ettercaps
/////////////////////////////////////


BEGIN @187 DESIGNATED 187 // PnP Spiders, Snakes and Ettercaps
GROUP @1 // PnP Tweaks
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required

INCLUDE "atweaks/lib/c187.tpa"


//////////////////////
// PnP Ooze creatures
//////////////////////


BEGIN @188 DESIGNATED 188 // PnP Ooze creatures
GROUP @1 // PnP Tweaks
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required

INCLUDE "atweaks/lib/c188.tpa"


///////////////////////
// PnP Plant creatures
///////////////////////


BEGIN @189 DESIGNATED 189 // PnP Ooze creatures
GROUP @1 // PnP Tweaks
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required

INCLUDE "atweaks/lib/c189.tpa"
*/

//////////////////
// PnP Elementals
//////////////////


BEGIN @190 DESIGNATED 190 // PnP Elementals
GROUP @1 // PnP Tweaks
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required

INCLUDE "atweaks/lib/c190.tpa"


//////////////////////////////////////////////
// Increase the Hit Dice of Elemental Princes
//////////////////////////////////////////////

BEGIN @191 DESIGNATED 191 // Increase the Hit Dice of Elemental Princes
GROUP @1 // PnP Tweaks
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~rr#easer.bcs~ @2000                         // PnP Elementals component required


INCLUDE "atweaks/lib/c191.tpa"



// ******************************************************* Gameplay tweaks ********************************************************************************
////////////////////////////////////////////////////////////////
// Allow Breach to take down Stoneskin effects applied by items
////////////////////////////////////////////////////////////////

BEGIN @200 DESIGNATED 200 // Allow Breach to take down Stoneskin effects applied by items
GROUP @2 // Gameplay tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required


COPY_EXISTING ~stonskin.itm~ ~override~ // ring which grants the stoneskin effect while equipped (used by Mekrath, Rayic Gethras, Terminsel... etc.)
  PATCH_IF SOURCE_SIZE > 0x71 BEGIN
    READ_LONG   0x64 "abil_off"
    READ_SHORT  0x68 "abil_num"
    READ_LONG   0x6a "fx_off"
    READ_SHORT  0x70 "fx_num"
    FOR (index = 0; index < "%fx_num%"; index = index + 1) BEGIN // loop through global effects
      READ_SHORT ("%fx_off%" +        ("%index%" * 0x30)) "type" // read effect type
      PATCH_IF ("%type%" = 218) BEGIN // if the effect is #218 (stoneskin)
        DELETE_BYTES ("%fx_off%" +        ("%index%" * 0x30)) 0x30 // delete effect
        SET "fx_num" = ("%fx_num%" - 1) // indicate that 1 global effect has been removed
        SET "index" = ("%index%" - 1) // end loop
       END
     END
    INSERT_BYTES("%fx_off%"       ) 0x30 // add new global effect
    WRITE_SHORT ("%fx_off%"       ) 146 // effect: #146 (cast spell at creature)
    WRITE_BYTE  ("%fx_off%" + 0x02) 1 // target: 1 (self)
    WRITE_LONG  ("%fx_off%" + 0x08) 1 // param2: 1 (cast instantly)
    WRITE_BYTE  ("%fx_off%" + 0x0c) 2 // timing mode: 2 (while equipped)
    WRITE_BYTE  ("%fx_off%" + 0x0d) 3 // resist: 3 (dispellable and bypasses magic resistance)
    WRITE_BYTE  ("%fx_off%" + 0x12) 100 // probability: 100%
    WRITE_ASCII ("%fx_off%" + 0x14) ~RR#STONS~ #8 // resref: RR#STONS (cloned stoneskin which lasts forever)
    SET "fx_num" = ("%fx_num%" + 1) // indicate that 1 global effect has been added
    WRITE_SHORT 0x70 "%fx_num%" // update the number of global effects
END
BUT_ONLY_IF_IT_CHANGES

// Clone stoneskin into a custom spell which will be used by the ring

COPY_EXISTING ~spwi408.spl~  ~override/RR#STONS.SPL~
PATCH_IF (%SOURCE_SIZE%>0x71) THEN BEGIN
SAY NAME1 #0 // null spell name to remove combat log feedback
WRITE_SHORT   0x22  0    // remove casting graphics
WRITE_ASCII   0x10 ~~ #8 // remove casting sound
READ_LONG  0x64 "abil_off"
  READ_SHORT 0x68 "abil_num"
  READ_LONG  0x6a "fx_off"
  SET "fx_delta" = 0
  FOR (index = 0; index < abil_num; index = index + 1) BEGIN
    READ_SHORT  ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
    READ_SHORT  ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
    SET "abil_fx_idx" = ("%abil_fx_idx%" + "%fx_delta%")
    WRITE_SHORT ("%abil_off%" + 0x20 + (0x28 * "%index%")) "%abil_fx_idx%"
    FOR (index2 = 0; index2 < abil_fx_num; index2 = index2 + 1) BEGIN
      READ_SHORT ("%fx_off%" +        (("%abil_fx_idx%" + "%index2%") * 0x30)) "opcode"
      PATCH_IF ("%opcode%" = 218) BEGIN // if the effect is #218 (stoneskin)
        WRITE_BYTE ("%fx_off%" + 0x0c + (("%abil_fx_idx%" + "%index2%") * 0x30)) 1 // set timing mode to 1 (permanent)
        WRITE_LONG ("%fx_off%" + 0x0e + (("%abil_fx_idx%" + "%index2%") * 0x30)) 0 // null duration
      END
      PATCH_IF ("%opcode%" = 215 OR "%opcode%" = 174 OR "%opcode%" = 50) BEGIN // remove visual and sound fx
        DELETE_BYTES ("%fx_off%" +        (("%abil_fx_idx%" + "%index2%") * 0x30)) 0x30 // delete effect
        SET "fx_delta" = "%fx_delta%" - 1
        SET "index2" = "%index2%" - 1
        SET "abil_fx_num" = "%abil_fx_num%" - 1
      END
    WRITE_SHORT  ("%abil_off%" + 0x1e + (0x28 * "%index%")) ("%abil_fx_num%")
   END
  END
END
BUT_ONLY_IF_IT_CHANGES


// Make the Gargoyle Boots cast a proper Stoneskin spell instead of mimicking its effect


ACTION_IF ENGINE_IS ~tob~ THEN BEGIN                                               // ToB engine required
ACTION_IF NOT MOD_IS_INSTALLED ITEM_REV.TP2 0 THEN BEGIN                           // Item Revisions mod check
COPY    ~aTweaks/SPL/RR#BT12.SPL~ ~override~                                       // Empty Stoneskin spell shell (used for inheriting item effects)
COPY_EXISTING ~boot12.itm~ ~override~                                              // Gargoyle Boots
SPRINT ~new_itm_spl~ ~RR#BT12.SPL~                                                 // new spell: RR#BT12.SPL (empty Stoneskin spell shell)
LAUNCH_PATCH_MACRO ~ITEM_EFFECT_TO_SPELL~                                          // transfer extended item effects to the first extended spell header
SET opcode_to_delete = "-1"                                                        // mark all item opcodes for deletion
SET header = "1"                                                                   // mark the first extended item header for deletion
LAUNCH_PATCH_MACRO ~DELETE_ITEM_EFFECT~                                            // delete all opcodes in the item's extended header
SET opcode = "146"                                                                 // effect: #146 (cast spell at creature)
SET target = "1"                                                                   // target: 1 (self)
SET timing = "1"                                                                   // timing mode: 1 (instant/permanent)
SET parameter1 = "0"                                                               // param1: 0
SET parameter2 = "1"                                                               // param2: 1 (cast instantly)
SET power = "0"                                                                    // power: 0
SET resist_dispel = "3"                                                            // dispel/resistance: 3 (dispellable and bypasses magic resistance)
SET duration = "0"                                                                 // duration: 0
SET probability1 = "100"                                                           // probability1: 100%
SET probability2 = "0"                                                             // probability2: 0%
SET dicenumber = "0"                                                               // dicenumber: 0
SET dicesize = "0"                                                                 // dicesize: 0
SET savingthrow = "0"                                                              // saving throw type: 0 (none)
SET savebonus = "0"                                                                // save bonus: 0
SPRINT ~resource~ ~RR#BT12~                                                        // resref: RR#BT12.SPL (newly created custom Stoneskin spell)
LAUNCH_PATCH_MACRO ~ADD_ITEM_EFFECT~                                               // add a new effect
END                                                                                // ends the ACTION_IF Item Revisions check
END                                                                                // ends the ACTION_IF ToB Engine check



///////////////////////////////////////
// Instant casting for warrior innates
///////////////////////////////////////

BEGIN @201 DESIGNATED 201 // Instant casting for warrior innates
GROUP @2 // Gameplay tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required

// This tweak reduces the casting time of warrior innate abilities to 0
// It also flags these abilities as non-magical which prevents Wild Magic and Dead Magic zones from affecting them

COPY_EXISTING ~spin117.spl~ ~override~ // Minsc Berserk
              ~spcl144.spl~ ~override~ // Kensai Kai
              ~spcl152.spl~ ~override~ // Barbarian Rage
              ~spcl211.spl~ ~override~ // Paladin Lay On Hands
              ~spcl321.spl~ ~override~ // Berserker Enrage
             ~spcl321d.spl~ ~override~ // Berserker Enrage secondary effect (becoming winded)
              ~spcl815.spl~ ~override~ // Monk Lay On Hands
              ~spin827.spl~ ~override~ // Mazzy Lay On Hands
  READ_LONG  0x64 "abil_off"
  READ_SHORT 0x68 "abil_num"
READ_BYTE  0x19 "flags"
   PATCH_IF (("%flags%" BAND 0b01000000) != 0b01000000) BEGIN // only patch if the "simplified duration" flag is unchecked
   WRITE_BYTE 0x19 ("%flags%" BOR 0b01000000) // add the "simplified duration" flag (this actually marks the ability as non-magical)
   END
    FOR ("index" = 0; "%index%" < "%abil_num%"; "index" = ("%index%" + 1)) BEGIN  // cycle through abilities
      WRITE_SHORT ("%abil_off%" + 0x12 + ("%index%" * 0x28)) 0                    // set casting time to 0
  END
BUT_ONLY_IF_IT_CHANGES



///////////////////////
// Revised Bhaalpowers
///////////////////////


BEGIN @240 DESIGNATED 202 // Enhance the Bhaalpowers and standardize their casting time
SUBCOMPONENT @202  // Revised Bhaalpowers
GROUP @2 // Gameplay tweaks
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required
REQUIRE_PREDICATE !GAME_IS ~ca~ @92                                                // Do not install this component on Classic Adventures
REQUIRE_PREDICATE !GAME_IS ~iwd-in-bg2~ @92                                        // Do not install this component on IWD-in-BG2

COPY    ~aTweaks/BAM/RR#BP1E.BAM~   ~override~                                     // Drain Life icon
COPY    ~aTweaks/BAM/RR#BP1G.BAM~   ~override~                                     // Healing Touch icon
COPY    ~aTweaks/BAM/RR#BP2E.BAM~   ~override~                                     // Cause Affliction icon
COPY    ~aTweaks/BAM/RR#BP2G.BAM~   ~override~                                     // Cure Affliction icon
COPY    ~aTweaks/BAM/RR#BP3E.BAM~   ~override~                                     // Divine Wrath icon
COPY    ~aTweaks/BAM/RR#BP3G.BAM~   ~override~                                     // Divine Might icon
COPY    ~aTweaks/BAM/RR#BPCA.BAM~   ~override~                                     // Cause Affliction visual effect BAM
COPY    ~aTweaks/BAM/RR#BPDW.BAM~   ~override~                                     // Divine Wrath visual effect BAM
COPY    ~aTweaks/VVC/RR#BPRA.VVC~   ~override~                                     // Cure Affliction visual effect animation
COPY    ~aTweaks/VVC/RR#BPCA.VVC~   ~override~                                     // Cause Affliction visual effect animation
COPY    ~aTweaks/VVC/RR#BPDW.VVC~   ~override~                                     // Divine Wrath visual effect animation


// Healing Touch (formerly Cure Light Wounds)

COPY ~aTweaks/SPL/SPIN101.SPL~ ~override~                                         // Cure Light Wounds Bhaalpower
  SAY NAME1 @242 SAY NAME2 @242                                                   // change name to Healing Touch
COPY ~aTweaks/EFF/RR#BP1G.EFF~ ~override~                                         // Healing Touch immunity effect (for constructs, undead, extraplanars etc.)



// Drain Life (formerly Larloch's Minor Drain)

COPY ~aTweaks/SPL/SPIN104.SPL~ ~override~                                         // Larloch's Minor Drain Bhaalpower
  SAY NAME1 @243 SAY NAME2 @243                                                   // change name to Drain Life
COPY ~aTweaks/SPL/RR#BP1EA.SPL~ ~override~                                        // Drain Life secondary spell (heals the original caster)
COPY ~aTweaks/EFF/RR#BP1E.EFF~ ~override~                                         // Drain Life immunity effect (for constructs, undead, extraplanars etc.)


// Cure Affliction (formerly Slow Poison)

COPY ~aTweaks/SPL/SPIN102.SPL~ ~override~                                         // Slow Poison Bhaalpower
 SAY NAME1 @244 SAY NAME2 @244                                                    // change name to Cure Affliction
COPY ~aTweaks/EFF/RR#BP2G.EFF~ ~override~                                         // Cure Affliction immunity effect (for constructs, undead, extraplanars etc.)

// Cause Affliction (formerly Horror)

COPY ~aTweaks/SPL/SPIN105.SPL~ ~override~                                         // Horror Bhaalpower
 SAY NAME1 @245 SAY NAME2 @245                                                    // change name to Cause Affliction
COPY ~aTweaks/EFF/RR#BP2E.EFF~ ~override~                                         // Cause Affliction immunity effect (for constructs, undead, extraplanars etc.)
COPY ~aTweaks/SPL/RR#BP2EA.SPL~ ~override~                                        // Cause Affliction secondary spell (-2 disease penalty to STR and CON)
COPY ~aTweaks/SPL/RR#BP2EB.SPL~ ~override~                                        // Cause Affliction tertiary spell (blindness and deafness)
COPY ~aTweaks/SPL/RR#BP2EC.SPL~ ~override~                                        // Cause Affliction quartiary spell (feeblemind)
COPY ~aTweaks/SPL/RR#BP2ED.SPL~ ~override~                                        // Cause Affliction quintiary spell (poison)


// Divine Might (formerly Draw Upon Holy Might)

ACTION_FOR_EACH ~file~ IN                                                          // for each of the following files
              ~spin103~                                                            // Draw Upon Holy Might Bhaalpower (BG1 & SoA)
              ~bhaal2a~                                                            // Draw Upon Holy Might Bhaalpower (ToB)
BEGIN                                                                              // execute the following
ACTION_IF FILE_EXISTS_IN_GAME ~%file%.spl~ BEGIN                                   // if the designated file with a SPL extension exists
COPY_EXISTING ~%file%.spl~ ~override~
PATCH_IF (%SOURCE_SIZE% > 0x71) THEN BEGIN                                         // file size sanity check (filters out 0 byte files i.e. ALLOW_MISSING)
// =============================================================================== // the actual work starts from here
SAY NAME1 @246 SAY NAME2 @246                                                     // change name to Divine Might
WRITE_LONG  0x34 1                                                                // set spell level to 1 (prevents scripting issues)
WRITE_ASCII 0x3a ~RR#BP3G~ #8                                                     // assign a new spellbook icon
READ_LONG   0x64 "abil_off"
READ_SHORT  0x68 "abil_num"
  FOR ("index" = 0; "%index%" < "%abil_num%"; "index" = ("%index%" + 1)) BEGIN    // cycle through abilities
    WRITE_ASCII ("%abil_off%" + 0x04 + ("%index%" * 0x28)) ~RR#BP3G~ #8           // assign a new casting icon
    WRITE_SHORT ("%abil_off%" + 0x12 + ("%index%" * 0x28)) 1                      // set casting time to 1
  END
// =============================================================================== // the actual work ends here
END                                                                                // ends file size check
BUT_ONLY_IF_IT_CHANGES
END                                                                                // ends ACTION_IF FILE_EXISTS_IN_GAME block
END                                                                                // ends ACTION_FOR_EACH block


// Divine Wrath (formerly Vampiric Touch)

COPY ~aTweaks/SPL/SPIN106.SPL~ ~override~                                         // Vampiric Touch Bhaalpower
  SAY NAME1 @247 SAY NAME2 @247                                                   // change name to Divine Wrath
COPY ~aTweaks/EFF/RR#BP3E.EFF~ ~override~                                         // Divine Wrath pushback immunity effect (for large creatures)
COPY ~aTweaks/SPL/RR#BP3EA.SPL~ ~override~                                        // Divine Wrath secondary spell (pushback, knockdown and panic)
COPY ~aTweaks/SPL/RR#BP3EB.SPL~ ~override~                                        // Divine Wrath tertiary spell (unholy red glow visual)


// Dynaheir shouldn't have the Cure Affliction Bhaalpower at her disposal

COPY_EXISTING ~SPPR212.SPL~  ~override/RR#PR212.SPL~                              // Clone Slow Poison into an innate ability
LAUNCH_PATCH_FUNCTION ~RR#SP2IN~ INT_VAR "rr#ctime" = "1" END                     // Execute the "spell to innate ablity" function

ACTION_FOR_EACH dyna IN dynahe dynahe2 dynahe4 dynahe6 _dynahe _dynahe2 _dynahe4 _dynahe6 BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%dyna%.cre" BEGIN
    COPY_EXISTING "%dyna%.cre" ~override~
      PATCH_IF (%SOURCE_SIZE% > 0x2d3) THEN BEGIN                                 // protects against invalid files
        REMOVE_MEMORIZED_SPELL ~spin102~                                          // Cure Affliction Bhaalpower
        ADD_MEMORIZED_SPELL ~RR#PR212~ #0 ~innate~                                // Adds 1x Slow Poison innate ability
      END
    BUT_ONLY_IF_IT_CHANGES
  END
END


// Ensure that the "Gained Special Ability X" strings are correctly displaying on BGT

ACTION_IF GAME_IS ~bgt~ THEN BEGIN                                                // BGT check
COPY_EXISTING ~PLAYER1D.BCS~  ~override~                                          // Player 1 dream script
  DECOMPILE_BCS_TO_BAF
REPLACE_TEXTUALLY EXACT_MATCH ~ApplySpellRES("GOODDRM2",Player1)~ ~AddSpecialAbility("SPIN101")~
REPLACE_TEXTUALLY EXACT_MATCH ~ApplySpellRES("GOODDRM4",Player1)~ ~AddSpecialAbility("SPIN102")~
REPLACE_TEXTUALLY EXACT_MATCH ~ApplySpellRES("GOODDRM6",Player1)~ ~AddSpecialAbility("SPIN103")~
REPLACE_TEXTUALLY EXACT_MATCH ~ApplySpellRES("BADDRM2",Player1)~ ~AddSpecialAbility("SPIN104")~
REPLACE_TEXTUALLY EXACT_MATCH ~ApplySpellRES("BADDRM4",Player1)~ ~AddSpecialAbility("SPIN105")~
REPLACE_TEXTUALLY EXACT_MATCH ~ApplySpellRES("BADDRM6",Player1)~ ~AddSpecialAbility("SPIN106")~
  COMPILE_BAF_TO_BCS
BUT_ONLY_IF_IT_CHANGES
END // ends BGT check


// Ensure that the "Gained Special Ability X" strings are correctly displaying on Tutu

ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN                                    // Tutu check
COPY_EXISTING ~BALDUR.BCS~  ~override~                                            // Main game script
  DECOMPILE_BCS_TO_BAF
REPLACE_TEXTUALLY EXACT_MATCH ~ActionOverride(Player1,ApplySpellRES("jpglvl1",Myself))~ ~ActionOverride(Player1,AddSpecialAbility("SPIN101"))~
REPLACE_TEXTUALLY EXACT_MATCH ~ActionOverride(Player1,ApplySpellRES("jpglvl2",Myself))~ ~ActionOverride(Player1,AddSpecialAbility("SPIN102"))~
REPLACE_TEXTUALLY EXACT_MATCH ~ActionOverride(Player1,ApplySpellRES("jpglvl3",Myself))~ ~ActionOverride(Player1,AddSpecialAbility("SPIN103"))~
REPLACE_TEXTUALLY EXACT_MATCH ~ActionOverride(Player1,ApplySpellRES("jpelvl1",Myself))~ ~ActionOverride(Player1,AddSpecialAbility("SPIN104"))~
REPLACE_TEXTUALLY EXACT_MATCH ~ActionOverride(Player1,ApplySpellRES("jpelvl2",Myself))~ ~ActionOverride(Player1,AddSpecialAbility("SPIN105"))~
REPLACE_TEXTUALLY EXACT_MATCH ~ActionOverride(Player1,ApplySpellRES("jpelvl3",Myself))~ ~ActionOverride(Player1,AddSpecialAbility("SPIN106"))~
REPLACE_TEXTUALLY EXACT_MATCH ~DisplayString(Player1,88753)~ ~~
REPLACE_TEXTUALLY EXACT_MATCH ~DisplayString(Player1,88754)~ ~~
REPLACE_TEXTUALLY EXACT_MATCH ~DisplayString(Player1,88755)~ ~~
REPLACE_TEXTUALLY EXACT_MATCH ~DisplayString(Player1,88756)~ ~~
REPLACE_TEXTUALLY EXACT_MATCH ~DisplayString(Player1,88757)~ ~~
REPLACE_TEXTUALLY EXACT_MATCH ~DisplayString(Player1,88758)~ ~~
  COMPILE_BAF_TO_BCS
BUT_ONLY_IF_IT_CHANGES
END // ends Tutu check


// The Emphatic Manifestation in the Unseeing Eye quest should not be flagged as a demon since extraplanar creatures aren't affected by healing spells
// Note: this actually undoes a change introduced by the G3 BG2 Fixpack. The creature was already flagged as "No Race" in the unmodded game

ACTION_FOR_EACH ~file~ IN                                                          // for each of the following files
              ~RIFTCR04~                                                           // Emphatic Manifestation
BEGIN                                                                              // execute the following
ACTION_IF FILE_EXISTS_IN_GAME ~%file%.cre~ BEGIN                                   // if the file exists
COPY_EXISTING ~%file%.cre~ ~override~
LAUNCH_PATCH_FUNCTION FJ_CRE_VALIDITY
 INT_VAR do_message = 1
END
WRITE_BYTE  0x0272 255                                                             // race: No Race
BUT_ONLY_IF_IT_CHANGES
END                                                                                // ends ACTION_IF FILE_EXISTS_IN_GAME block
END                                                                                // ends ACTION_FOR_EACH block




// ************************************************************** Subcomponent Marker **********************************

BEGIN @241 DESIGNATED 241 // Only standardize the Bhaalpowers' casting time
SUBCOMPONENT @202  // Revised Bhaalpowers
GROUP @2 // Gameplay tweaks
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required
REQUIRE_PREDICATE !GAME_IS ~ca~ @92                                                // Do not install this component on Classic Adventures
REQUIRE_PREDICATE !GAME_IS ~iwd-in-bg2~ @92                                        // Do not install this component on IWD-in-BG2

// This tweak reduces the casting time of all SoA Bhaalpowers to 1 in order to match the ToB Bhaalpowers

COPY_EXISTING ~spin101.spl~ ~override~ // Bhaalpower Cure Light Wounds
              ~spin102.spl~ ~override~ // Bhaalpower Slow Poison
              ~spin103.spl~ ~override~ // Bhaalpower Draw Upon Holy Might
              ~spin104.spl~ ~override~ // Bhaalpower Larloch's Minor Drain
              ~spin105.spl~ ~override~ // Bhaalpower Horror
              ~spin106.spl~ ~override~ // Bhaalpower Vampiric Touch
  READ_LONG  0x64 "abil_off"
  READ_SHORT 0x68 "abil_num"
    FOR ("index" = 0; "%index%" < "%abil_num%"; "index" = ("%index%" + 1)) BEGIN  // cycle through abilities
      WRITE_SHORT ("%abil_off%" + 0x12 + ("%index%" * 0x28)) 1                    // set casting time to 1
  END
BUT_ONLY_IF_IT_CHANGES



/////////////////////////////
// Regain Bhaalpowers in ToB
/////////////////////////////

BEGIN @218 DESIGNATED 218 //Regain Bhaalpowers in ToB
GROUP @2 
REQUIRE_PREDICATE GAME_IS tob @90
REQUIRE_PREDICATE !GAME_IS ~tutu tutu_totsc~ @91
REQUIRE_PREDICATE !GAME_IS ~ca~ @92                                                // Do not install this component on Classic Adventures
REQUIRE_PREDICATE !GAME_IS ~iwd-in-bg2~ @92                                        // Do not install this component on IWD-in-BG2

ACTION_IF BGT BEGIN
  OUTER_SPRINT BG2Check ~Global("ENDOFBG1","GLOBAL",2)~
END ELSE BEGIN
  OUTER_SPRINT BG2Check ~~
END

ACTION_FOR_EACH spell IN spin101 spin102 spin103 spin104 spin105 spin106 BEGIN //Stinkin' game's hard-coded to remove the SPIN###s in ToB
  OUTER_PATCH_SAVE shell "%spell%" BEGIN
    REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH spin fl#gp
  END
  OUTER_PATCH_SAVE copy "%spell%" BEGIN
    REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH spin fl#bp
  END
  COPY_EXISTING "%spell%.spl" "override/%copy%.spl"
  COPY "atweaks/spl/base.spl" "override/%shell%.spl"
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 171 target = 2 timing = 1 STR_VAR resource = EVAL "%copy%" END
END

EXTEND_BOTTOM dplayer3.bcs "atweaks/baf/regainbhaalpowers1.baf" EVAL

OUTER_SPRINT ToBCheck ""
EXTEND_BOTTOM ar4000.bcs "atweaks/baf/regainbhaalpowers2.baf" EVAL
OUTER_SPRINT ToBCheck ~GGT("DefeatedJon",0)~
EXTEND_BOTTOM ar3000.bcs "atweaks/baf/regainbhaalpowers2.baf" EVAL



//////////////////////////////////////////////
// Make druidic shapeshifting uninterruptable
//////////////////////////////////////////////

BEGIN @203 DESIGNATED 203 // Make druidic shapeshifting uninterruptable
GROUP @2 // Gameplay tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required

// This tweak reduces the casting time of the druidic shapeshifting abilities to 0 which effectively makes them uninterruptable
// It also flags these abilities as non-magical which prevents Wild Magic and Dead Magic zones from affecting them

ACTION_FOR_EACH ~file~ IN                                                          // for each of the following files
              ~spcl611~                                                            // Druid Shapeshift Brown Bear
              ~spcl612~                                                            // Druid Shapeshift Wolf
              ~spcl613~                                                            // Druid Shapeshift Black Bear
              ~spcl632~                                                            // Avenger Shapeshift Spider
              ~spcl633~                                                            // Avenger Shapeshift Baby Wyvern
              ~spcl634~                                                            // Avenger Shapeshift Fire Salamander
              ~spcl643~                                                            // Shapeshifter Werewolf Transformation
              ~spcl644~                                                            // Shapeshifter Greater Werewolf Transformation
              ~sppr731~                                                            // Druid Fire Elemental Transformation
              ~sppr732~                                                            // Druid Earth Elemental Transformation
BEGIN                                                                              // execute the following
ACTION_IF FILE_EXISTS_IN_GAME ~%file%.spl~ BEGIN                                   // if the designated file with a SPL extension exists
COPY_EXISTING ~%file%.spl~ ~override~
PATCH_IF (%SOURCE_SIZE% > 0x71) THEN BEGIN                                         // file size sanity check (filters out 0 byte files i.e. ALLOW_MISSING)
// =============================================================================== // the actual work starts from here
  READ_LONG  0x64 "abil_off"
  READ_SHORT 0x68 "abil_num"
  READ_BYTE  0x19 "flags"
   PATCH_IF (("%flags%" BAND 0b01000000) != 0b01000000) BEGIN // only patch if the "simplified duration" flag is unchecked
   WRITE_BYTE 0x19 ("%flags%" BOR 0b01000000) // add the "simplified duration" flag (this actually marks the ability as non-magical)
   END
    FOR ("index" = 0; "%index%" < "%abil_num%"; "index" = ("%index%" + 1)) BEGIN  // cycle through abilities
      WRITE_SHORT ("%abil_off%" + 0x12 + ("%index%" * 0x28)) 0                    // set casting time to 0
  END
// =============================================================================== // the actual work ends here
END                                                                                // ends file size check
BUT_ONLY_IF_IT_CHANGES
END                                                                                // ends ACTION_IF FILE_EXISTS_IN_GAME block
END                                                                                // ends ACTION_FOR_EACH block


// Add the non-dispellable flag to druidic shapechange weapons if TobEx is detected
// This will prevent the natural weapons of the shapechange forms (i.e. claws and such) from being removed by Dispel Magic and Dead Magic Zones
ACTION_IF FILE_EXISTS_IN_GAME ~tobex_ini/tobexcore.ini~ BEGIN                      // if TobEx is installed
ACTION_FOR_EACH ~file~ IN                                                          // for each of the following files
               ~brbrp~                                                             // Brown Bear shape attack
               ~brblp~                                                             // Black Bear shape attack
               ~wolfm~                                                             // Wolf shape attack
             ~plyspid~                                                             // Sword Spider shape attack
            ~plywyvrn~                                                             // Baby Wyvern shape attack
             ~plysala~                                                             // Fire Salamander shape attack
              ~drufir~                                                             // Fire Elemental shape attack
              ~druear~                                                             // Earth Elemental shape attack
BEGIN                                                                              // execute the following
ACTION_IF FILE_EXISTS_IN_GAME ~%file%.itm~ BEGIN                                   // if the designated file with an ITM extension exists
COPY_EXISTING ~%file%.itm~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN                                         // protects against invalid files
// =============================================================================== // the actual work starts from here
    READ_BYTE 0x1b "flags"
    PATCH_IF (("%flags%" BAND "0b00000001") = "0b00000000") BEGIN                  // if bit 24 isn't set
      WRITE_BYTE "0x1b" ("%flags%" BOR "0b00000001")                               // set bit 24
    END
// =============================================================================== // the actual work ends here
END                                                                                // ends file size check
BUT_ONLY_IF_IT_CHANGES
END                                                                                // ends second ACTION_IF FILE_EXISTS_IN_GAME block
END                                                                                // ends ACTION_FOR_EACH block
END                                                                                // ends first ACTION_IF FILE_EXISTS_IN_GAME block



//////////////////////////////////////////////////
// Prevent Mislead clones from singing Bard songs
//////////////////////////////////////////////////

BEGIN @204 DESIGNATED 204 // Prevent Mislead clones from singing Bard songs
GROUP @2 // Gameplay tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required
REQUIRE_PREDICATE !MOD_IS_INSTALLED spell_rev.tp2 0 @98

// This tweak prevents illusionary Mislead clones from singing Bard songs


COPY    ~aTweaks/SPL/RR#MBS0.SPL~ ~override~                                       // Empty battlesong which does nothing
COPY_EXISTING ~MISLEAD.SPL~ ~override~                                             // Mislead spell effects which apply to the clone
SET opcode = "251"                                                                 // effect: #251 (change bard song effect)
SET target = "1"                                                                   // target: 1 (self)
SET timing = "9"                                                                   // timing mode: 9 (permanent after death)
SET parameter1 = "0"                                                               // param1: 0
SET parameter2 = "0"                                                               // param2: 0
SET power = "0"                                                                    // power: 0
SET resist_dispel = "0"                                                            // dispel/resistance: 0 (non-magical)
SET duration = "0"                                                                 // duration: 0
SET probability1 = "100"                                                           // probability1: 100%
SET probability2 = "0"                                                             // probability2: 0%
SET dicenumber = "0"                                                               // dicenumber: 0
SET dicesize = "0"                                                                 // dicesize: 0
SET savingthrow = "0"                                                              // saving throw type: 0 (none)
SET savebonus = "0"                                                                // save bonus: 0
SPRINT ~resource~ ~RR#MBS0~                                                        // resref: RR#MBS0
SET header = "0"                                                                   // add effect to every header
LAUNCH_PATCH_MACRO ~ADD_SPELL_EFFECT~                                              // add new spell effect
BUT_ONLY_IF_IT_CHANGES




//////////////////////////////////////////////////////////////////////////
// Prevent Project Image and Simulacrum clones from using quickslot items
//////////////////////////////////////////////////////////////////////////

BEGIN @205 DESIGNATED 205 // Prevent Project Image and Simulacrum clones from using quickslot items
GROUP @2 // Gameplay tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required
REQUIRE_PREDICATE !MOD_IS_INSTALLED spell_rev.tp2 0 @98

// This tweak prevents Project Image and Simulacrum clones from using quickslot items

COPY_EXISTING ~PROJIMAG.SPL~ ~override~                                            // Project Image spell effects which apply to the clone
              ~SIMULACR.SPL~ ~override~                                            // Simulacrum spell effects which apply to the clone
SET opcode = "144"                                                                 // effect: #144 (disable Button)
SET target = "1"                                                                   // target: 1 (self)
SET timing = "9"                                                                   // timing mode: 9 (permanent after death)
SET parameter1 = "0"                                                               // param1: 0
SET parameter2 = "9"                                                               // param2: 9 (first quick item button)
SET power = "0"                                                                    // power: 0
SET resist_dispel = "0"                                                            // dispel/resistance: 0 (non-magical)
SET duration = "0"                                                                 // duration: 0
SET probability1 = "100"                                                           // probability1: 100%
SET probability2 = "0"                                                             // probability2: 0%
SET dicenumber = "0"                                                               // dicenumber: 0
SET dicesize = "0"                                                                 // dicesize: 0
SET savingthrow = "0"                                                              // saving throw type: 0 (none)
SET savebonus = "0"                                                                // save bonus: 0
SPRINT ~resource~ ~~                                                               // resref: none
SET header = "0"                                                                   // add effect to every header
LAUNCH_PATCH_MACRO ~ADD_SPELL_EFFECT~                                              // add new spell effect

SET opcode = "144"                                                                 // effect: #144 (disable Button)
SET target = "1"                                                                   // target: 1 (self)
SET timing = "9"                                                                   // timing mode: 9 (permanent after death)
SET parameter1 = "0"                                                               // param1: 0
SET parameter2 = "11"                                                              // param2: 11 (second quick item button)
SET power = "0"                                                                    // power: 0
SET resist_dispel = "0"                                                            // dispel/resistance: 0 (non-magical)
SET duration = "0"                                                                 // duration: 0
SET probability1 = "100"                                                           // probability1: 100%
SET probability2 = "0"                                                             // probability2: 0%
SET dicenumber = "0"                                                               // dicenumber: 0
SET dicesize = "0"                                                                 // dicesize: 0
SET savingthrow = "0"                                                              // saving throw type: 0 (none)
SET savebonus = "0"                                                                // save bonus: 0
SPRINT ~resource~ ~~                                                               // resref: none
SET header = "0"                                                                   // add effect to every header
LAUNCH_PATCH_MACRO ~ADD_SPELL_EFFECT~                                              // add new spell effect

SET opcode = "144"                                                                 // effect: #144 (disable Button)
SET target = "1"                                                                   // target: 1 (self)
SET timing = "9"                                                                   // timing mode: 9 (permanent after death)
SET parameter1 = "0"                                                               // param1: 0
SET parameter2 = "12"                                                              // param2: 11 (third quick item button)
SET power = "0"                                                                    // power: 0
SET resist_dispel = "0"                                                            // dispel/resistance: 0 (non-magical)
SET duration = "0"                                                                 // duration: 0
SET probability1 = "100"                                                           // probability1: 100%
SET probability2 = "0"                                                             // probability2: 0%
SET dicenumber = "0"                                                               // dicenumber: 0
SET dicesize = "0"                                                                 // dicesize: 0
SET savingthrow = "0"                                                              // saving throw type: 0 (none)
SET savebonus = "0"                                                                // save bonus: 0
SPRINT ~resource~ ~~                                                               // resref: none
SET header = "0"                                                                   // add effect to every header
LAUNCH_PATCH_MACRO ~ADD_SPELL_EFFECT~                                              // add new spell effect
BUT_ONLY_IF_IT_CHANGES



///////////////////////////////////////////////
// Make Death Ward protect against Vorpal Hits
///////////////////////////////////////////////

BEGIN @211 DESIGNATED 211 // Make Death Ward protect against Vorpal Hits
GROUP @2 // Gameplay tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required
REQUIRE_PREDICATE !MOD_IS_INSTALLED spell_rev.tp2 0 @98                            // Spell Revisions already does this


COPY_EXISTING ~sppr409.spl~ ~override~                                             // Death Ward
  READ_LONG  0x64 "abil_off" ELSE 0
  READ_SHORT 0x68 "abil_num" ELSE 0
  READ_LONG  0x6a "fx_off"   ELSE 0
  SET "delta" = 0
  FOR (index = 0 ; index < abil_num ; index = index + 1) BEGIN
    READ_SHORT ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
    READ_SHORT ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
    SET "abil_fx_idx" = ("%abil_fx_idx%" + "%delta%")
    WRITE_SHORT ("%abil_off%" + 0x20 + (0x28 * "%index%")) "%abil_fx_idx%"
    FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
     READ_SHORT ("%fx_off%" +        (0x30 * ("%abil_fx_idx%" + "%index2%"))) "opcode"
      PATCH_IF ("%opcode%" = 101) BEGIN // clone Protection: from Opcode effect
        READ_ASCII ("%fx_off%" +        (("%abil_fx_idx%" + "%index2%") * 0x30)) "clone_fx" (0x30)
        SET "index2"= "%abil_fx_num%" // kills loop
        INSERT_BYTES            ("%fx_off%" +        ("%abil_fx_idx%" * 0x30)) 0x30             // new effect
          WRITE_EVALUATED_ASCII ("%fx_off%" +        ("%abil_fx_idx%" * 0x30)) "%clone_fx%"     // cloned effect
          WRITE_LONG            ("%fx_off%" + 0x08 + ("%abil_fx_idx%" * 0x30)) "13"             // param2: 13 (Instant Death opcode)
        SET "delta" = "%delta%" + 1
        WRITE_SHORT ("%abil_off%" + 0x1e + (0x28 * "%index%")) ("%abil_fx_num%" + 1)
      END
    END
  END
BUT_ONLY_IF_IT_CHANGES



////////////////////////////////////////////////////////////////////
// Restore the Dispel Magic vulnerability to Nishruu and Hakeashars
////////////////////////////////////////////////////////////////////

BEGIN @210 DESIGNATED 210 // Restore the Dispel Magic vulnerability to Nishruu and Hakeashars
GROUP @2 // Gameplay tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required

// Dispel Magic and Remove Magic are supposed to destroy Nishruu and Hakeashar. However, due to targeting issues, this doesn't work for player characters in the unmodded game. To circumvent this, a dispellable equipped effect (Detect Illusion skill modifier) is added to the Nishruu/Hakeashar attack and is then checked by the creature's script.

ACTION_FOR_EACH ~file~ IN                                                          // for each of the following files
               ~NISHRUU~                                                           // Nishruu and Hakeashar attack
                 ~HAKSU~                                                           // Hakeashar attack from Spell Revisions
BEGIN                                                                              // execute the following
ACTION_IF FILE_EXISTS_IN_GAME ~%file%.itm~ BEGIN                                   // if the designated file with a ITM extension exists
COPY_EXISTING ~%file%.itm~ ~override~
// =============================================================================== // the actual work starts from here
PATCH_IF (%SOURCE_SIZE% > 0x71) THEN BEGIN                                         // file size sanity check (filters out 0 byte files i.e. ALLOW_MISSING)
SET opcode = "276"                                                                 // effect: #276 (Detect Illusions skill modifier)
SET target = "1"                                                                   // target: 1 (self)
SET timing = "2"                                                                   // timing mode: 2 (while equipped)
SET parameter1 = "20"                                                              // param1: 20 (Statistic Modifier)
SET parameter2 = "1"                                                               // param2: 1 (Type: set to value)
SET power = "0"                                                                    // power: 0
SET resist_dispel = "3"                                                            // dispel/resistance: 3 (dispellable, bypasses magic resistance)
SET duration = "0"                                                                 // duration: 0
SET probability1 = "100"                                                           // probability1: 100%
SET probability2 = "0"                                                             // probability2: 0%
SET dicenumber = "0"                                                               // dicenumber: 0
SET dicesize = "0"                                                                 // dicesize: 0
SET savingthrow = "0"                                                              // saving throw type: 0 (none)
SET savebonus = "0"                                                                // save bonus: 0
SPRINT ~resource~ ~~                                                               // resref: none
LAUNCH_PATCH_MACRO ~ADD_ITEM_EQEFFECT~                                             // add new equipping effect
PATCH_INCLUDE ~aTweaks/lib/fj_spl_itm_reindex.tpp~                                 // Nythrun's SPL/ITM re-indexer macro (needed for Global Effects)
// =============================================================================== // the actual work ends here
END                                                                                // ends file size check
BUT_ONLY_IF_IT_CHANGES
END                                                                                // ends ACTION_IF FILE_EXISTS_IN_GAME block
END                                                                                // ends ACTION_FOR_EACH block

EXTEND_TOP ~NISHRU01.BCS~ ~aTweaks/BAF/NISHRU01.BAF~                               // Nishruu/Hakeashar AI script

COPY_EXISTING ~NISHRUSU.CRE~ ~override~                                            // Nishruu
                 ~HAKSU.CRE~ ~override~                                            // Hakeashar
WRITE_BYTE 0x0272 "136"                                                            // race: MIST
WRITE_LONG 0x14  "0"                                                               // XP value when killed: 0 (XP is now awarded via script)
BUT_ONLY_IF_IT_CHANGES




/////////////////////////////////////////////////
// Make alignment detection spells more accurate
/////////////////////////////////////////////////

BEGIN @212 DESIGNATED 212 // Make alignment detection spells more accurate
GROUP @2 // Gameplay tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required


INCLUDE "atweaks/lib/c212.tpa"





////////////////////////////////////////////////////////////////////////
// Expanded saving throw bonus tables for Dwarves, Gnomes and Halflings
////////////////////////////////////////////////////////////////////////

BEGIN @213 DESIGNATED 213 // Expanded saving throw bonus tables for Dwarves, Gnomes and Halflings
GROUP @2 // Gameplay tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                           // SoA or ToB engine required

// Dwarf and Halfling table

COPY_EXISTING ~SAVECNDH.2DA~ ~override~                             // Dwarf and Halfling saving throw bonus table
SET_2DA_ENTRY_LATER ~RR#SAVECNDH~ 1 21 ~6~                          // Set poison save bonus to 6 at level 21
SET_2DA_ENTRY_LATER ~RR#SAVECNDH~ 2 21 ~6~                          // Set wand save bonus to 6 at level 21
SET_2DA_ENTRY_LATER ~RR#SAVECNDH~ 5 21 ~6~                          // Set spell save bonus to 6 at level 21
SET_2DA_ENTRY_LATER ~RR#SAVECNDH~ 1 22 ~6~                          // Set poison save bonus to 6 at level 22
SET_2DA_ENTRY_LATER ~RR#SAVECNDH~ 2 22 ~6~                          // Set wand save bonus to 6 at level 22
SET_2DA_ENTRY_LATER ~RR#SAVECNDH~ 5 22 ~6~                          // Set spell save bonus to 6 at level 22
SET_2DA_ENTRY_LATER ~RR#SAVECNDH~ 1 23 ~6~                          // Set poison save bonus to 6 at level 23
SET_2DA_ENTRY_LATER ~RR#SAVECNDH~ 2 23 ~6~                          // Set wand save bonus to 6 at level 23
SET_2DA_ENTRY_LATER ~RR#SAVECNDH~ 5 23 ~6~                          // Set spell save bonus to 6 at level 23
SET_2DA_ENTRY_LATER ~RR#SAVECNDH~ 1 24 ~6~                          // Set poison save bonus to 6 at level 24
SET_2DA_ENTRY_LATER ~RR#SAVECNDH~ 2 24 ~6~                          // Set wand save bonus to 6 at level 24
SET_2DA_ENTRY_LATER ~RR#SAVECNDH~ 5 24 ~6~                          // Set spell save bonus to 6 at level 24
SET_2DA_ENTRY_LATER ~RR#SAVECNDH~ 1 25 ~7~                          // Set poison save bonus to 7 at level 25
SET_2DA_ENTRY_LATER ~RR#SAVECNDH~ 2 25 ~7~                          // Set wand save bonus to 7 at level 25
SET_2DA_ENTRY_LATER ~RR#SAVECNDH~ 5 25 ~7~                          // Set spell save bonus to 7 at level 25
SET_2DA_ENTRIES_NOW ~RR#SAVECNDH~ 20                                // Actually make the changes
PRETTY_PRINT_2DA
BUT_ONLY_IF_IT_CHANGES

// Gnome table

COPY_EXISTING ~SAVECNG.2DA~ ~override~                               // Gnome saving throw bonus table
SET_2DA_ENTRY_LATER ~RR#SAVECNG~ 2 21 ~6~                            // Set wand save bonus to 6 at level 21
SET_2DA_ENTRY_LATER ~RR#SAVECNG~ 5 21 ~6~                            // Set spell save bonus to 6 at level 21
SET_2DA_ENTRY_LATER ~RR#SAVECNG~ 2 22 ~6~                            // Set wand save bonus to 6 at level 22
SET_2DA_ENTRY_LATER ~RR#SAVECNG~ 5 22 ~6~                            // Set spell save bonus to 6 at level 22
SET_2DA_ENTRY_LATER ~RR#SAVECNG~ 2 23 ~6~                            // Set wand save bonus to 6 at level 23
SET_2DA_ENTRY_LATER ~RR#SAVECNG~ 5 23 ~6~                            // Set spell save bonus to 6 at level 23
SET_2DA_ENTRY_LATER ~RR#SAVECNG~ 2 24 ~6~                            // Set wand save bonus to 6 at level 24
SET_2DA_ENTRY_LATER ~RR#SAVECNG~ 5 24 ~6~                            // Set spell save bonus to 6 at level 24
SET_2DA_ENTRY_LATER ~RR#SAVECNG~ 2 25 ~7~                            // Set wand save bonus to 7 at level 25
SET_2DA_ENTRY_LATER ~RR#SAVECNG~ 5 25 ~7~                            // Set spell save bonus to 7 at level 25
SET_2DA_ENTRIES_NOW ~RR#SAVECNG~ 20                                  // Actually make the changes
PRETTY_PRINT_2DA
BUT_ONLY_IF_IT_CHANGES




/////////////////////////////////
// Bard songs break invisibility
/////////////////////////////////


BEGIN @216 DESIGNATED 216 // Only the Jester song breaks invisibility
SUBCOMPONENT @215  // Bard songs break invisibility
GROUP @2 // Gameplay tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required

COPY_EXISTING ~sw1h01.itm~ ~override/RR#BSBI1.RR~                                  // component marker (needed for the "Simple Bard Script" component)

// Apply the tweak to Jester songs only

ACTION_FOR_EACH ~file~ IN                                                          // for each of the following files
              ~SPCL751A~                                                           // default Jester song
              ~RR#BJS00~                                                           // Rogue Rebalancing Jester song
              ~RR#BJS02~                                                           // Rogue Rebalancing Jester Enhanced song
              ~RR#BJS04~                                                           // Rogue Rebalancing Jester Lingering song
                ~a!gyps~                                                           // Song and Silence Gypsy song
              ~a!dssong~                                                           // Song and Silence Dirgesinger song
BEGIN                                                                              // execute the following
ACTION_IF FILE_EXISTS_IN_GAME ~%file%.spl~ BEGIN                                   // if the designated file with a SPL extension exists
COPY_EXISTING ~%file%.spl~ ~override~
// =============================================================================== // the actual work starts from here
PATCH_IF (%SOURCE_SIZE% > 0x71) THEN BEGIN                                         // file size sanity check (filters out 0 byte files i.e. ALLOW_MISSING)
LAUNCH_PATCH_FUNCTION ~RR#AGLEF~                                                   // Start FUNCTION call (my custom "Add Global Effect" function)
INT_VAR                                                                            // initialize variables
  "rr#opcode" = "136"                                                              // effect: #136 (force visible)
  "rr#target" = "1"                                                                // target: 1 (self)
  "rr#timing" = "1"                                                                // timing mode: 1 (permanent)
  "rr#prob1" = "100"                                                               // probability1: 100%
 END                                                                               // end FUNCTION call
PATCH_INCLUDE ~aTweaks/lib/fj_spl_itm_reindex.tpp~                                 // Nythrun's SPL/ITM re-indexer macro (needed for Global Effects)
// =============================================================================== // the actual work ends here
END                                                                                // ends file size check
BUT_ONLY_IF_IT_CHANGES
END                                                                                // ends ACTION_IF FILE_EXISTS_IN_GAME block
END                                                                                // ends ACTION_FOR_EACH block


BEGIN @217 DESIGNATED 217 // All Bard songs break invisibility
SUBCOMPONENT @215  // Bard songs break invisibility
GROUP @2 // Gameplay tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required

COPY_EXISTING ~sw1h01.itm~ ~override/RR#BSBI2.RR~                                  // component marker (needed for the "Simple Bard Script" component)

// All Bard songs break invisibility

ACTION_FOR_EACH ~file~ IN                                                          // for each of the following files
              ~SPCL542A~                                                           // default Skald song
              ~SPCL751A~                                                           // default Jester song
              ~SPCL920A~                                                           // default Enhanced Bard song HLA
               ~fjbarda~                                                           // G3 BG2 Fixpack Bard song #1
               ~fjbardb~                                                           // G3 BG2 Fixpack Bard song #2
              ~fjbladeb~                                                           // G3 BG2 Fixpack Blade song
              ~RR#BDF02~                                                           // Rogue Rebalancing True Bard song
              ~RR#BBL02~                                                           // Rogue Rebalancing Blade song
              ~RR#BSK00~                                                           // Rogue Rebalancing Skald song
              ~RR#BJS00~                                                           // Rogue Rebalancing Jester song
              ~RR#BDF04~                                                           // Rogue Rebalancing True Bard Enhanced song
              ~RR#BDF06~                                                           // Rogue Rebalancing True Bard Lingering song
              ~RR#BSK02~                                                           // Rogue Rebalancing Skald Enhanced song
              ~RR#BSK04~                                                           // Rogue Rebalancing Skald Lingering song
              ~RR#BJS02~                                                           // Rogue Rebalancing Jester Enhanced song
              ~RR#BJS04~                                                           // Rogue Rebalancing Jester Lingering song
                ~a!gyps~                                                           // Song and Silence Gypsy song
              ~a!dssong~                                                           // Song and Silence Dirgesinger song
               ~spbax2a~                                                           // Classic Adventures Harper of Twilight song
BEGIN                                                                              // execute the following
ACTION_IF FILE_EXISTS_IN_GAME ~%file%.spl~ BEGIN                                   // if the designated file with a SPL extension exists
COPY_EXISTING ~%file%.spl~ ~override~
// =============================================================================== // the actual work starts from here
PATCH_IF (%SOURCE_SIZE% > 0x71) THEN BEGIN                                         // file size sanity check (filters out 0 byte files i.e. ALLOW_MISSING)
LAUNCH_PATCH_FUNCTION ~RR#AGLEF~                                                   // Start FUNCTION call (my custom "Add Global Effect" function)
INT_VAR                                                                            // initialize variables
  "rr#opcode" = "136"                                                              // effect: #136 (force visible)
  "rr#target" = "1"                                                                // target: 1 (self)
  "rr#timing" = "1"                                                                // timing mode: 1 (permanent)
  "rr#prob1" = "100"                                                               // probability1: 100%
 END                                                                               // end FUNCTION call
PATCH_INCLUDE ~aTweaks/lib/fj_spl_itm_reindex.tpp~                                 // Nythrun's SPL/ITM re-indexer macro (needed for Global Effects)
// =============================================================================== // the actual work ends here
END                                                                                // ends file size check
BUT_ONLY_IF_IT_CHANGES
END                                                                                // ends ACTION_IF FILE_EXISTS_IN_GAME block
END



//////////////////////////////////////////////////////
//Altered XP rewards from locks, traps and scrolls
//////////////////////////////////////////////////////

BEGIN @261 SUBCOMPONENT @260 DESIGNATED 261                                        //Improved XP rewards from locks, traps and scrolls
GROUP @2 //Gameplay tweaks
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required

ACTION_DEFINE_ARRAY xpbonus_locks BEGIN 0 20  20  50  50  50  100  100  100  200  200  300  300  400  400  400  500 END
ACTION_DEFINE_ARRAY xpbonus_traps BEGIN 0 50  50  100 100 100 200  200  200  400  400  600  600  800  800  800  1000 END
ACTION_DEFINE_ARRAY xpbonus_scrolls BEGIN 0 50 100 200 500 1000 3000 5000 7000 9000 END
ACTION_DEFINE_ARRAY xpbonus_master BEGIN nil xpbonus_locks xpbonus_traps xpbonus_scrolls END

COPY_EXISTING xpbonus.2da override
  COUNT_2DA_COLS num_col
  FOR (i=1;i<4;++i) BEGIN
    SPRINT array $xpbonus_master("%i%")
    FOR (j=1;j<num_col;++j) BEGIN
      PATCH_IF VARIABLE_IS_SET $EVAL "%array%"("%j%") BEGIN
        k = j
        SET_2DA_ENTRY_LATER s2el_xpbonus i j $EVAL "%array%"("%j%")
      END ELSE PATCH_IF i < 3 BEGIN
        SET_2DA_ENTRY_LATER s2el_xpbonus i j $EVAL "%array%"("%k%")
      END
    END
  END
  SET_2DA_ENTRIES_NOW s2el_xpbonus num_col - 1
  PRETTY_PRINT_2DA
BUT_ONLY


BEGIN @262 SUBCOMPONENT @260 DESIGNATED 262                                        //No XP rewards from locks, traps and scrolls
GROUP @2 //Gameplay tweaks
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required

COPY_EXISTING xpbonus.2da override
  COUNT_2DA_COLS num_col
  FOR (i=1;i<4;++i) BEGIN
    FOR (j=1;j<num_col;++j) BEGIN
      SET_2DA_ENTRY_LATER s2el_xpbonus i j 0
    END
  END
  SET_2DA_ENTRIES_NOW s2el_xpbonus num_col - 1
  PRETTY_PRINT_2DA
BUT_ONLY



///////////////////////
// Simple Thief script
///////////////////////

BEGIN @220 DESIGNATED 220 // Simple Thief script
GROUP @2 // Gameplay tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob totlm iwd2~ @92                              // Works on SoA, ToB, IWD1 +TotLM and IWD2

ACTION_IF GAME_IS iwd2 BEGIN
  OUTER_SPRINT SeeDead ~,0~
END ELSE BEGIN
  OUTER_SPRINT SeeDead ~~
END

// This tweak introduces a simple AI script for Thief PCs which makes them search for traps or hide in shadows when idle
ACTION_IF GAME_IS ~totlm iwd2~ BEGIN
  COMPILE ~atweaks/baf/iwd/rr#thf01.baf~ EVAL
END ELSE BEGIN
  COMPILE ~aTweaks/BAF/RR#THF01.BAF~                                                // Simple Thief script
END
COPY_EXISTING ~RR#THF01.BCS~ ~scripts\RR#THF01.BS~                                // move the script to the player scripts folder

APPEND  ~SCRPDESC.2DA~ ~RR#THF01    RR#TS001    RR#TS002~                         // Set temporary values (to be replaced with custom strings later)
COPY_EXISTING ~SCRPDESC.2DA~               ~override~                             // Replace temporary values with proper strings
  REPLACE ~RR#TS001~ @221                                                         // script title
  REPLACE ~RR#TS002~ @222                                                         // script description
  PRETTY_PRINT_2DA
BUT_ONLY_IF_IT_CHANGES



//////////////////////
// Simple Bard script
//////////////////////

BEGIN @230 DESIGNATED 230 // Simple Bard script
GROUP @2 // Gameplay tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob totlm iwd2~ @92                              // Works on SoA, ToB, IWD1 +TotLM and IWD2

ACTION_IF GAME_IS iwd2 BEGIN
  OUTER_SPRINT SeeDead ~,0~
  OUTER_SPRINT LevelBard CLASSLEVELBARD
END ELSE BEGIN
  OUTER_SPRINT SeeDead ~~
  OUTER_SPRINT LevelBard LEVEL
END

// This tweak introduces a simple AI script for Bard PCs which makes them sing the battlesong when idle
ACTION_IF GAME_IS ~totlm iwd2~ BEGIN
  COMPILE ~atweaks/baf/iwd/rr#brd01.baf~ EVAL
END ELSE BEGIN
  COMPILE ~aTweaks/BAF/RR#brd01.BAF~                                              // Simple Bard script
END
ACTION_IF FILE_EXISTS_IN_GAME ~RR#BSBI1.RR~ THEN BEGIN                            // Checks for the "Only the Jester song breaks invisibility" subcomponent
COMPILE ~aTweaks/BAF/JES/RR#BRD01.BAF~                                            // Simple Bard script (Jesters doesn't sing while invisible)
END                                                                               // ends "Bard songs beak invisibility" component check
ACTION_IF FILE_EXISTS_IN_GAME ~RR#BSBI2.RR~ THEN BEGIN                            // Checks for the "All bard songs break invisibility" subcomponent
COMPILE ~aTweaks/BAF/ALT/RR#BRD01.BAF~                                            // Simple Bard script (All bards don't sing while invisible)
END                                                                               // ends "Bard songs beak invisibility" component check
COPY_EXISTING ~RR#BRD01.BCS~ ~scripts\RR#BRD01.BS~                                // move the script to the player scripts folder

APPEND  ~SCRPDESC.2DA~ ~RR#BRD01    RR#BS001    RR#BS002~                         // Set temporary values (to be replaced with custom strings later)
COPY_EXISTING ~SCRPDESC.2DA~               ~override~                             // Replace temporary values with proper strings
  REPLACE ~RR#BS001~ @231                                                         // script title
  PATCH_IF GAME_IS ~totlm iwd2~ BEGIN
    REPLACE ~RR#BS002~ @238
  END ELSE BEGIN
    REPLACE ~RR#BS002~ @232                                                         // script description
  END
  PRETTY_PRINT_2DA
BUT_ONLY_IF_IT_CHANGES




// ******************************************************* Cosmetic tweaks ********************************************************************************
///////////////////////////////////////////////
// Use Icewind Dale's Dimension Door animation
///////////////////////////////////////////////

BEGIN @321 DESIGNATED 300 // Fast animation speed (matches IWD)
SUBCOMPONENT @300  // Use Icewind Dale's Dimension Door animation and sound effect
GROUP @3 // Cosmetic tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required

COPY    ~aTweaks/BAM/SPDIMNDR.BAM~   ~override~                                    // IWD1's Dimension Door animation
COPY    ~aTweaks/WAV/EFF_M09.WAV~    ~override~                                    // IWD1's Dimension Door sound effect
COPY    ~atweaks/bam/rr#ddbg2.bam~   ~override~                                    // Default BG2 Dimension Door animation (needed for a few static animations)

// Ensure that the portal in Sendai's lair always uses the BG2-style Dimension Door animation (IWD-style doesn't look so good here)

COPY_EXISTING ~ar6108.are~ ~override~
  READ_LONG 0xac "anim_num"
  READ_LONG 0xb0 "anim_off"
  FOR (index = 0 ; index < anim_num ; index = index + 1) BEGIN
    READ_ASCII ("%anim_off%" + ("%index%" * 0x4c)) "anim_name"
    PATCH_IF ("%anim_name%" STRING_EQUAL_CASE "SPDIMNDR") BEGIN
      WRITE_ASCII ("%anim_off%" + 0x28 + ("%index%" * 0x4c)) "rr#ddbg2" #8 // Animation resref (BAM)
    END
  END
BUT_ONLY_IF_IT_CHANGES


BEGIN @322 DESIGNATED 322 // Slow animation speed (matches BG2)
SUBCOMPONENT @300  // Use Icewind Dale's Dimension Door animation and sound effect
GROUP @3 // Cosmetic tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required

COPY    ~aTweaks/BAM/SPDIMNDR.BAM~   ~override~                                    // IWD1's Dimension Door animation
COPY    ~aTweaks/WAV/EFF_M09.WAV~    ~override~                                    // IWD1's Dimension Door sound effect
COPY    ~atweaks/bam/rr#ddbg2.bam~   ~override~                                    // Default BG2 Dimension Door animation (needed for a few static animations)
COPY_EXISTING ~SPDIMNDR.VVC~ ~override~                                            // Dimension Door VVC
  WRITE_LONG  0x34 "5"                                                             // Set the frame rate to 5
BUT_ONLY_IF_IT_CHANGES

// Ensure that the portal in Sendai's lair always uses the BG2-style Dimension Door animation (IWD-style doesn't look so good here)

COPY_EXISTING ~ar6108.are~ ~override~
  READ_LONG 0xac "anim_num"
  READ_LONG 0xb0 "anim_off"
  FOR (index = 0 ; index < anim_num ; index = index + 1) BEGIN
    READ_ASCII ("%anim_off%" + ("%index%" * 0x4c)) "anim_name"
    PATCH_IF ("%anim_name%" STRING_EQUAL_CASE "SPDIMNDR") BEGIN
      WRITE_ASCII ("%anim_off%" + 0x28 + ("%index%" * 0x4c)) "rr#ddbg2" #8 // Animation resref (BAM)
    END
  END
BUT_ONLY_IF_IT_CHANGES


BEGIN @323 DESIGNATED 323 // Use IWD animation for spells, retain BG2 animation for other visuals
SUBCOMPONENT @300  // Use Icewind Dale's Dimension Door animation and sound effect
GROUP @3 // Cosmetic tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required

ACTION_IF NOT FILE_EXISTS_IN_GAME ~RR#DDOOR.BAM~ THEN BEGIN                        // Checks for the Icewind Dale's Dimension Door animation component
  COPY    ~aTweaks/BAM/SPDIMNDR.BAM~   ~override/RR#DDOOR.BAM~                     // IWD1's Dimension Door animation (custom file name)
  COPY    ~aTweaks/WAV/EFF_M09.WAV~    ~override/RR#DDOOR.WAV~                     // IWD1's Dimension Door sound effect (custom file name)
  COPY    ~aTweaks/VVC/RR#DDOOR.VVC~   ~override~                                  // Custom Dimension Door VVC
END                                                                                // ends ACTION_IF block

ACTION_FOR_EACH ~file~ IN                                                          // for each of the following files
              ~SPWI402~                                                            // Wizard Dimension Door
              ~SPWI450~                                                            // Wizard Dimension Door (D0Tweak and Tutu)
              ~SPWI505~                                                            // Wizard Shadow Door
              ~SPWI995~                                                            // Dryad Teleport
BEGIN                                                                              // execute the following
ACTION_IF FILE_EXISTS_IN_GAME ~%file%.spl~ BEGIN                                   // if the designated file with a SPL extension exists
COPY_EXISTING ~%file%.spl~ ~override~
// =============================================================================== // the actual work starts from here
PATCH_IF (%SOURCE_SIZE% > 0x71) THEN BEGIN  // file size sanity check (filters out 0 byte files i.e. ALLOW_MISSING)
READ_LONG  0x64 "abil_off"
READ_SHORT 0x68 "abil_num"
READ_LONG  0x6a "fx_off"
  FOR (index = 0 ; index < abil_num ; index = index + 1 ) BEGIN // cycle through abilities
    READ_SHORT  ("%abil_off%" + 0x1e + (0x28 * "%index%")) "abil_fx_num"
    READ_SHORT  ("%abil_off%" + 0x20 + (0x28 * "%index%")) "abil_fx_idx"
    FOR (index2 = 0 ; index2 < abil_fx_num ; index2 = index2 + 1) BEGIN
      READ_SHORT ("%fx_off%" +        (0x30 * ("%index2%" + "%abil_fx_idx%"))) "opcode"
      READ_LONG  ("%fx_off%" + 0x04 + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "param1"
      READ_LONG  ("%fx_off%" + 0x08 + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "param2"
      READ_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "resref"
      PATCH_IF (("%opcode%" = "141") AND ("%param2%" = "38")) BEGIN  // effect #141: Lighting Effects - Dimension Door (38)
       WRITE_SHORT ("%fx_off%"      + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "215" // effect #215: Play 3D Effect
       WRITE_LONG  ("%fx_off%" + 0x08 + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "1" // param2: 1 (play over target)
       WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "RR#DDOOR" #8 // resref: RR#DDOOR.VVC
       WRITE_LONG  ("%fx_off%" + 0x0e + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "1" // duration: 1
       WRITE_BYTE  ("%fx_off%" + 0x0c + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "0" // timing mode: 0 (duration)
         PATCH_IF ("%param1%" = "1") BEGIN // Delayed duration indicator
         WRITE_LONG  ("%fx_off%" + 0x04 + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "0" // param1: 0
         WRITE_LONG  ("%fx_off%" + 0x0e + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "2" // duration: 2
         WRITE_BYTE  ("%fx_off%" + 0x0c + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "3" // timing mode: 3 (delayed duration)
         END
         PATCH_IF ("%SOURCE_RES%" STRING_EQUAL_CASE ~SPWI995~) BEGIN // Dryad Teleport requires a 1 second delay
         WRITE_LONG  ("%fx_off%" + 0x0e + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "1" // duration: 1
         WRITE_BYTE  ("%fx_off%" + 0x0c + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "3" // timing mode: 3 (delayed duration)
         END
      END
      PATCH_IF (("%opcode%" = "174") AND ("%resref%" STRING_EQUAL_CASE ~EFF_M09~)) BEGIN  // effect #174: Play Sound Effect
       WRITE_ASCII ("%fx_off%" + 0x14 + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "RR#DDOOR" #8 // resref: RR#DDOOR.WAV
       WRITE_LONG  ("%fx_off%" + 0x0e + (0x30 * ("%index2%" + "%abil_fx_idx%"))) "1" // duration: 1
      END
    END
  END
PATCH_INCLUDE ~aTweaks/lib/fj_spl_itm_reindex.tpp~                                 // Nythrun's SPL/ITM re-indexer macro (needed for Global Effects)
// =============================================================================== // the actual work ends here
END                                                                                // ends file size check
BUT_ONLY_IF_IT_CHANGES
END                                                                                // ends ACTION_IF FILE_EXISTS_IN_GAME block
END                                                                                // ends ACTION_FOR_EACH block

BEGIN @324 DESIGNATED 324 //Fast animation speed, shorter delay between animation start and creature appearance/disappearance
SUBCOMPONENT @300 // Use Icewind Dale's Dimension Door animation and sound effect
GROUP @3 // Cosmetic tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required

COPY    ~aTweaks/BAM/SPDIMNDR.BAM~   ~override~                                    // IWD1's Dimension Door animation
COPY    ~aTweaks/WAV/EFF_M09.WAV~    ~override~                                    // IWD1's Dimension Door sound effect
COPY    ~atweaks/bam/rr#ddbg2.bam~   ~override~                                    // Default BG2 Dimension Door animation (needed for a few static animations)

// Ensure that the portal in Sendai's lair always uses the BG2-style Dimension Door animation (IWD-style doesn't look so good here)

COPY_EXISTING ~ar6108.are~ ~override~
  READ_LONG 0xac "anim_num"
  READ_LONG 0xb0 "anim_off"
  FOR (index = 0 ; index < anim_num ; index = index + 1) BEGIN
    READ_ASCII ("%anim_off%" + ("%index%" * 0x4c)) "anim_name"
    PATCH_IF ("%anim_name%" STRING_EQUAL_CASE "SPDIMNDR") BEGIN
      WRITE_ASCII ("%anim_off%" + 0x28 + ("%index%" * 0x4c)) "rr#ddbg2" #8 // Animation resref (BAM)
    END
  END
BUT_ONLY_IF_IT_CHANGES


COPY_EXISTING_REGEXP GLOB ~.*\.dlg$~ override
  DECOMPILE_DLG_TO_D
    REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~CreateCreatureDoor(\(\".+\"\),\(\[-?[0-9]+\.-?[0-9]+\]\),\([0-9]+\))~ ~CreateVisualEffect("spdimndr",\2) Wait(1) CreateCreature(\1,\2,\3)~
    REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~CreateCreatureObjectDoor(\(\".+\"\),\(\"?.+\"?\),\([0-9]+\),\([0-9]+\),\([0-9]+\))~ ~CreateVisualEffectObject("spdimndr",\2) Wait(1) CreateCreatureObject(\1,\2,\3,\4,\5)~
  COMPILE_D_TO_DLG
IF ~CreateCreatureDoor\|CreateCreatureObjectDoor~
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~.*\.bcs$~ override
  DECOMPILE_BCS_TO_BAF
    REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~CreateCreatureDoor(\(\".+\"\),\(\[-?[0-9]+\.-?[0-9]+\]\),\([0-9]+\))~ ~CreateVisualEffect("spdimndr",\2) Wait(1) CreateCreature(\1,\2,\3)~
    REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~CreateCreatureObjectDoor(\(\".+\"\),\(\"?.+\"?\),\([0-9]+\),\([0-9]+\),\([0-9]+\))~ ~CreateVisualEffectObject("spdimndr",\2) Wait(1) CreateCreatureObject(\1,\2,\3,\4,\5)~
  COMPILE_BAF_TO_BCS
IF ~23[12]OB~
BUT_ONLY

ACTION_FOR_EACH file IN spwi402 spwi450 spwi995 BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.spl" BEGIN
    COPY_EXISTING "%file%.spl" override
      PATCH_IF SOURCE_SIZE > 0x72 BEGIN
        FOR (READ_LONG 0x6a fx_off;fx_off<SOURCE_SIZE;fx_off+=0x30) BEGIN
          READ_SHORT fx_off       opcode ELSE 999
          READ_BYTE  fx_off + 0xc timing ELSE 999
          READ_LONG  fx_off + 0xe time   ELSE 0
          PATCH_IF (opcode = 68 OR opcode = 124 OR opcode = 168) AND timing = 4 AND time > 1 BEGIN
            WRITE_LONG fx_off + 0xe 1
          END
        END
      END
    BUT_ONLY
  END
END




////////////////////////////////////////////
// Change the appearance of Valygar's armor
////////////////////////////////////////////

BEGIN @301 DESIGNATED 301 // Change the appearance of Valygar's armor
GROUP @3 // Cosmetic tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required
REQUIRE_PREDICATE !GAME_IS ~iwd-in-bg2~ @92                                        // Do not install this component on IWD-in-BG2
REQUIRE_PREDICATE !GAME_IS ~ca~ @92                                                // Do not install this component on Classic Adventures
REQUIRE_PREDICATE !GAME_IS ~tutu tutu_totsc~ @91                                   // Forbid installation on Tutu


// This tweak changes the appearance of Valygar's armor to leather (rather than chain) and changes its colorization in order to match its inventory picture

COPY_EXISTING ~npchan.itm~ ~override~                                              // Valygar's armor
  WRITE_ASCII 0x22 ~2A~ #2                                                         // set the paperdoll animation ID to leather (2A)
    READ_LONG   0x64 "abil_off"
    READ_SHORT  0x68 "abil_num"
    READ_LONG   0x6a "fx_off"
    READ_SHORT  0x70 "fx_num"
    FOR (index2 = 0 ; index2 < fx_num ; index2 = index2 + 1) BEGIN
      READ_SHORT ("%fx_off%" +        ("%index2%" * 0x30)) "opcode"
      READ_SHORT ("%fx_off%" + 0x04 + ("%index2%" * 0x30)) "param1"
      READ_SHORT ("%fx_off%" + 0x08 + ("%index2%" * 0x30)) "param2"
      PATCH_IF (("%opcode%" = 7) AND  ("%param2%" = 5)) BEGIN                      // set character color, location - Armor (5)
      WRITE_SHORT ("%fx_off%" + 0x04 + ("%index2%" * 0x30)) "60"                   // Color gradient 60 (dark purple)
      END
      PATCH_IF (("%opcode%" = 7) AND ("%param2%" = 4)) BEGIN                       // set character color, location - Strap color (4)
      WRITE_SHORT ("%fx_off%" + 0x04 + ("%index2%" * 0x30)) "60"                   // color gradient 60 (dark purple)
      END
      PATCH_IF (("%opcode%" = 7) AND ("%param2%" = 0)) BEGIN                       // set character color, location - Belt Buckle (0)
      WRITE_SHORT ("%fx_off%" + 0x04 + ("%index2%" * 0x30)) "60"                   // color gradient 60 (dark purple)
      END
    END
BUT_ONLY_IF_IT_CHANGES



//////////////////////////////////////////////
// Change the appearance of the Robe of Vecna
//////////////////////////////////////////////

// This tweak changes the appearance of the Robe of Vecna and turns it into a fully hooded robe (i.e. like the Robe of the Good Archmagi)

BEGIN @302 DESIGNATED 302 // Change the appearance of the Robe of Vecna
GROUP @3 // Cosmetic tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required
REQUIRE_PREDICATE !GAME_IS ~iwd-in-bg2~ @92                                        // Do not install this component on IWD-in-BG2
REQUIRE_PREDICATE !GAME_IS ~tutu tutu_totsc~ @91                                   // Forbid installation on Tutu

COPY_EXISTING ~wa2robe.itm~ ~override~                                             // Robe of Vecna
  WRITE_ASCII 0x22 ~4W~ #2                                                         // set the paperdoll animation ID to hooded robe (4W)
BUT_ONLY_IF_IT_CHANGES



////////////////////////////////////////////////////////////
// Give Shambling Mounds their proper soundset (DEPRECATED)
////////////////////////////////////////////////////////////

BEGIN @303 DESIGNATED 303 // Give Shambling Mounds their proper soundset
GROUP @3 // Cosmetic tweaks
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required
DEPRECATED @93 // Note: now a part of Distinctive creature soundsets



//////////////////////////////////////////////////////////
// Give Mariliths their Icewind Dale soundset (DEPRECATED)
//////////////////////////////////////////////////////////

BEGIN @304 DESIGNATED 304 // Give Mariliths their Icewind Dale soundset
GROUP @3 // Cosmetic tweaks
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required
DEPRECATED @93 // Note: now a part of Distinctive creature soundsets



/////////////////////////////////
// Distinctive creature coloring
/////////////////////////////////

BEGIN @310 DESIGNATED 310 // Distinctive creature coloring
GROUP @3 // Cosmetic tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required


INCLUDE "atweaks/lib/c310.tpa"



//////////////////////////////////
// Distinctive creature soundsets
//////////////////////////////////

BEGIN @315 DESIGNATED 315 // Distinctive creature soundsets
GROUP @3 // Cosmetic tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required

INCLUDE "atweaks/lib/c315.tpa"



// ******************************************************* Miscellaneous tweaks ****************************************************************************
/////////////////////////////////////////////////////
// Slightly expanded storage capacity for containers
/////////////////////////////////////////////////////

BEGIN @501 DESIGNATED 500 // Use the recommended storage capacity value
SUBCOMPONENT @500  // Slightly expanded storage capacity for containers
GROUP @4 // Miscellaneous tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required

// This tweak raises the storage capacity of all containers in the game to 999, but only in case the container's capacity is currently lower than that

COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~                                  // parses through all stores in the game (even those added by mods)
PATCH_IF (%SOURCE_SIZE% > 0x9b) THEN BEGIN                                        // file size sanity check (filters out 0 byte files i.e. ALLOW_MISSING)
  READ_LONG 0x08 "itemtype" ELSE 0                                                // check store type
  PATCH_IF ("%itemtype%" = 5) BEGIN                                               // if the store is a container
    READ_SHORT 0x22 "capacity"                                                    // check the storage capacity
    PATCH_IF ("%capacity%" < 999) BEGIN                                           // if the storage capacity is lower than 999
      WRITE_SHORT 0x22 999                                                        // set the storage capacity to 999
    END
  END
END                                                                               // end file size sanity check
BUT_ONLY_IF_IT_CHANGES



BEGIN @502 DESIGNATED 502 // Manually enter the storage capacity value
SUBCOMPONENT @500  // Slightly expanded storage capacity for containers
GROUP @4 // Miscellaneous tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required

// This tweak raises the storage capacity of all containers in the game to the designated value

PRINT @503 // Please enter the storage capacity value (0-65535)
ACTION_READLN ~man_cap~

OUTER_WHILE NOT (IS_AN_INT %man_cap%) BEGIN
 PRINT @504 // The storage capacity value must be entered in the form of an integer (0-65535)
 ACTION_READLN ~man_cap~
END

COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~                                  // parses through all stores in the game (even those added by mods)
PATCH_IF (%SOURCE_SIZE% > 0x9b) THEN BEGIN                                        // file size sanity check (filters out 0 byte files i.e. ALLOW_MISSING)
  READ_LONG 0x08 "itemtype" ELSE 0                                                // check store type
  PATCH_IF ("%itemtype%" = 5) BEGIN                                               // if the store is a container
    READ_SHORT 0x22 "capacity"                                                    // check the storage capacity
    PATCH_IF ("%capacity%" < "%man_cap%") BEGIN                                   // if the storage capacity is lower than the designated value
      WRITE_SHORT 0x22 %man_cap%                                                  // set the storage capacity to the designated value
    END
  END
END                                                                               // end file size sanity check
BUT_ONLY_IF_IT_CHANGES
PRINT @505 // Container storage capacity value has been set to:
PRINT ~%man_cap%~



////////////////////////////
// Expanded temple services
////////////////////////////

BEGIN @510 DESIGNATED 510 // Expanded temple services
GROUP @4 // Miscellaneous tweaks
REQUIRE_PREDICATE ENGINE_IS ~bg2 tob~ @92                                          // SoA or ToB engine required


INCLUDE "atweaks/lib/c510.tpa"


/*
/////////////////////////////////////
//Blackrazor does not override haste
/////////////////////////////////////

BEGIN @520 DESIGNATED 520 // Blackrazor does not override haste
GROUP @4 // Miscellaneous tweaks
REQUIRE_PREDICATE ENGINE_IS ~tob~ @90                                              // ToB engine required

*/


///////////////////////////////////////
// BG2-style icons for aTweaks content
///////////////////////////////////////

BEGIN @520 DESIGNATED 999 // BG2-style icons for aTweaks content
GROUP @4 // Miscellaneous tweaks

COPY    ~atweaks/bam/bg2s~   ~override~




// *********************************** END OF FILE ***********************************************
